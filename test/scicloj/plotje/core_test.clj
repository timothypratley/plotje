(ns scicloj.plotje.core-test
  "Hand-written unit tests for plotje core logic."
  (:require [clojure.test :refer [deftest testing is are]]
            [clojure.string :as str]
            [tablecloth.api :as tc]
            [java-time.api :as jt]
            [scicloj.plotje.api :as pj]
            [scicloj.plotje.impl.defaults :as defaults]
            [scicloj.plotje.impl.stat :as stat]
            [scicloj.plotje.impl.scale :as scale]
            [scicloj.plotje.impl.position :as position]
            [scicloj.plotje.render.mark :as mark]
            [scicloj.plotje.impl.extract :as extract]
            [scicloj.plotje.impl.resolve :as resolve]
            [scicloj.plotje.layer-type :as layer-type]
            [scicloj.metamorph.ml.rdatasets :as rdatasets]))

;; ============================================================
;; defaults.clj
;; ============================================================

(deftest hex->rgba-test
  (testing "6-digit hex"
    (let [[r g b a] (defaults/hex->rgba "#FF0000")]
      (is (== 1.0 r))
      (is (== 0.0 g))
      (is (== 0.0 b))
      (is (== 1.0 a))))
  (testing "8-digit hex with alpha"
    (let [[_ _ _ a] (defaults/hex->rgba "#FF000080")]
      (is (< 0.3 a 0.6))))
  (testing "3-digit shorthand"
    (let [[r g b _] (defaults/hex->rgba "#F00")]
      (is (== 1.0 r))
      (is (== 0.0 g))))
  (testing "without #"
    (let [[r _ _ _] (defaults/hex->rgba "00FF00")]
      (is (== 0.0 r)))))

(deftest color-for-test
  (testing "index-based default palette"
    (let [cats ["a" "b" "c"]
          c1 (defaults/color-for cats "a")
          c2 (defaults/color-for cats "b")]
      (is (= 4 (count c1)))
      (is (not= c1 c2))))
  (testing "palette as map"
    (let [cats ["a" "b"]
          c (defaults/color-for cats "a" {"a" "#FF0000"})]
      (is (== 1.0 (first c)))))
  (testing "palette as vector"
    (let [cats ["x" "y"]
          c (defaults/color-for cats "x" ["#00FF00" "#0000FF"])]
      (is (== 0.0 (first c)))
      (is (== 1.0 (second c)))))
  (testing "wrap-around index"
    (let [cats (mapv str (range 20))
          c (defaults/color-for cats "0")
          c2 (defaults/color-for cats "15")]
      (is (= 4 (count c)))
      (is (= 4 (count c2))))))

(deftest fixed-color-survives-grouping-test
  (testing "a literal :color applies to every group made by :group"
    ;; Grouping fills each group's color slot with the group key. A fixed
    ;; color has to win over that, or "one line per series, all in one
    ;; color" cannot be said -- the pale background behind a few named
    ;; series in the Cookbook's annotation recipes.
    (let [grey [0.8 0.8 0.8 1.0]
          colors (fn [pose]
                   (->> (pj/plan pose) :panels first :layers first :groups
                        (mapv :color)))]
      (is (= [grey grey]
             (colors (pj/lay-line {:g ["a" "a" "b" "b"] :x [1 2 1 2] :y [1 2 2 3]}
                                  :x :y {:group :g :color "#cccccc"})))
          "grouping discarded the fixed color")
      (testing "and one group still gets it"
        (is (= [grey]
               (colors (pj/lay-line {:x [1 2] :y [1 2]} :x :y {:color "#cccccc"})))))
      (testing "while a :color column still drives the palette"
        (let [cs (colors (pj/lay-line {:g ["a" "a" "b" "b"] :x [1 2 1 2] :y [1 2 2 3]}
                                      :x :y {:color :g}))]
          (is (= 2 (count cs)))
          (is (apply not= cs)))))))

(deftest gradient-color-test
  (testing "t=0.0 (dark blue -- low end)"
    (let [[r g b a] (defaults/gradient-color 0.0)]
      (is (< r 0.1))
      (is (< b 0.3))
      (is (== 1.0 a))))
  (testing "t=1.0 (light blue -- high end)"
    (let [[r g b _] (defaults/gradient-color 1.0)]
      (is (> b 0.9))
      (is (> g 0.6))))
  (testing "t=0.5 (mid blue)"
    (let [[r g b _] (defaults/gradient-color 0.5)]
      (is (> b 0.5))
      (is (> g 0.3))
      (is (< r 0.3))))
  (testing "clamping"
    (is (= (defaults/gradient-color -1.0) (defaults/gradient-color 0.0)))
    (is (= (defaults/gradient-color 2.0) (defaults/gradient-color 1.0)))))

(deftest legend-serializable-test
  (testing "continuous legend has :color-range keyword, no :gradient-fn"
    (let [ds (tc/dataset {:x (range 50) :y (range 50) :c (range 50)})
          pl (pj/plan (-> ds (pj/lay-point :x :y {:color :c})))
          legend (:legend pl)]
      (is (= :continuous (:type legend)))
      (is (contains? legend :color-range))
      (is (not (contains? legend :gradient-fn)))
      (is (nil? (:color-range legend)) "default color range is nil")))
  (testing "explicit :color-range is stored as keyword"
    (let [ds (tc/dataset {:x (range 50) :y (range 50) :c (range 50)})
          pl (pj/plan (-> ds (pj/lay-point :x :y {:color :c}))
                      {:color-range :inferno})
          legend (:legend pl)]
      (is (= :inferno (:color-range legend)))
      (is (false? (:range-from-spec? legend)))))
  (testing "a range from a scale spec is marked as such, so render-time"
    ;; configuration does not repaint a legend the spec decided.
    (let [ds (tc/dataset {:x (range 50) :y (range 50) :c (range 50)})
          legend (-> ds
                     (pj/lay-point :x :y {:color :c})
                     (pj/scale :color {:range :inferno})
                     pj/plan :legend)]
      (is (= :inferno (:color-range legend)))
      (is (true? (:range-from-spec? legend)))))
  (testing "legend has 20 pre-computed stops"
    (let [ds (tc/dataset {:x (range 50) :y (range 50) :c (range 50)})
          pl (pj/plan (-> ds (pj/lay-point :x :y {:color :c})))
          legend (:legend pl)]
      (is (= 20 (count (:stops legend))))
      (is (== 0.0 (:t (first (:stops legend)))))
      (is (== 1.0 (:t (last (:stops legend))))))))

(deftest fmt-name-test
  (testing "keywords lose their separators"
    (is (= "sepal length" (defaults/fmt-name :sepal_length)))
    (is (= "sepal length" (defaults/fmt-name :sepal-length)))
    (is (= "x" (defaults/fmt-name :x))))
  (testing "symbols lose theirs too -- a symbol cannot hold a space either"
    (is (= "sepal length" (defaults/fmt-name 'sepal-length)))
    (is (= "sepal length" (defaults/fmt-name 'sepal_length))))
  (testing "a string is left as written -- a string could have held a space"
    (is (= "sepal_length" (defaults/fmt-name "sepal_length")))
    (is (= "Cost-Benefit Ratio" (defaults/fmt-name "Cost-Benefit Ratio"))))
  (testing "a name that is neither formats as its printed form"
    (is (= "0" (defaults/fmt-name 0)))
    (is (= "" (defaults/fmt-name nil)))))

(deftest fmt-category-label-test
  (testing "a keyword category reads as words, like a column name does"
    (is (= "setosa" (defaults/fmt-category-label :setosa)))
    (is (= "not applicable" (defaults/fmt-category-label :not-applicable)))
    (is (= "north east" (defaults/fmt-category-label :north_east))))
  (testing "a string category is left as written"
    (is (= "2026-07-31" (defaults/fmt-category-label "2026-07-31")))
    (is (= "Cost-Benefit Ratio" (defaults/fmt-category-label "Cost-Benefit Ratio"))))
  (testing "other values format as their printed form"
    (is (= "-5" (defaults/fmt-category-label -5)))
    (is (= "" (defaults/fmt-category-label nil)))))

(deftest categories-differing-only-by-separator-are-combined-with-a-warning-test
  ;; Two keyword categories that differ only by separator format alike. On a
  ;; categorical axis that is a data transform, not a relabelling, so they
  ;; land in one band -- rare enough to be worth a warning rather than a rule.
  (let [ds (tc/dataset {:cat [:a-b :a_b :a-b :a_b] :v [1 2 3 4]})
        ticks (atom nil)
        out (with-out-str
              (reset! ticks
                      (-> ds (pj/lay-bar :cat :v) pj/plan :panels first :x-ticks)))]
    (is (= ["a b"] (:values (deref ticks))))
    (is (= ["a b"] (:labels (deref ticks))))
    (is (re-find #"Warning: categories" out))
    (is (re-find #":a-b" out))
    (is (re-find #":a_b" out))
    (is (re-find #"Convert column :cat to strings" out)))

  (testing "a category on :color is only relabelled -- the groups survive"
    ;; The colour path does not map the column, so the two keep their own bars
    ;; and only the legend text repeats.
    (let [pl (-> {:x [1 2 3 4] :y [1 2 1 2] :c [:a-b :a_b :a-b :a_b]}
                 (pj/lay-bar :x :y {:color :c})
                 pj/plan)]
      (is (= ["a b" "a b"] (mapv :label (:entries (:legend pl)))))))

  (testing "no warning when the categories are distinguishable"
    (is (= "" (with-out-str
                (-> (tc/dataset {:cat [:a-b :c-d] :v [1 2]})
                    (pj/lay-bar :cat :v)
                    pj/plan))))))

(deftest integer-column-names-auto-label-test
  ;; A dataset built without column names gets integer ones; auto-labelling
  ;; used to throw on them (issue reported by Timothy Pratley, PR #32).
  (let [pl (-> (tc/dataset [[1 2] [3 4]]) pj/plan)]
    (is (= "0" (:x-label pl)))
    (is (= "1" (:y-label pl)))))

(deftest an-integer-column-name-reaches-the-error-message
  ;; `validate-numeric-column` formatted the column with `name`, which
  ;; throws on a Long, so the clear error it exists to give arrived as
  ;; `Long cannot be cast to clojure.lang.Named` -- naming neither the
  ;; column, the stat, nor the fix. Same shape as PR #32.
  (let [ds (tc/dataset [["a" 1.0] ["b" 2.0] ["c" 3.0]])]
    (doseq [[label pose-fn] [["histogram" #(pj/lay-histogram ds 0)]
                             ["density"   #(pj/lay-density ds 0)]
                             ["smooth"    #(pj/lay-smooth ds 0 1)]]]
      (is (thrown-with-msg?
           clojure.lang.ExceptionInfo #"requires a numeric column"
           (pj/plan (pose-fn)))
          label)))
  (testing "and the message names a way forward"
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo #"convert-types"
         (pj/plan (pj/lay-histogram (tc/dataset {:d ["2020-01-01" "2021-01-01"]}) :d))))))

(deftest the-conversion-a-message-names-parses-that-column
  ;; The message named `:local-date` for every column of text, and
  ;; `tc/convert-types` refuses that for a GitHub timestamp such as
  ;; 2015-03-21T10:00:00Z -- so the error that greeted Adrian Smith's
  ;; :created_at column told him to run a call that throws on the data
  ;; that produced it. The datatype is now measured on the column.
  (let [msg (fn [values]
              (try (pj/plan (pj/lay-histogram (tc/dataset {:c (vec values)}) :c))
                   nil
                   (catch clojure.lang.ExceptionInfo e (ex-message e))))
        suggests (fn [values] (some #(when (re-find (re-pattern (str "convert-types ds :c " %))
                                                    (msg values))
                                       %)
                                    [":local-date-time" ":local-date" ":instant"]))]
    (testing "each shape of text date is offered the datatype that reads it"
      (is (= ":local-date"      (suggests (repeat 4 "2024-03-01"))))
      (is (= ":local-date-time" (suggests (repeat 4 "2024-03-01T10:00:00"))))
      (is (= ":instant"         (suggests (repeat 4 "2024-03-01T10:00:00Z"))))
      (is (= ":instant"         (suggests (concat [nil] (repeat 4 "2024-03-01T10:00:00Z"))))
          "a leading missing value does not decide the datatype"))

    (testing "every one of those conversions actually parses its column"
      (doseq [text ["2024-03-01" "2024-03-01T10:00:00" "2024-03-01T10:00:00Z"]]
        (let [dt (keyword (subs (suggests (repeat 4 text)) 1))
              ds (tc/convert-types (tc/dataset {:c (vec (repeat 4 text))}) :c dt)]
          (is (= dt (:datatype (meta (:c ds)))) text))))

    (testing "a column that is not a date at all is offered no conversion"
      (let [m (msg ["ant" "bee" "cow"])]
        (is (re-find #"names groups instead" m))
        (is (not (re-find #"convert-types ds" m)))))))

(deftest a-column-with-no-values-is-not-called-numerical
  ;; An empty column and an all-missing one are both typed :boolean by
  ;; tech.ml.dataset, and `column-type` reads both as :numerical so the
  ;; pipeline has something to work with. Saying "is numerical" of
  ;; either sent the reader to check a column type that was not the
  ;; problem.
  (testing "an empty dataset"
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo #"has no rows"
         (pj/plan (pj/lay-summary {:c [] :y []} :c :y)))))
  (testing "a column of nothing but nils"
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo #"has no values"
         (pj/plan (pj/lay-lollipop {:c [nil nil nil] :y [1.0 2.0 3.0]} :c :y)))))
  (testing "a column that really is numerical still says so"
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo #"is numerical"
         (pj/plan (pj/lay-summary {:c [1.0 2.0] :y [1.0 2.0]} :c :y))))))

(deftest string-column-names-auto-label-test
  ;; A string name titles its axis as written. Substituting its separators
  ;; would corrupt a name that meant them, which keyword names cannot express.
  (let [pl (-> (tc/dataset {"sepal_length" [1 2 3] "Cost-Benefit Ratio" [4 5 6]})
               (pj/lay-point "sepal_length" "Cost-Benefit Ratio")
               pj/plan)]
    (is (= "sepal_length" (:x-label pl)))
    (is (= "Cost-Benefit Ratio" (:y-label pl)))))

;; ============================================================
;; stat.clj
;; ============================================================

(def tiny-ds (tc/dataset {:x [1 2 3 4 5] :y [2 4 6 8 10]}))
(def cat-ds (tc/dataset {:cat ["a" "a" "b" "b" "b"] :val [1 2 3 4 5]}))

(deftest compute-stat-identity-test
  (let [view {:mark :point :data tiny-ds :x :x :y :y :x-type :numerical :y-type :numerical}
        result (stat/compute-stat (assoc view :cfg defaults/defaults))]
    (is (seq (:points result)))
    (is (= 5 (count (:xs (first (:points result))))))))

(deftest compute-stat-bin-test
  (let [view {:mark :bar :stat :bin :data tiny-ds :x :x :x-type :numerical
              :cfg defaults/defaults}
        result (stat/compute-stat view)]
    (is (= 1 (count (:bins result))))
    (is (pos? (count (:bin-maps (first (:bins result))))))))

(deftest compute-stat-count-test
  (let [view {:mark :rect :stat :count :data cat-ds :x :cat :x-type :categorical
              :cfg defaults/defaults}
        result (stat/compute-stat view)]
    (is (= ["a" "b"] (vec (:categories result))))
    (is (seq (:bars result)))
    ;; counts is a vector of {:category "a" :count N} maps
    (let [counts (:counts (first (:bars result)))]
      (is (= 2 (:count (first (filter #(= "a" (:category %)) counts)))))
      (is (= 3 (:count (first (filter #(= "b" (:category %)) counts))))))))

(deftest compute-stat-lm-test
  (testing "perfect linear data y=2x"
    (let [view {:mark :line :stat :linear-model :data tiny-ds :x :x :y :y
                :x-type :numerical :cfg defaults/defaults}
          result (stat/compute-stat view)
          line (first (:lines result))]
      (is line)
      (is (< (:x1 line) (:x2 line)))
      (is (< (Math/abs (- (:y1 line) 2.0)) 0.01))
      (is (< (Math/abs (- (:y2 line) 10.0)) 0.01))))
  (testing "n=2 produces nil (needs >= 3)"
    (let [ds2 (tc/dataset {:x [1 2] :y [3 4]})
          view {:mark :line :stat :linear-model :data ds2 :x :x :y :y
                :x-type :numerical :cfg defaults/defaults}
          result (stat/compute-stat view)]
      (is (empty? (:lines result))))))

(deftest compute-stat-kde-test
  (let [view {:mark :area :stat :density :data tiny-ds :x :x :x-type :numerical
              :cfg defaults/defaults}
        result (stat/compute-stat view)]
    (is (seq (:points result)))
    (is (> (count (:xs (first (:points result)))) 10))))

(deftest compute-stat-boxplot-test
  (let [ds (tc/dataset {:cat ["a" "a" "a" "a" "a" "a"]
                        :val [1.0 2.0 3.0 4.0 5.0 100.0]})
        view {:mark :boxplot :stat :boxplot :data ds :x :cat :y :val
              :x-type :categorical :cfg defaults/defaults}
        result (stat/compute-stat view)
        box (first (:boxes result))]
    (is box)
    (is (= "a" (:category box)))
    (is (<= (:q1 box) (:median box) (:q3 box)))
    (is (seq (:outliers box)))))

(deftest compute-stat-summary-test
  (let [ds (tc/dataset {:cat ["a" "a" "a" "b" "b" "b"]
                        :val [10.0 20.0 30.0 40.0 50.0 60.0]})
        view {:mark :pointrange :stat :summary :data ds :x :cat :y :val
              :x-type :categorical :cfg defaults/defaults}
        result (stat/compute-stat view)]
    (is (seq (:points result)))
    (let [p (first (:points result))]
      (is (= 2 (count (:xs p))))
      ;; mean of [10 20 30] = 20, mean of [40 50 60] = 50
      (is (< (Math/abs (- (first (:ys p)) 20.0)) 0.01))
      (is (< (Math/abs (- (second (:ys p)) 50.0)) 0.01)))))

(deftest compute-stat-bin2d-test
  (let [ds (tc/dataset {:x (range 100) :y (range 100)})
        view {:mark :tile :stat :bin2d :data ds :x :x :y :y
              :x-type :numerical :y-type :numerical :cfg defaults/defaults}
        result (stat/compute-stat view)]
    (is (seq (:tiles result)))
    (is (= #{:x-lo :x-hi :y-lo :y-hi :fill} (set (tc/column-names (:tiles result)))))))

;; ============================================================
;; position.clj
;; ============================================================

(deftest apply-positions-identity-test
  (let [layers [{:mark :point :groups [{:xs [1 2] :ys [3 4]}]}]]
    (is (= layers (position/apply-positions layers)))))

(deftest apply-positions-dodge-test
  (let [layers [{:mark :rect :position :dodge
                 :categories ["a" "b"]
                 :groups [{:label "g1" :counts [{:category "a" :count 10} {:category "b" :count 20}]}
                          {:label "g2" :counts [{:category "a" :count 30} {:category "b" :count 40}]}]}]
        result (position/apply-positions layers)
        layer (first result)]
    (is (:dodge-ctx layer))
    (is (= 2 (:n-groups (:dodge-ctx layer))))
    (is (= 0 (:dodge-idx (first (:groups layer)))))
    (is (= 1 (:dodge-idx (second (:groups layer)))))))

(deftest a-step-layer-carries-its-position-test
  ;; The layer reached the position stage carrying no :position, was
  ;; grouped as :identity, and drew its raw values. Nothing said so --
  ;; three lines were drawn either way, which is what the chapter's
  ;; assertion checked.
  (let [d {:x [0 1 2 0 1 2] :y [1 2 3 10 10 10]
           :g ["A" "A" "A" "B" "B" "B"]}
        groups (-> d
                   (pj/lay-step :x :y {:position :stack :color :g})
                   pj/plan
                   :panels first :layers first :groups)]
    (is (= ["A" "B"] (mapv :label groups)))
    ;; B is laid down first and A sits on top of it.
    (is (= [0.0 0.0 0.0] (vec (:y0s (second groups)))))
    (is (= [10.0 10.0 10.0] (vec (:ys (second groups)))))
    (is (= [10.0 10.0 10.0] (vec (:y0s (first groups)))))
    (is (= [11.0 12.0 13.0] (vec (:ys (first groups)))))))

(deftest dodge-order-follows-the-settled-categories-test
  ;; The index used to follow whichever group the stat emitted first,
  ;; so a dodged bar could read in a different order from the legend
  ;; beside it, and a `:domain` moved the legend alone.
  (let [layers [{:mark :rect :position :dodge
                 :categories ["a"]
                 :groups [{:label "g2" :counts [{:category "a" :count 10}]}
                          {:label "g1" :counts [{:category "a" :count 20}]}]}]]
    (testing "with no ranking, the order is the one the layer arrived in"
      (let [gs (:groups (first (position/apply-positions layers)))]
        (is (= [0 1] (mapv :dodge-idx gs)))))

    (testing "a ranking places them, whatever order the stat emitted"
      (let [gs (:groups (first (position/apply-positions layers {"g1" 0 "g2" 1})))]
        (is (= [1 0] (mapv :dodge-idx gs)))))

    (testing "a label the ranking does not name goes last"
      (let [gs (:groups (first (position/apply-positions layers {"g2" 0})))]
        (is (= [0 1] (mapv :dodge-idx gs)))))))

(deftest boxes-and-violins-dodge-in-the-legend-order-test
  ;; A boxplot stores its groups under :boxes and a violin under
  ;; :violins, so the ordering step that sorts :groups never reaches
  ;; them. Their index comes from the same ranking instead.
  (let [boxes [{:mark :box :position :dodge
                :boxes [{:category "compact" :color-category "4"}
                        {:category "compact" :color-category "f"}]}]
        violins [{:mark :violin :position :dodge
                  :violins [{:category "compact" :color-category "4"}
                            {:category "compact" :color-category "f"}]}]
        rank {"f" 0 "4" 1}]
    (is (= [1 0] (mapv :dodge-idx (:boxes (first (position/apply-positions boxes rank))))))
    (is (= [1 0] (mapv :dodge-idx (:violins (first (position/apply-positions violins rank))))))))

(deftest band-position-counts-from-the-reader-s-first-end-test
  ;; An x band scale runs left to right and a y band scale places its
  ;; first category at the foot of the panel, so its bandwidth is
  ;; negative. Counting sub-bands from index zero in drawing units put
  ;; the first group at the bottom of a horizontal dodge while the
  ;; legend listed it at the top.
  (let [forward (scale/make-scale ["Mon" "Tue"] [0.0 300.0] {})
        inverted (scale/make-scale ["Mon" "Tue"] [300.0 0.0] {})
        mid (fn [s i] (:mid (mark/band-position s "Mon" i 3 0.9)))]
    (testing "on an x band scale the first group is leftmost"
      (is (< (mid forward 0) (mid forward 1) (mid forward 2))))

    (testing "on a y band scale the first group is topmost -- the least y"
      (is (< (mid inverted 0) (mid inverted 1) (mid inverted 2))))

    (testing "a lone group sits at the middle of its band on either scale"
      ;; Mon spans 0 to 150 on the forward scale and 300 to 150 on the
      ;; inverted one, so its middle is 75 and 225.
      (is (== 75.0 (:mid (mark/band-position forward "Mon" 0 1 0.9))))
      (is (== 225.0 (:mid (mark/band-position inverted "Mon" 0 1 0.9)))))))

(deftest apply-positions-stack-test
  (let [layers [{:mark :rect :position :stack
                 :categories ["a" "b"]
                 :groups [{:label "g1" :counts [{:category "a" :count 10} {:category "b" :count 20}]}
                          {:label "g2" :counts [{:category "a" :count 30} {:category "b" :count 40}]}]}]
        result (position/apply-positions layers)
        g1-counts (:counts (first (:groups (first result))))
        g2-counts (:counts (second (:groups (first result))))]
    ;; The last group is laid down first, so g2 starts at zero and g1
    ;; finishes on top -- the order the legend lists them in, and the
    ;; order ggplot2's position_stack draws them (measured, 4.0.0).
    (is (= 0.0 (:y0 (first g2-counts))))
    (is (= 30.0 (:y1 (first g2-counts))))
    (is (= 30.0 (:y0 (first g1-counts))))
    (is (= 40.0 (:y1 (first g1-counts))))
    ;; The groups come back in the order they arrived; only where each
    ;; one sits changed.
    (is (= ["g1" "g2"] (mapv :label (:groups (first result)))))))

(deftest apply-positions-fill-test
  (let [layers [{:mark :rect :position :fill
                 :categories ["a" "b"]
                 :groups [{:label "g1" :counts [{:category "a" :count 10} {:category "b" :count 20}]}
                          {:label "g2" :counts [{:category "a" :count 30} {:category "b" :count 40}]}]}]
        result (position/apply-positions layers)
        g1-counts (:counts (first (:groups (first result))))
        g2-counts (:counts (second (:groups (first result))))]
    ;; fill normalizes: cat "a" gives g1 10/(10+30)=0.25 and g2 the
    ;; remaining 0.75. g2 is laid down first, so it runs 0 to 0.75 and
    ;; g1 -- the first group, and the legend's first row -- tops it off.
    (is (< (Math/abs (- (:y0 (first g2-counts)) 0.0)) 0.01))
    (is (< (Math/abs (- (:y1 (first g2-counts)) 0.75)) 0.01))
    (is (< (Math/abs (- (:y0 (first g1-counts)) 0.75)) 0.01))
    (is (< (Math/abs (- (:y1 (first g1-counts)) 1.0)) 0.01))))

(deftest cross-layer-dodge-test
  (let [layers [{:mark :rect :position :dodge
                 :categories ["a" "b"]
                 :groups [{:label "g1" :counts [{:category "a" :count 10} {:category "b" :count 20}]}
                          {:label "g2" :counts [{:category "a" :count 30} {:category "b" :count 40}]}]}
                {:mark :errorbar :position :dodge
                 :groups [{:label "g1" :xs ["a" "b"] :ys [10 20]
                           :ymins [8 18] :ymaxs [12 22]}]}]
        result (position/apply-positions layers)]
    ;; Both layers should share same n-groups
    (is (= (:n-groups (:dodge-ctx (first result)))
           (:n-groups (:dodge-ctx (second result)))))))

(deftest count-stat-x-equals-color-test
  (testing "count stat when x and color map to the same column"
    (let [pl (-> {:species ["setosa" "setosa" "versicolor" "versicolor"]}
                 (pj/lay-bar :species {:color :species})
                 pj/plan)
          groups (get-in pl [:panels 0 :layers 0 :groups])]
      ;; Each group should only have non-zero count for its own species
      (doseq [g groups]
        (doseq [{:keys [category count]} (:counts g)]
          (if (= category (:label g))
            (is (pos? count) (str (:label g) " should have count for " category))
            (is (zero? count) (str (:label g) " should have 0 count for " category))))))))

(deftest stacked-bars-y0s-test
  (testing "stacked value bars use y0s baselines"
    (let [pl (-> {:day ["Mon" "Mon"] :count [30 20] :meal ["lunch" "dinner"]}
                 (pj/lay-bar :day :count {:color :meal :position :stack})
                 pj/plan)
          groups (get-in pl [:panels 0 :layers 0 :groups])
          lunch (first groups)
          dinner (second groups)]
      ;; The last group is laid down first, so dinner sits on the
      ;; baseline and lunch -- the legend's first row -- sits on top of
      ;; it, twenty units up.
      (is (= [0.0] (vec (:y0s dinner))))
      (is (= [20.0] (vec (:y0s lunch)))))))

;; ============================================================
;; scale.clj
;; ============================================================

(deftest make-scale-linear-test
  (let [s (scale/make-scale [0 100] [0 500] {})]
    (is (== 0 (s 0)))
    (is (== 500 (s 100)))
    (is (== 250 (s 50)))))

(deftest make-scale-categorical-test
  (let [s (scale/make-scale ["a" "b" "c"] [0 300] {})]
    (is (some? s))
    (is (pos? (wadogo.scale/data s :bandwidth)))))

(deftest make-scale-categorical-n-ticks-test
  (testing ":n-ticks thins a categorical band scale to roughly n evenly-spaced ticks"
    ;; The count is asked for at the tick call rather than built into
    ;; the scale, so one band scale answers both questions and the
    ;; numeric and categorical axes read `:n-ticks` the same way.
    (let [s (scale/make-scale (mapv str (range 50)) [0 300] {})]
      (is (= 50 (count (wadogo.scale/ticks s))))
      (is (= 10 (count (wadogo.scale/ticks s 10))))))

  (testing "and a tick count is what :n-ticks names, on either column type"
    (is (= 10 (scale/tick-count 600.0 {:n-ticks 10} 60)))
    (is (= 10 (scale/tick-count 600.0 {} 60)) "or how many fit at that spacing")
    (is (= 2 (scale/tick-count 10.0 {} 60)) "never fewer than two"))
  (testing ":n-ticks flows end-to-end through pj/scale onto a categorical x-axis"
    (let [d (tc/dataset {:x (mapv str (range 50)) :y (range 50)})
          labels (fn [pose] (-> pose pj/plan :panels first :x-ticks :labels))]
      (is (= 50 (count (labels (pj/lay-point d :x :y)))))
      (is (= 10 (count (labels (-> (pj/lay-point d :x :y)
                                   (pj/scale :x {:n-ticks 10}))))))))
  (testing ":n-ticks larger than the category count shows every category, no error"
    (let [d (tc/dataset {:x ["a" "b" "c"] :y [1 2 3]})
          labels (-> (pj/lay-point d :x :y)
                     (pj/scale :x {:n-ticks 10})
                     pj/plan :panels first :x-ticks :labels)]
      (is (= ["a" "b" "c"] (vec labels))))))

(deftest categorical-breaks-labels-test
  (let [xticks (fn [pose] (-> pose pj/plan :panels first :x-ticks))]
    (testing ":breaks selects a category subset and :tick-labels relabels it"
      (let [t (xticks (-> (tc/dataset {:x ["a" "m" "q" "z"] :y [1 2 3 4]})
                          (pj/lay-point :x :y)
                          (pj/scale :x {:breaks ["a" "z"] :tick-labels ["A" "Z"]})))]
        (is (= ["a" "z"] (vec (:values t))))
        (is (= ["A" "Z"] (vec (:labels t))))))
    (testing "a break naming no category is dropped (matched categories remain)"
      (let [t (xticks (-> (tc/dataset {:x (mapv str (range 50)) :y (range 50)})
                          (pj/lay-point :x :y)
                          (pj/scale :x {:breaks ["1" "25" "50"]})))]
        ;; "50" is not a category (domain is "0".."49") -- dropped.
        (is (= ["1" "25"] (vec (:values t))))))
    (testing "explicit :breaks win over :n-ticks (no thinning applied)"
      (let [t (xticks (-> (tc/dataset {:x (mapv str (range 50)) :y (range 50)})
                          (pj/lay-point :x :y)
                          (pj/scale :x {:n-ticks 5 :breaks ["3" "40"]})))]
        (is (= ["3" "40"] (vec (:values t))))))
    (testing "breaks match categories by displayed label (keyword column)"
      (let [t (xticks (-> (tc/dataset {:x [:widget :gadget :gizmo] :y [1 2 3]})
                          (pj/lay-point :x :y)
                          (pj/scale :x {:breaks ["widget" "gizmo"]})))]
        (is (= ["widget" "gizmo"] (mapv str (:labels t))))))))

(deftest make-scale-log-test
  (let [s (scale/make-scale [1 1000] [0 300] {:type :log})]
    (is (== 0 (s 1)))
    (is (== 300 (s 1000)))))

(deftest domain-padding-config-test
  (testing ":domain-padding is read through the config chain, not the defaults map"
    (let [pose (-> (rdatasets/datasets-iris)
                   (pj/lay-point :sepal-length :sepal-width))
          dom  (fn [p] (-> p pj/plan :panels first :x-domain))]
      ;; The data runs 4.3 to 7.9; the default 5% pads it to [4.12 8.08].
      (is (= [4.12 8.08] (mapv #(-> % (* 100) Math/round (/ 100.0)) (dom pose))))
      ;; No padding leaves the raw extent.
      (is (= [4.3 7.9] (dom (pj/options pose {:domain-padding 0.0}))))
      ;; A plot option and a thread-local binding both reach it.
      (is (= (dom (pj/options pose {:domain-padding 0.5}))
             (pj/with-config {:domain-padding 0.5} (dom pose))))
      (is (< (first (dom (pj/options pose {:domain-padding 0.5}))) 4.12)))))

(deftest pad-domain-test
  (testing "numeric padding"
    (let [[lo hi] (scale/pad-domain [0.0 10.0] {})]
      (is (< lo 0.0))
      (is (> hi 10.0))))
  (testing "log padding stays positive"
    (let [[lo hi] (scale/pad-domain [1.0 100.0] {:type :log})]
      (is (pos? lo))
      (is (> hi 100.0)))))

(deftest format-ticks-test
  (testing "integer ticks"
    (let [s (scale/make-scale [0 10] [0 500] {})
          labels (scale/format-ticks s [0.0 5.0 10.0])]
      (is (= ["0" "5" "10"] labels))))
  (testing "decimal ticks"
    (let [s (scale/make-scale [0 1] [0 500] {})
          labels (scale/format-ticks s [0.0 0.5 1.0])]
      (is (every? string? labels)))))

;; ============================================================
;; extract.clj
;; ============================================================

(deftest resolve-color-test
  ;; The third argument is the resolved draft layer, which answers both
  ;; "is a fixed color set" and "is this layer's color column drawn as
  ;; it stands" -- the two ways a color can arrive already decided.
  (let [cfg (assoc defaults/defaults :color-values nil)]
    (testing "column color"
      (let [c (extract/resolve-color ["a" "b"] "a" {} cfg)]
        (is (= 4 (count c)))))
    (testing "fixed hex color"
      (let [c (extract/resolve-color nil nil {:fixed-color "#FF0000"} cfg)]
        (is (== 1.0 (first c)))))
    (testing "fixed color named by a keyword"
      ;; Reaches here only since the data decides which values name
      ;; columns; before, a keyword was a column reference and nothing else.
      (let [c (extract/resolve-color nil nil {:fixed-color :red} cfg)]
        (is (= [1.0 0.0 0.0 1.0] c))))
    (testing "a drawn color column uses its own value, not the palette"
      (let [c (extract/resolve-color ["#FF0000" "#0000FF"] "#0000FF"
                                     {:color-drawn? true} cfg)]
        (is (= [0.0 0.0 1.0 1.0] c))))
    (testing "the same value scaled, for contrast"
      (let [c (extract/resolve-color ["#FF0000" "#0000FF"] "#0000FF" {} cfg)]
        (is (not= [0.0 0.0 1.0 1.0] c) "a palette entry, not the value itself")))
    (testing "nil falls to default"
      (let [c (extract/resolve-color nil nil {} cfg)]
        (is (= 4 (count c)))))))

(deftest extract-layer-point-test
  (let [view {:mark :point :data tiny-ds :x :x :y :y
              :x-type :numerical :y-type :numerical}
        rv (resolve/resolve-draft-layer view)
        stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
        layer (extract/extract-layer rv stat-result [] defaults/defaults)]
    (is (= :point (:mark layer)))
    (is (seq (:groups layer)))
    (is (= 5 (count (:xs (first (:groups layer))))))))

(deftest extract-layer-bar-test
  (let [view {:mark :bar :stat :bin :data tiny-ds :x :x
              :x-type :numerical}
        rv (resolve/resolve-draft-layer view)
        stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
        layer (extract/extract-layer rv stat-result [] defaults/defaults)]
    (is (= :bar (:mark layer)))
    (is (seq (:groups layer)))
    (is (seq (:bars (first (:groups layer)))))))

(deftest apply-shift-test
  (testing ":dx shifts xs"
    (let [view {:mark :point :data (tc/dataset {:x [1.0 2.0] :y [3.0 4.0]})
                :x :x :y :y :x-type :numerical :dx 0.5}
          rv (resolve/resolve-draft-layer view)
          stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
          layer (extract/extract-layer rv stat-result [] defaults/defaults)]
      (is (= [1.5 2.5] (:xs (first (:groups layer)))))))
  (testing "no shift is no-op"
    (let [view {:mark :point :data tiny-ds :x :x :y :y :x-type :numerical}
          rv (resolve/resolve-draft-layer view)
          stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
          layer (extract/extract-layer rv stat-result [] defaults/defaults)]
      (is (= [1 2 3 4 5] (:xs (first (:groups layer)))))))
  (testing ":dx on a categorical x axis is deferred to the layer, not folded into xs"
    (let [view {:mark :text :data (tc/dataset {:cat ["a" "b"] :v [1.0 2.0]})
                :x :cat :y :v :text :v :x-type :categorical :y-type :numerical
                :dx 0.5}
          rv (resolve/resolve-draft-layer view)
          stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
          layer (extract/extract-layer rv stat-result [] defaults/defaults)]
      (is (= 0.5 (:dx layer)))
      (is (= ["a" "b"] (:xs (first (:groups layer)))))))
  (testing ":dy on a categorical y axis is deferred to the layer, not folded into ys"
    (let [view {:mark :text :data (tc/dataset {:v [1.0 2.0] :cat ["a" "b"]})
                :x :v :y :cat :text :v :x-type :numerical :y-type :categorical
                :dy 0.5}
          rv (resolve/resolve-draft-layer view)
          stat-result (stat/compute-stat (assoc rv :cfg defaults/defaults))
          layer (extract/extract-layer rv stat-result [] defaults/defaults)]
      (is (= 0.5 (:dy layer)))
      (is (= ["a" "b"] (:ys (first (:groups layer))))))))

;; ============================================================
;; layer_type.clj -- mark constructors
;; ============================================================

(deftest mark-constructors-test
  (testing "registry entries have the correct :mark key"
    (are [k mk] (= mk (:mark (layer-type/lookup k)))
      :point :point
      :line :line
      :step :step
      :histogram :bar
      :bar :rect
      :smooth :line
      :text :text
      :label :text
      :area :area
      :density :area
      :tile :tile
      :density-2d :tile
      :contour :contour
      :ridgeline :ridgeline
      :boxplot :boxplot
      :violin :violin
      :rug :rug
      :summary :pointrange
      :errorbar :errorbar
      :lollipop :lollipop)))

;; ============================================================
;; draft->plan (integration)
;; ============================================================

(deftest views-to-plan-test
  (let [views (-> tiny-ds
                  (pj/pose :x :y)
                  pj/lay-point)
        pl (pj/plan views)]
    (is (map? pl))
    (is (contains? pl :panels))
    (is (contains? pl :width))
    (is (contains? pl :height))
    (is (= 1 (count (:panels pl))))
    (let [panel (first (:panels pl))]
      (is (seq (:layers panel)))
      (is (contains? panel :x-domain))
      (is (contains? panel :y-domain)))))

(deftest plan-with-color-test
  (let [ds (tc/dataset {:x [1 2 3 4] :y [1 2 3 4] :g ["a" "a" "b" "b"]})
        views (-> ds (pj/pose :x :y) (pj/lay-point {:color :g}))
        pl (pj/plan views)]
    (is (:legend pl))
    (is (= 2 (count (:entries (:legend pl)))))))

(deftest plan-faceted-test
  (let [ds (tc/dataset {:x [1 2 3 4 5 6] :y [1 2 3 4 5 6]
                        :g ["a" "a" "b" "b" "c" "c"]})
        views (-> ds (pj/pose :x :y) (pj/facet :g) pj/lay-point)
        pl (pj/plan views)]
    (is (= 3 (count (:panels pl))))))

(deftest coord-fixed-test
  (testing "coord :fixed end-to-end -- equal ranges produce square panel"
    (let [ds (tc/dataset {:x [0 10 5] :y [0 10 5]})
          pl (-> ds (pj/pose :x :y) (pj/coord :fixed) pj/lay-point pj/plan)]
      (is (== (:panel-width pl) (:panel-height pl)) "Equal data ranges -> square panel")))
  (testing "coord :fixed end-to-end -- asymmetric ranges"
    (let [ds (tc/dataset {:x [0 100 50] :y [0 10 5]})
          pl (-> ds (pj/pose :x :y) (pj/coord :fixed) pj/lay-point pj/plan)]
      (is (> (:panel-width pl) (:panel-height pl)) "Wide data -> wider panel"))))

(deftest diverging-color-test
  (testing "diverging-color endpoints"
    (let [[r g b _] (defaults/diverging-color 0.0)]
      (is (> r g) "t=0 red > green")
      (is (> r b) "t=0 red > blue"))
    (let [[r g b _] (defaults/diverging-color 1.0)]
      (is (> b r) "t=1 blue > red")
      (is (> b g) "t=1 blue > green"))
    (let [[r g b _] (defaults/diverging-color 0.5)]
      (is (> r 0.9) "t=0.5 is whitish (r)")
      (is (> g 0.9) "t=0.5 is whitish (g)")
      (is (> b 0.9) "t=0.5 is whitish (b)")))
  (testing "normalize-midpoint"
    (is (== 0.0 (defaults/normalize-midpoint -5 -5 5 0)))
    (is (== 0.5 (defaults/normalize-midpoint 0 -5 5 0)))
    (is (== 1.0 (defaults/normalize-midpoint 5 -5 5 0)))
    (is (== 0.25 (defaults/normalize-midpoint -2.5 -5 5 0)))
    (is (== 0.75 (defaults/normalize-midpoint 2.5 -5 5 0)))
    (is (== 0.5 (defaults/normalize-midpoint 5 0 10 nil))))
  (testing "resolve-gradient-fn"
    (is (fn? (defaults/resolve-gradient-fn nil)))
    (is (fn? (defaults/resolve-gradient-fn :diverging)))
    (is (fn? (defaults/resolve-gradient-fn :inferno)))
    (is (fn? (defaults/resolve-gradient-fn {:low "#FF0000" :mid "#FFFFFF" :high "#0000FF"}))))
  (testing "a scale spec is not a gradient"
    ;; A colour scale's `:range` holds a gradient and nothing else. A
    ;; whole spec written there is a mistake, and drawing it as three
    ;; default stops would change a plot's colours without saying so.
    (is (not (defaults/gradient-map? {:type :log})))
    (is (defaults/gradient-map? {:low "#000000" :high "#FFFFFF"}))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"names at least one of :low"
                          (defaults/resolve-gradient-fn {:type :log})))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"names at least one of :low"
                          (-> (tc/dataset {:x [1 2] :y [1 2] :c [1.0 2.0]})
                              (pj/lay-point :x :y {:color :c})
                              (pj/scale :color {:range {:type :log}})))))
  (testing "a log color scale keeps the default gradient"
    (let [ds (tc/dataset {:x (range 50) :y (range 50) :c (range 1 51)})
          stops (fn [pose] (->> pose pj/plan :legend :stops (mapv :color)))
          base (-> ds (pj/lay-point :x :y {:color :c}))]
      (is (= (stops base) (stops (pj/scale base :color :log)))
          "the spacing is what :log asks for, not the colors")
      (is (= :log (:scale-type (:legend (pj/plan (pj/scale base :color :log)))))
          "and the legend says which scale it explains")
      (is (seq (:ticks (:legend (pj/plan (pj/scale base :color :log)))))
          "with ticks, so the gradient bar can be read back to a value")
      (is (not= (stops base) (stops (pj/options base {:color-range :inferno})))
          "a gradient name still changes the gradient")
      (is (= (stops (pj/options base {:color-range :inferno}))
             (stops (pj/scale base :color {:range :inferno})))
          "and the spec spelling gives the same gradient as the option")))
  (testing "diverging end-to-end"
    (let [ds (tc/dataset {:x (range 10) :y (range 10) :z (map #(- % 5) (range 10))})
          fig (-> ds (pj/pose :x :y)
                  (pj/lay-point {:color :z})
                  (pj/plot {:color-range :diverging :color-midpoint 0}))
          s (pj/svg-summary fig)]
      (is (= 10 (:points s))))))

(deftest loess-se-test
  (testing "LOESS with SE produces ribbon"
    (let [ds (tc/dataset {:x (range 20) :y (map #(+ (* 0.1 % %) (Math/sin %)) (range 20))})
          fig (-> ds (pj/pose :x :y)
                  pj/lay-point
                  (pj/lay-smooth {:confidence-band true :bootstrap-resamples 50})
                  pj/plot)
          s (pj/svg-summary fig)]
      (is (= 20 (:points s)))
      (is (= 1 (:lines s)))
      (is (= 1 (:polygons s)) "confidence ribbon polygon")))
  (testing "LOESS without SE has no ribbon"
    (let [ds (tc/dataset {:x (range 20) :y (map #(+ (* 0.1 % %) (Math/sin %)) (range 20))})
          fig (-> ds (pj/pose :x :y)
                  pj/lay-point
                  pj/lay-smooth
                  pj/plot)
          s (pj/svg-summary fig)]
      (is (= 1 (:lines s)))
      (is (zero? (:polygons s)))))
  (testing "LOESS dedup handles duplicate x values"
    (let [ds (tc/dataset {:x [1 1 2 2 3 3 4 4 5 5] :y [2 3 4 5 6 7 8 9 10 11]})
          fig (-> ds (pj/pose :x :y) pj/lay-smooth pj/plot)
          s (pj/svg-summary fig)]
      (is (= 1 (:lines s))))))

(deftest arrange-test
  (testing "flat sketches -> composite pose of leaves"
    (let [sk1 (-> tiny-ds (pj/pose :x :y) pj/lay-point)
          sk2 (-> tiny-ds (pj/pose :x :y) pj/lay-point)
          result (pj/arrange [sk1 sk2])]
      (is (pj/pose? result))
      (is (= 1 (count (:poses result))) "one row with both leaves")
      (is (= 2 (count (:poses (first (:poses result))))))))
  (testing "nested rows -> outer vertical of rows"
    (let [sk1 (-> tiny-ds (pj/pose :x :y) pj/lay-point)
          result (pj/arrange [[sk1 sk1] [sk1 sk1]])]
      (is (pj/pose? result))
      (is (= 2 (count (:poses result))) "two rows")
      (is (every? #(= 2 (count (:poses %))) (:poses result))
          "each row has two leaves")))
  (testing "title flows through to composite opts"
    (let [sk1 (-> tiny-ds (pj/pose :x :y) pj/lay-point)
          result (pj/arrange [sk1 sk1] {:title "Test" :cols 2})]
      (is (= "Test" (-> result :opts :title)))
      (let [plotted (pj/plot result)]
        (is (= :svg (first plotted)))
        (is (some #(= "Test" %) (:texts (pj/svg-summary plotted)))
            "title text appears in rendered svg"))))
  (testing "hiccup input is rejected with a clear error"
    (let [pre-rendered (-> tiny-ds (pj/pose :x :y) pj/lay-point pj/plot)]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"looks like rendered hiccup"
                            (pj/arrange [pre-rendered pre-rendered]))))))

(deftest valid-plan-test
  (let [views (-> tiny-ds (pj/pose :x :y) pj/lay-point)
        pl (pj/plan views)]
    (is (pj/valid-plan? pl))))

;; ============================================================
;; Configuration System
;; ============================================================

(deftest config-returns-defaults-test
  (testing "config returns a map with all expected keys"
    (let [cfg (defaults/config)]
      (is (map? cfg))
      (is (= 600 (:width cfg)))
      (is (= 400 (:height cfg)))
      (is (= 10 (:margin cfg)))
      (is (= 3.0 (:point-radius cfg)))
      (is (= 0.75 (:point-opacity cfg)))
      (is (= 0.85 (:bar-opacity cfg)))
      (is (= 2.5 (:line-width cfg)))
      (is (= 0.6 (:grid-stroke-width cfg)))
      (is (string? (:rule-color cfg)))
      (is (= 0.15 (:band-opacity cfg)))
      (is (= 60 (:x-tick-spacing cfg)))
      (is (= 40 (:y-tick-spacing cfg)))
      (is (= :sturges (:bin-method cfg)))
      (is (= 0.05 (:domain-padding cfg)))
      (is (= 13 (:label-font-size cfg)))
      (is (= 15 (:title-font-size cfg)))
      (is (= 11 (:strip-font-size cfg)))
      (is (= 38 (:label-offset cfg)))
      (is (= 18 (:title-offset cfg)))
      (is (= 16 (:strip-height cfg)))
      (is (true? (:validate cfg)))))
  (testing "config includes theme nested map"
    (let [theme (:theme (defaults/config))]
      (is (map? theme))
      (is (string? (:bg theme)))
      (is (string? (:grid theme)))
      (is (number? (:font-size theme))))))

(deftest the-configuration-sizes-a-composite-test
  ;; Issue #56: a composite with no :width/:height of its own drew at a
  ;; 600 by 400 written in the compositor, ignoring set-config! and
  ;; with-config; and pj/arrange copied the configured size into the
  ;; pose when it was called, so a set-config! after it did not apply.
  (let [iris (rdatasets/datasets-iris)
        cols [:sepal-length :sepal-width]
        size (fn [pose] (let [s (pj/svg-summary pose)] [(:width s) (:height s)]))
        matrix (fn [] (pj/cross-matrix iris cols))
        arranged (pj/arrange [(pj/lay-point iris :sepal-length :sepal-width)
                              (pj/lay-point iris :petal-length :petal-width)])]
    (try
      (defaults/set-config! {:width 1024 :height 1024})
      (testing "set-config! sizes a scatterplot matrix"
        (is (= [1024 1024] (size (matrix)))))
      (testing "and an arrangement built before it"
        (is (= [1024 1024] (size arranged))))
      (testing "a size written on the composite still wins"
        (is (= [500 300] (size (pj/options (matrix) {:width 500 :height 300}))))
        (is (= [500 300] (size (pj/arrange [(pj/lay-point iris :sepal-length :sepal-width)]
                                           {:width 500 :height 300})))))
      (finally
        (defaults/set-config! nil)))
    (testing "with-config sizes a scatterplot matrix"
      (is (= [900 700] (pj/with-config {:width 900 :height 700} (size (matrix))))))
    (testing "the configuration's own default is unchanged"
      (is (= [600 400] (size (matrix)))))))

(deftest set-config!-test
  (testing "set-config! overrides specific keys"
    (try
      (defaults/set-config! {:width 800})
      (is (= 800 (:width (defaults/config))))
      ;; Other keys remain at defaults
      (is (= 400 (:height (defaults/config))))
      (finally
        (defaults/set-config! nil))))
  (testing "set-config! nil resets to defaults"
    (try
      (defaults/set-config! {:width 999})
      (is (= 999 (:width (defaults/config))))
      (defaults/set-config! nil)
      (is (= 600 (:width (defaults/config))))
      (finally
        (defaults/set-config! nil))))
  (testing "set-config! overrides theme with merge at top level"
    (try
      (defaults/set-config! {:theme {:bg "#FFFFFF" :grid "#EEEEEE" :font-size 12}})
      (let [theme (:theme (defaults/config))]
        (is (= "#FFFFFF" (:bg theme)))
        (is (= "#EEEEEE" (:grid theme)))
        (is (= 12 (:font-size theme))))
      (finally
        (defaults/set-config! nil)))))

(deftest deep-merge-config-test
  (testing "set-config! partial theme deep-merges, preserving other theme keys"
    (try
      (defaults/set-config! {:theme {:bg "#000"}})
      (let [theme (:theme (defaults/config))]
        (is (= "#000" (:bg theme)))
        ;; grid and font-size preserved from library defaults
        (is (string? (:grid theme)) "grid should be preserved")
        (is (number? (:font-size theme)) "font-size should be preserved"))
      (finally
        (defaults/set-config! nil))))
  (testing "with-config partial theme deep-merges"
    (pj/with-config {:theme {:bg "#111"}}
      (let [theme (:theme (defaults/config))]
        (is (= "#111" (:bg theme)))
        (is (string? (:grid theme)) "grid should be preserved")
        (is (number? (:font-size theme)) "font-size should be preserved"))))
  (testing "partial theme via with-config renders without error"
    (pj/with-config {:theme {:bg "#222"}}
      (let [svg (-> {:x [1 2 3] :y [4 5 6]}
                    (pj/lay-point :x :y)
                    pj/plot)]
        (is (vector? svg)))))
  (testing "pj/options deep-merges theme across calls"
    (let [sketch (-> {:x [1 2 3] :y [4 5 6]}
                     (pj/lay-point :x :y)
                     (pj/options {:theme {:bg "#FFF"} :width 800})
                     (pj/options {:theme {:font-size 14}}))]
      (is (= "#FFF" (get-in (:opts sketch) [:theme :bg])))
      (is (= 14 (get-in (:opts sketch) [:theme :font-size])))
      (is (= 800 (:width (:opts sketch)))))))

(deftest dynamic-binding-test
  (testing "binding *config* overrides set-config!"
    (try
      (defaults/set-config! {:width 800})
      (binding [defaults/*config* {:width 1200}]
        (is (= 1200 (:width (defaults/config)))))
      ;; Outside binding, set-config! still applies
      (is (= 800 (:width (defaults/config))))
      (finally
        (defaults/set-config! nil))))
  (testing "binding *config* overrides defaults"
    (binding [defaults/*config* {:point-radius 5.0}]
      (is (= 5.0 (:point-radius (defaults/config)))))
    ;; Outside binding, back to defaults
    (is (= 3.0 (:point-radius (defaults/config))))))

(deftest resolve-config-test
  (testing "per-call opts override everything"
    (try
      (defaults/set-config! {:width 800})
      (binding [defaults/*config* {:width 1200}]
        (let [cfg (defaults/resolve-config {:width 900})]
          (is (= 900 (:width cfg)))))
      (finally
        (defaults/set-config! nil))))
  (testing "per-call opts with no overrides returns config"
    (let [cfg (defaults/resolve-config {})]
      (is (= 600 (:width cfg)))))
  (testing "per-call theme merges into default theme"
    (let [cfg (defaults/resolve-config {:theme {:bg "#FFF"}})]
      (is (= "#FFF" (get-in cfg [:theme :bg])))
      ;; grid and font-size preserved from defaults
      (is (= "#F5F5F5" (get-in cfg [:theme :grid])))
      (is (= 11 (get-in cfg [:theme :font-size])))))
  (testing "per-call palette"
    (let [cfg (defaults/resolve-config {:color-values :dark2})]
      (is (= :dark2 (:color-values cfg)))))
  (testing "per-call color-scale"
    (let [cfg (defaults/resolve-config {:color-range :diverging})]
      (is (= :diverging (:color-range cfg)))))
  (testing "per-call validate false"
    (let [cfg (defaults/resolve-config {:validate false})]
      (is (false? (:validate cfg))))))

(deftest precedence-chain-test
  (testing "full precedence: per-call > binding > set-config! > defaults"
    (try
      (defaults/set-config! {:width 800 :height 300})
      (binding [defaults/*config* {:width 1200 :height 500}]
        ;; per-call wins for width; binding wins for height
        (let [cfg (defaults/resolve-config {:width 900})]
          (is (= 900 (:width cfg)))
          (is (= 500 (:height cfg)))
          ;; margin untouched by any override -> from defaults
          (is (= 10 (:margin cfg)))))
      (finally
        (defaults/set-config! nil)))))

;; ---- Public API config functions ----

(deftest api-config-test
  (testing "pj/config returns resolved config"
    (let [cfg (pj/config)]
      (is (map? cfg))
      (is (= 600 (:width cfg)))))
  (testing "pj/set-config! and reset"
    (try
      (pj/set-config! {:width 777})
      (is (= 777 (:width (pj/config))))
      (pj/set-config! nil)
      (is (= 600 (:width (pj/config))))
      (finally
        (pj/set-config! nil))))
  (testing "pj/with-config overrides for body"
    (pj/with-config {:width 1234}
      (is (= 1234 (:width (pj/config)))))
    (is (= 600 (:width (pj/config))))))

;; ---- Config affects plan output ----

(deftest config-affects-plan-test
  (let [views (-> tiny-ds (pj/pose :x :y) pj/lay-point)]
    (testing "default width/height in plan"
      (let [s (pj/plan views)]
        (is (= 600 (:width s)))
        (is (= 400 (:height s)))))
    (testing "per-call opts change plan dimensions"
      (let [s (pj/plan views {:width 800 :height 300})]
        (is (= 800 (:width s)))
        (is (= 300 (:height s)))))
    (testing "set-config! changes plan dimensions"
      (try
        (pj/set-config! {:width 700})
        (let [s (pj/plan views)]
          (is (= 700 (:width s))))
        (finally
          (pj/set-config! nil))))
    (testing "with-config changes plan dimensions"
      (pj/with-config {:height 500}
        (let [s (pj/plan views)]
          (is (= 500 (:height s)))))
      ;; After with-config, back to default
      (let [s (pj/plan views)]
        (is (= 400 (:height s)))))
    (testing "plan does NOT contain :theme key"
      (let [s (pj/plan views)]
        (is (not (contains? s :theme)))))))

;; ---- Config affects rendered SVG ----

(deftest config-affects-render-test
  (let [views (-> tiny-ds (pj/pose :x :y) pj/lay-point)]
    (testing "default theme bg appears in SVG"
      (let [svg (pj/plot views)
            summary (pj/svg-summary svg)]
        (is (= 1 (:panels summary)))
        (is (= 5 (:points summary)))))
    (testing "per-call theme overrides bg in SVG"
      (let [svg (pj/plot views {:theme {:bg "#FFFFFF" :grid "#EEEEEE" :font-size 8}})
            s (str svg)]
        ;; Default bg is rgb(235,235,235); custom is rgb(255,255,255)
        (is (clojure.string/includes? s "rgb(255,255,255)"))))
    (testing "with-config theme overrides bg in SVG"
      (let [svg (pj/with-config {:theme {:bg "#FF0000" :grid "#FFFFFF" :font-size 8}}
                  (pj/plot views))
            s (str svg)]
        (is (clojure.string/includes? s "rgb(255,0,0)"))))
    (testing "per-call width changes SVG viewBox"
      ;; Under the total-dimensions semantics, :width is the TOTAL
      ;; SVG width; the panel is derived by subtracting overhead.
      ;; So the output viewBox width equals :width exactly.
      (let [svg (pj/plot views {:width 800})
            attrs (second svg)]
        (is (= 800.0 (double (:width attrs))))))))

;; ---- Config with palette ----

(defn- group-colors-from-plan [pl]
  ;; Each panel's first layer has :groups; each group has :color [r g b a].
  (->> (:panels pl)
       (mapcat :layers)
       (mapcat :groups)
       (keep :color)
       (mapv vec)
       distinct
       set))

(deftest config-palette-test
  (let [ds (tc/dataset {:x [1 2 3 4 5 6]
                        :y [10 20 30 15 25 35]
                        :g ["a" "a" "a" "b" "b" "b"]})
        views (-> ds (pj/pose :x :y) (pj/lay-point {:color :g}))]
    (testing "default palette assigns distinct colors per category"
      (let [colors (group-colors-from-plan (pj/plan views))]
        (is (= 2 (count colors))
            "two categories, two colors")))
    (testing "per-call :color-values :dark2 produces colors that differ from default"
      (let [default-colors (group-colors-from-plan (pj/plan views))
            dark2-colors (group-colors-from-plan (pj/plan views {:color-values :dark2}))]
        (is (= 2 (count dark2-colors)))
        (is (not= default-colors dark2-colors)
            "palette change must actually change the rendered colors")
        (is (= dark2-colors
               (group-colors-from-plan
                (pj/plan (pj/scale views :color {:values :dark2}))))
            "and the spec spelling gives the same colours as the option")))
    (testing "set-config! palette flows through"
      (try
        (let [default-colors (group-colors-from-plan (pj/plan views))]
          (pj/set-config! {:color-values :set2})
          (let [set2-colors (group-colors-from-plan (pj/plan views))]
            (is (= 2 (count set2-colors)))
            (is (not= default-colors set2-colors)
                ":set2 palette must produce colors distinct from default")))
        (finally
          (pj/set-config! nil))))))

;; ---- Config validation flag ----

(deftest config-validate-flag-test
  (let [views (-> tiny-ds (pj/pose :x :y) pj/lay-point)]
    (testing "validate true (default) -- valid plan passes"
      (is (some? (pj/plan views))))
    (testing "validate false skips schema check"
      (is (some? (pj/plan views {:validate false}))))
    (testing "validate true throws when the plan fails schema"
      ;; Force the schema check to fail by stubbing the explain
      ;; function. The flag must actually drive whether the throw
      ;; happens; the previous test only covered the happy path
      ;; on both branches, so the flag could have been ignored.
      (with-redefs [scicloj.plotje.impl.plan-schema/explain
                    (fn [_] {:errors [:fake]})]
        (is (thrown-with-msg? clojure.lang.ExceptionInfo
                              #"does not conform to schema"
                              (pj/plan views))
            ":validate true must throw on schema failure")
        (is (some? (pj/plan views {:validate false}))
            ":validate false must skip the throw")))))

;; ---- Edge case tests ----

(deftest single-point-dataset-test
  (testing "plan with a single data point does not throw"
    (let [ds (tc/dataset {:x [5] :y [10]})
          pl (pj/plan (-> ds (pj/lay-point :x :y)))]
      (is (= 1 (count (:panels pl))))
      (is (some? (pj/plot (-> ds (pj/lay-point :x :y))))))))

(deftest two-point-dataset-test
  (testing "regression with exactly 2 points -- lm needs n>=3 so falls back gracefully"
    (let [ds (tc/dataset {:x [1 2] :y [3 4]})
          views (-> ds (pj/pose :x :y) pj/lay-point)]
      (is (some? (pj/plan views))))))

(deftest all-same-values-test
  (testing "scatter where all x values are identical"
    (let [ds (tc/dataset {:x [5 5 5 5] :y [1 2 3 4]})
          pl (pj/plan (-> ds (pj/lay-point :x :y)))]
      (is (some? pl))
      (is (= 1 (count (:panels pl))))))
  (testing "scatter where all y values are identical"
    (let [ds (tc/dataset {:x [1 2 3 4] :y [5 5 5 5]})
          pl (pj/plan (-> ds (pj/lay-point :x :y)))]
      (is (some? pl)))))

(deftest categorical-single-category-test
  (testing "bar chart with only one category"
    (let [ds (tc/dataset {:cat ["a" "a" "a"] :val [1 2 3]})
          pl (pj/plan (-> ds (pj/lay-bar :cat :val)))]
      (is (= 1 (count (:panels pl)))))))

(deftest histogram-uniform-data-test
  (testing "histogram with all identical values"
    (let [ds (tc/dataset {:x [5 5 5 5 5]})
          pl (pj/plan (-> ds (pj/lay-histogram :x)))]
      (is (some? pl)))))

(deftest polar-coord-test
  (testing "polar coordinate plan structure"
    (let [ds (tc/dataset {:cat ["A" "B" "C"] :val [10 20 30]})
          views (-> ds
                    (pj/pose :cat :val)
                    pj/lay-bar
                    (pj/coord :polar))
          pl (pj/plan views)]
      (is (= :polar (get-in pl [:panels 0 :coord]))))))

(deftest flip-coord-test
  (testing "flipped coordinates swap x/y domains"
    (let [views (-> cat-ds
                    (pj/pose :cat :val)
                    pj/lay-bar
                    (pj/coord :flip))
          pl (pj/plan views)
          panel (first (:panels pl))]
      (is (= :flip (:coord panel))))))

(deftest labs-test
  (testing "axis labels propagate to plan via options"
    (let [pl (-> tiny-ds
                 (pj/pose :x :y)
                 pj/lay-point
                 (pj/options {:x-label "X Axis" :y-label "Y Axis"})
                 pj/plan)]
      (is (= "X Axis" (:x-label pl)))
      (is (= "Y Axis" (:y-label pl)))))
  (testing "title/subtitle/caption propagate via options"
    (let [pl (-> tiny-ds
                 (pj/pose :x :y)
                 pj/lay-point
                 (pj/options {:title "T" :subtitle "ST" :caption "C"})
                 pj/plan)]
      (is (= "T" (:title pl)))
      (is (= "ST" (:subtitle pl)))
      (is (= "C" (:caption pl))))))

(deftest log-scale-test
  (testing "log scale is recorded in plan"
    (let [ds (tc/dataset {:x [1 10 100 1000] :y [1 2 3 4]})
          views (-> ds
                    (pj/pose :x :y)
                    pj/lay-point
                    (pj/scale :x :log))
          pl (pj/plan views)
          panel (first (:panels pl))]
      (is (= :log (get-in panel [:x-scale :type]))))))

(deftest log-scale-nonpositive-test
  (testing "non-positive values are filtered on log-scaled x axis"
    (let [pl (pj/plan (-> {:x [0 -1 1 10 100] :y [1 2 3 4 5]}
                          (pj/lay-point :x :y)
                          (pj/scale :x :log)))
          layer (first (:layers (first (:panels pl))))
          group (first (:groups layer))]
      (is (= 3 (count (:xs group))))
      (is (= [1 10 100] (vec (:xs group))))))
  (testing "non-positive values are filtered on log-scaled y axis"
    (let [pl (pj/plan (-> {:x [1 2 3 4 5] :y [0 -1 1 10 100]}
                          (pj/lay-point :x :y)
                          (pj/scale :y :log)))
          layer (first (:layers (first (:panels pl))))
          group (first (:groups layer))]
      (is (= 3 (count (:xs group))))))
  (testing "all-positive data is not filtered"
    (let [pl (pj/plan (-> {:x [1 10 100] :y [1 2 3]}
                          (pj/lay-point :x :y)
                          (pj/scale :x :log)))
          layer (first (:layers (first (:panels pl))))
          group (first (:groups layer))]
      (is (= 3 (count (:xs group)))))))

(deftest infinity-filtering-test
  (testing "infinite y values are filtered with warning"
    (let [pl (pj/plan (-> {:x [1 2 3 4 5]
                           :y [10.0 Double/POSITIVE_INFINITY 30.0 Double/NEGATIVE_INFINITY 50.0]}
                          (pj/lay-point :x :y)))
          layer (first (:layers (first (:panels pl))))
          group (first (:groups layer))]
      (is (= 3 (count (:xs group))))
      (is (= [1 3 5] (vec (:xs group))))
      (is (= [10.0 30.0 50.0] (vec (:ys group))))))
  (testing "infinite x values are filtered"
    (let [pl (pj/plan (-> {:x [1.0 Double/POSITIVE_INFINITY 3.0]
                           :y [10 20 30]}
                          (pj/lay-point :x :y)))
          group (-> pl :panels first :layers first :groups first)]
      (is (= 2 (count (:xs group))))))
  (testing "SVG has no NaN after infinity filtering"
    (let [svg (pj/plot (-> {:x [1 2 3] :y [1.0 Double/POSITIVE_INFINITY 3.0]}
                           (pj/lay-point :x :y)))]
      (is (not (clojure.string/includes? (str svg) "NaN")))))
  (testing "all-finite data is not filtered"
    (let [pl (pj/plan (-> {:x [1 2 3] :y [10.0 20.0 30.0]}
                          (pj/lay-point :x :y)))
          group (-> pl :panels first :layers first :groups first)]
      (is (= 3 (count (:xs group)))))))

(deftest stacked-negative-domain-test
  (testing "all-negative stacked bars produce correct y-domain"
    (let [pl (pj/plan (-> {:category ["A" "A" "B" "B"]
                           :group ["g1" "g2" "g1" "g2"]
                           :value [-10 -20 -5 -15]}
                          (pj/lay-bar :category :value {:color :group :position :stack})))
          [lo hi] (:y-domain (first (:panels pl)))]
      (is (neg? lo) "lower bound should be negative for all-negative stacked data")
      ;; The baseline is where the stack is measured from, so it sits on
      ;; the panel edge as an unstacked bar's does. It used to be padded
      ;; past, which put the baseline of a stacked chart and the
      ;; baseline of an unstacked one in different places.
      (is (== 0.0 hi) "the zero baseline is the top edge, not padded past")))
  (testing "mixed positive/negative stacked bars span both sides"
    (let [pl (pj/plan (-> {:category ["A" "A" "B" "B"]
                           :group ["g1" "g2" "g1" "g2"]
                           :value [10 -20 5 -15]}
                          (pj/lay-bar :category :value {:color :group :position :stack})))
          [lo hi] (:y-domain (first (:panels pl)))]
      (is (neg? lo) "lower bound extends below zero")
      ;; `position_stack` accumulates signed values, so a +10 written
      ;; after a -20 is drawn from -20 up to -10 rather than above the
      ;; baseline. Measured: the layer's y0s are 0 and -20, its ys -20
      ;; and -10, so nothing on this chart is drawn above zero and the
      ;; domain that covers it ends there.
      (is (== 0.0 hi) "the baseline is the top of what this chart draws")))
  (testing "all-negative stacked bars render without NaN"
    (let [svg (pj/plot (-> {:category ["A" "A" "B" "B"]
                            :group ["g1" "g2" "g1" "g2"]
                            :value [-10 -20 -5 -15]}
                           (pj/lay-bar :category :value {:color :group :position :stack})))]
      (is (not (clojure.string/includes? (str svg) "NaN"))))))

(deftest boolean-color-test
  (testing "Boolean false is not dropped as group key"
    (let [pl (-> {:x [1 2 3 4] :y [10 20 30 40] :flag [true false true false]}
                 (pj/lay-point :x :y {:color :flag})
                 pj/plan)
          groups (-> pl :panels first :layers first :groups)]
      (is (= 2 (count groups)) "two groups for true/false")
      (is (= "true" (:label (first groups))))
      (is (= "false" (:label (second groups))))
      ;; Both groups should get distinct palette colors (not default gray)
      (is (not= (:color (first groups)) (:color (second groups)))
          "true and false get different colors")))
  (testing "Legend matches rendering for boolean groups"
    (let [pl (-> {:x [1 2 3 4] :y [10 20 30 40] :flag [true false true false]}
                 (pj/lay-point :x :y {:color :flag})
                 pj/plan)
          legend-colors (mapv :color (:entries (:legend pl)))
          group-colors (mapv :color (-> pl :panels first :layers first :groups))]
      (is (= (count legend-colors) (count group-colors)))
      (is (= (set legend-colors) (set group-colors))
          "legend colors match group colors"))))

(deftest x-only-validation-test
  (testing "histogram rejects :y column"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"lay-histogram uses only the x column"
                          (pj/lay-histogram {:x [1 2 3] :y [4 5 6]} :x :y))))
  (testing "bar accepts a :y column (value bars)"
    (is (some? (pj/lay-bar {:x ["a" "b"] :y [1 2]} :x :y))))
  (testing "density rejects :y column"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"lay-density uses only the x column"
                          (pj/lay-density {:x [1 2 3] :y [4 5 6]} :x :y))))
  (testing "histogram with opts (not :y) still works"
    (is (some? (pj/lay-histogram {:x [1 2 3 4 5]} :x {:color :x})))))

(deftest multiple-layers-test
  (testing "plan with point + line layers"
    (let [views (-> tiny-ds
                    (pj/pose :x :y)
                    pj/lay-point
                    pj/lay-line)
          pl (pj/plan views)
          layers (get-in pl [:panels 0 :layers])]
      (is (= 2 (count layers))))))

(deftest color-groups-test
  (testing "color mapping with string values produces legend"
    (let [ds (tc/dataset {:x [1 2 3] :y [4 5 6] :g ["a" "b" "a"]})
          pl (pj/plan (-> ds (pj/lay-point :x :y {:color :g})))]
      (is (some? (:legend pl)))
      (is (= 2 (count (get-in pl [:legend :entries])))))))

(deftest plan-dimensions-test
  (testing "custom width and height"
    (let [views (-> tiny-ds (pj/pose :x :y) pj/lay-point)
          pl (pj/plan views {:width 800 :height 300})]
      (is (= 800 (:width pl)))
      (is (= 300 (:height pl))))))

(deftest cross-grid-strip-labels-test
  (testing "cross plot (full grid) shows all strip labels"
    (let [ds (tc/dataset {:a [1 2 3 4 5] :b [5 4 3 2 1] :c [2 4 6 8 10]})
          views (-> ds
                    (pj/cross-matrix [:a :b :c])
                    pj/lay-point)
          svg (pj/plot views)
          s (pj/svg-summary svg)
          texts (:texts s)]
      (is (= 9 (:panels s)))
      (is (some #{"a"} texts))
      (is (some #{"b"} texts))
      (is (some #{"c"} texts)))))

(deftest save-test
  (testing "pj/save writes valid SVG file"
    (let [ds (tc/dataset "https://raw.githubusercontent.com/mwaskom/seaborn-data/master/iris.csv"
                         {:key-fn keyword})
          path (str (java.io.File/createTempFile "plotje" ".svg"))
          views (-> ds (pj/pose :sepal_length :sepal_width)
                    (pj/lay-point {:color :species}))]
      (pj/save views path)
      (let [content (slurp path)]
        (is (.startsWith content "<?xml"))
        (is (.contains content "<svg"))
        (is (.contains content "setosa")))
      (.delete (java.io.File. path)))))

(deftest temporal-epoch-ms-test
  (testing "LocalDate converts to epoch-ms"
    (let [d (jt/local-date 2025 1 1)
          ms (resolve/temporal->epoch-ms d)]
      (is (double? ms))
      (is (== ms (* (.toEpochDay d) 86400000)))))
  (testing "LocalDateTime preserves sub-day precision"
    (let [dt (jt/local-date-time 2025 3 15 12 30 0)
          ms (resolve/temporal->epoch-ms dt)]
      (is (double? ms))
      ;; Should differ from midnight by 12.5 hours in ms
      (let [midnight-ms (resolve/temporal->epoch-ms (jt/local-date 2025 3 15))]
        (is (== (- ms midnight-ms) (* 12.5 3600000))))))
  (testing "Temporal plan has datetime ticks"
    (let [pl (-> (tc/dataset {:date [(jt/local-date 2025 1 1)
                                     (jt/local-date 2025 6 1)
                                     (jt/local-date 2025 12 1)]
                              :val [10 20 30]})
                 (pj/pose :date :val)
                 pj/lay-point
                 pj/plan)]
      (is (= 1 (count (:panels pl))))
      (is (seq (get-in pl [:panels 0 :x-ticks :labels]))))))

(deftest a-whole-day-axis-names-its-month
  (let [labels (fn labels
                 ([start n step] (labels start n step 600))
                 ([start n step width]
                  (-> {:d (vec (for [i (range n)]
                                 (jt/plus (jt/local-date start) (jt/days (* i step)))))
                       :v (vec (repeat n 1.0))}
                      (pj/lay-point :d :v)
                      (pj/options {:width width})
                      pj/plan :panels first :x-ticks :labels)))]
    (testing "a span inside one month is labelled with that month"
      ;; wadogo formats these as the bare day -- "02" "05" "08" -- which
      ;; reads the same over January as over March. ggplot2 4.0.0 names
      ;; the month on the same 28 dates.
      (is (= ["Jan-01" "Jan-08" "Jan-15" "Jan-22"]
             (labels "2024-01-01" 28 1))))

    (testing "a span of about ten days, which wadogo labels Tue 01:00"
      ;; The ticks here are a day apart but fall at 01:00, so the test
      ;; that decides this is the gap between ticks, not the time of day.
      (is (every? #(re-find #"^[A-Z][a-z]{2}-\d{2}$" %) (labels "2024-01-01" 10 1))))

    (testing "ticks less than a day apart keep the hour that tells them apart"
      ;; Widened deliberately. This asserts on the formatter -- given
      ;; sub-day ticks, the label says the hour -- and five daily dates
      ;; stopped producing sub-day ticks at 600 wide once the temporal
      ;; picker began rejecting a tick count whose labels do not fit
      ;; (2026-09-09). At 600 the same span is now ticked once a day and
      ;; labelled Mar-04 .. Mar-08, which is the right picture for daily
      ;; data; at 1200 there is room to tick every six hours, which is
      ;; the case this rule is about.
      (is (every? #(re-find #":" %) (labels "2024-03-04" 5 1 1200)))
      (is (= ["Mar-04" "Mar-05" "Mar-06" "Mar-07" "Mar-08"]
             (labels "2024-03-04" 5 1))
          "and at the default width the day is enough to tell them apart"))

    (testing "a span crossing a month boundary names both months"
      (is (= ["Jan-01" "Jan-08" "Jan-15" "Jan-22" "Jan-29"
              "Feb-05" "Feb-12" "Feb-19" "Feb-26"]
             (labels "2024-01-01" 60 1))))

    (testing "a span carrying a year keeps the year"
      ;; Relabelling these to month and day would lose which year.
      (is (every? #(re-find #"^\d{4}-" %) (labels "2024-01-01" 104 7))))

    (testing "ticks that fall at a time of day are left to say the hour"
      (is (every? #(re-find #":" %) (labels "2024-01-01" 3 1))))))

(deftest a-date-axis-is-labelled-at-the-step-it-takes
  ;; wadogo's own formatter measures a year as 365 days and a month as
  ;; 30, and asks for a step longer than that, so a step of exactly one
  ;; calendar unit fell through to the unit below: fifteen years of
  ;; January firsts were written 2001-01, 2002-01, and a year of month
  ;; starts Feb-01, Mar-01. The month and the day are the same on every
  ;; one of those ticks, so they take up room without telling any two
  ;; ticks apart.
  (let [labels (fn [pose] (-> pose pj/plan :panels first :x-ticks :labels vec))
        yearly (fn [from to]
                 (let [yrs (vec (range from to))]
                   (-> {:d (mapv #(jt/local-date % 6 15) yrs)
                        :v (mapv double yrs)}
                       (pj/lay-line :d :v))))
        monthly (fn [n]
                  (let [ds (vec (for [i (range n)]
                                  (jt/plus (jt/local-date 2020 1 1) (jt/months i))))]
                    (-> {:d ds :v (vec (map double (range n)))}
                        (pj/lay-line :d :v))))]

    (testing "ticks a whole number of years apart carry the year alone"
      (is (= ["2002" "2004" "2006" "2008" "2010" "2012" "2014"]
             (labels (yearly 2000 2015)))))

    (testing "ticks several years apart are labelled the same way"
      (is (every? #(re-matches #"\d{4}" %) (labels (yearly 1938 1972)))))

    (testing "ticks a month apart carry the year once the axis covers one"
      ;; A year of month starts spans 368 drawn days once padded, past
      ;; the point where the month alone stops saying which year it is.
      ;; ggplot2 4.0.0 turns over between a 301-day panel and a 368-day
      ;; one too.
      (is (= ["Jan 2020" "Mar 2020" "May 2020" "Jul 2020" "Sep 2020" "Nov 2020"]
             (labels (monthly 12)))))

    (testing "and carry the month alone on a shorter axis"
      (is (= ["Jan" "Feb" "Mar" "Apr" "May" "Jun" "Jul" "Aug" "Sep" "Oct"]
             (labels (monthly 10)))))

    (testing "ticks a month apart across years carry the year too"
      (is (every? #(re-matches #"\d{4}-\d{2}" %) (labels (monthly 48)))))))

(deftest a-bar-that-cannot-be-drawn-on-a-log-axis-is-dropped
  ;; A histogram bin holding nothing counts zero, and a log scale has no
  ;; reading for zero, so the bar's top scaled to an infinity that went
  ;; straight into the drawables and the SVG:
  ;; `points="92.53,352.00 ... 149.45,Infinity"`. Java2D clipped it in
  ;; silence, which is why a PNG looked right; Skia threw `Value out of
  ;; range for float: Infinity` from inside its own renderer. Reported
  ;; by Adrian Smith on Zulip, 2026-08-20, and reproduced with his data
  ;; on 0.8.0, 0.8.1 and 0.9.0.
  (let [gappy {:v (vec (concat (repeat 200 1.0) (repeat 3 100.0)))}
        infinities (fn [x] (->> (tree-seq coll? seq x)
                                (filter #(and (number? %)
                                              (Double/isInfinite (double %))))
                                clojure.core/count))]
    (testing "no infinity reaches the SVG"
      (let [svg (str (pj/plot (-> gappy (pj/lay-histogram :v) (pj/scale :y :log))))]
        (is (not (str/includes? svg "Infinity")))))

    (testing "no infinity reaches the Membrane drawables"
      (is (zero? (infinities (:drawables (pj/membrane (-> gappy
                                                          (pj/lay-histogram :v)
                                                          (pj/scale :y :log))))))))

    (testing "the bars that can be drawn still are"
      ;; two bins hold rows -- the 200 at 1.0 and the 3 at 100.0
      (is (= 2 (:polygons (pj/svg-summary
                           (pj/plot (-> gappy (pj/lay-histogram :v) (pj/scale :y :log))))))))

    (testing "a linear axis keeps every bar, empty ones included"
      (is (= 9 (:polygons (pj/svg-summary
                           (pj/plot (-> gappy (pj/lay-histogram :v))))))))

    (testing "the drop is reported"
      (is (str/includes? (with-out-str
                           (pj/plot (-> gappy (pj/lay-histogram :v) (pj/scale :y :log))))
                         "Removed 7 bars")))))

(deftest a-log-axis-draws-no-tick-outside-its-panel
  (let [y-ticks (fn [pose] (-> pose pj/plan :panels first :y-ticks))
        domain  (fn [pose] (-> pose pj/plan :panels first :y-domain))]
    (testing "a break below the domain is dropped, as ggplot2 drops it"
      ;; y = 3, 40, 900 gives the domain 2.2556..1197.01 in both
      ;; libraries. ggplot2 4.0.0 labels 10, 100 and 1000 and drops the
      ;; 1; Plotje drew the 1 below the panel box entirely.
      (let [pose (-> {:x [1.0 2.0 3.0] :y [3.0 40.0 900.0]}
                     (pj/lay-point :x :y)
                     (pj/scale :y :log)
                     (pj/options {:width 620 :height 300}))
            [lo hi] (domain pose)
            {:keys [values labels]} (y-ticks pose)]
        (is (= ["10" "100" "1000"] labels))
        (is (every? #(<= (double lo) (double %) (double hi)) values)
            "every tick sits inside the domain, so every label sits inside the panel")))

    (testing "a narrow domain keeps its labels rather than losing them"
      ;; Between 6 and 9 the only break the log generator offers is 10.
      ;; Dropping it would leave the axis with no labels at all, which is
      ;; worse than one just outside, so the filter does not apply.
      (let [pose (-> {:x [1.0 2.0 3.0 4.0] :y [6.0 7.0 8.0 9.0]}
                     (pj/lay-point :x :y)
                     (pj/scale :y :log))]
        (is (seq (:labels (y-ticks pose))))))))

(deftest include-makes-an-axis-reach-a-value
  (let [ydom (fn [pose] (vec (-> pose pj/plan :panels first :y-domain)))
        base (-> {:c [1 2 3] :v [40 50 60]} (pj/lay-point :c :v))]
    (testing "the value sits exactly at the panel edge, the other end is padded"
      ;; The same place `lay-bar` puts its baseline, so a bar chart and
      ;; a scatter of one column span one axis.
      (is (= [0.0 63.0] (ydom (pj/scale base :y {:include 0}))))
      (is (= [0.0 63.0] (ydom (-> {:c ["a" "b" "c"] :v [40 50 60]}
                                  (pj/lay-bar :c :v))))))

    (testing "a value inside the data changes nothing"
      (is (= (ydom base) (ydom (pj/scale base :y {:include 50})))))

    (testing "it reaches downward or upward, whichever the data misses"
      (is (= [-42.0 0.0] (ydom (-> {:c [1 2] :v [-40 -10]}
                                   (pj/lay-point :c :v)
                                   (pj/scale :y {:include 0}))))))

    (testing "a collection is a set, so the order it is written in says nothing"
      (is (= [0.0 100.0] (ydom (pj/scale base :y {:include [0 100]}))))
      (is (= [0.0 100.0] (ydom (pj/scale base :y {:include [100 0]})))))

    (testing "a :domain is an ordered pair, which is what tells the two apart"
      (is (= [100 0] (ydom (pj/scale base :y {:domain [100 0]}))))
      (is (= [80.0 70.0 60.0 50.0 40.0 30.0 20.0 10.0 0.0]
             (vec (-> (pj/scale base :y {:domain [85 0]})
                      pj/plan :panels first :y-ticks :values)))))

    (testing "what it refuses"
      ;; The shape is a property of the written value, so it is reported
      ;; at the call; the rest needs the merged spec and the column, so
      ;; they are reported at plan time.
      (are [spec] (thrown? clojure.lang.ExceptionInfo
                           (pj/scale base :y spec))
        {:include "a"}
        {:include []}
        {:include [0 nil]}
        {:include ##Inf})
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"both :domain .* and :include"
                            (ydom (pj/scale base :y {:include 0 :domain [0 85]}))))
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"beside a log scale"
                            (ydom (pj/scale base :y {:include 0 :type :log}))))
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"categorical axis"
                            (ydom (-> {:c [1 2 3] :v ["a" "b" "c"]}
                                      (pj/lay-point :c :v)
                                      (pj/scale :y {:include 0})))))
      (is (thrown? clojure.lang.ExceptionInfo (pj/scale base :color {:include 0}))
          "only the two aesthetics drawn as an axis read it"))

    (testing "the pair is reported wherever the two settings accumulated from"
      ;; Scale settings merge key by key down the scope chain, so a
      ;; :domain on the pose and an :include on a layer arrive as one
      ;; spec. That is why the pair is answered at plan time.
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"both :domain .* and :include"
                            (ydom (-> {:c [1 2 3] :v [40 50 60]}
                                      (pj/pose {:x :c :y :v})
                                      (pj/scale :y {:domain [0 85]})
                                      (pj/lay-point {:y {:scale {:include 0}}}))))))))

(deftest a-stacked-baseline-sits-where-an-unstacked-one-does
  ;; The stacked path put zero among the values it padded, so the
  ;; baseline floated above the panel edge its unstacked sibling sat on.
  (let [ydom (fn [pose] (vec (-> pose pj/plan :panels first :y-domain)))]
    (is (= 0.0 (first (ydom (-> {:c ["a" "a" "b" "b"] :g ["m" "n" "m" "n"] :v [10 20 30 40]}
                                (pj/lay-bar :c :v {:color :g :position :stack}))))))
    (is (= 0.0 (first (ydom (-> {:c ["a" "b"] :v [30 70]}
                                (pj/lay-bar :c :v))))))))

(deftest an-axis-domain-is-checked-against-the-column
  (let [base (-> {:c [1 2 3] :v [40 50 60]} (pj/lay-point :c :v))
        ydom (fn [pose] (vec (-> pose pj/plan :panels first :y-domain)))]
    (testing "a continuous column takes two finite numbers and nothing else"
      ;; scale/validate-bounds-pair! cannot ask this at the call: the
      ;; column's type decides how a domain is read, and a categorical
      ;; one takes a list of categories.
      (are [d] (thrown-with-msg? clojure.lang.ExceptionInfo
                                 #":domain .* is not a pair of two finite numbers"
                                 (ydom (pj/scale base :y {:domain d})))
        [0]
        [0 50 100]
        [0 nil]
        [0 ##Inf])
      (is (= [0 85] (ydom (pj/scale base :y {:domain [0 85]})))))

    (testing "a categorical column still takes its categories"
      (is (= ["c" "b" "a"] (ydom (-> {:c [1 2 3] :v ["a" "b" "c"]}
                                     (pj/lay-point :c :v)
                                     (pj/scale :y {:domain ["c" "b" "a"]}))))))

    (testing "a temporal axis takes dates, as its :breaks do"
      ;; The axis holds epoch milliseconds. Given the dates straight
      ;; through, the domain held LocalDate objects and the first mark
      ;; placed died on a null doubleValue.
      (let [pose (-> {:d [(jt/local-date 2020 3 1)
                          (jt/local-date 2020 6 1)
                          (jt/local-date 2020 9 1)]
                      :v [1 2 3]}
                     (pj/lay-point :d :v)
                     (pj/scale :x {:domain [(jt/local-date 2020 1 1)
                                            (jt/local-date 2020 12 31)]}))]
        (is (every? number? (-> pose pj/plan :panels first :x-domain)))
        (is (= 3 (:points (pj/svg-summary pose))))))))

(deftest an-axis-of-whole-numbers-is-ticked-at-whole-numbers
  (let [xl (fn [pose] (vec (-> pose pj/plan :panels first :x-ticks :labels)))
        yl (fn [pose] (vec (-> pose pj/plan :panels first :y-ticks :labels)))]
    (testing "the case two users reported"
      ;; Timothy Pratley and Adrian Smith, Zulip 2026-08-20. wadogo picks
      ;; the step from the span and the count asked for, so four whole
      ;; values asked for ten ticks were given halves.
      (is (= ["0" "1" "2" "3"] (xl (-> {:x [0 1 2 3] :y [1 2 3 4]}
                                       (pj/lay-point :x :y)))))
      (is (= ["1" "2" "3"] (xl (-> {:x [1 2 3] :y [1 2 3]}
                                   (pj/lay-point :x :y)))))
      ;; ggplot2 4.0.0 draws 1, 1.5, 2, 2.5, 3 here (measured):
      ;; `scales::extended_breaks` honours the count it is given, as
      ;; wadogo does. This is not that algorithm.
      (is (= ["4" "5" "6"] (xl (-> {:x [4 5 6] :y [1 2 3]}
                                   (pj/lay-point :x :y))))))

    (testing "a count axis, whatever computes the count"
      (is (= ["0" "1" "2"] (yl (-> {:c ["a" "b" "a" "c"]} (pj/lay-bar :c)))))
      (is (= ["0" "1" "2" "3"] (yl (-> {:v [1 2 2 3 3 3 4 5 5]}
                                       (pj/lay-histogram :v))))))

    (testing "an axis whose values are not whole is left alone"
      (is (some #(clojure.string/includes? % ".")
                (xl (-> {:x [1.5 2.5 3.5] :y [1 2 3]} (pj/lay-point :x :y)))))
      ;; The extent alone would call this whole. The value at 1.5 is
      ;; what says otherwise, and ticking 1 and 2 would leave it with
      ;; nothing to read it against.
      (is (some #(clojure.string/includes? % ".")
                (xl (-> {:x [1.0 1.5 2.0] :y [10 20 30]} (pj/lay-point :x :y))))))

    (testing "a proportion axis keeps its fractions"
      ;; `:fill` rewrites whole counts as proportions, so the stat's own
      ;; values say nothing about what is drawn.
      (is (some #(clojure.string/includes? % ".")
                (yl (-> {:x ["a" "a" "b" "b"] :g ["m" "n" "m" "n"] :v [10 20 30 40]}
                        (pj/lay-bar :x :v {:color :g :position :fill}))))))

    (testing "a written :domain is read too"
      (is (= ["0" "1" "2" "3"] (xl (-> {:x [0 1 2 3] :y [1 2 3 4]}
                                       (pj/lay-point :x :y)
                                       (pj/scale :x {:domain [0 3]})))))
      (is (some #(clojure.string/includes? % ".")
                (xl (-> {:x [0 1 2 3] :y [1 2 3 4]}
                        (pj/lay-point :x :y)
                        (pj/scale :x {:domain [0.5 3.5]}))))))

    (testing "an axis already ticked at whole numbers is unchanged"
      (is (= ["0" "10" "20" "30" "40" "50" "60" "70" "80" "90" "100"]
             (xl (-> {:x [0 50 100] :y [1 2 3]} (pj/lay-point :x :y))))))))

(deftest a-legend-and-its-axis-label-one-column-the-same-way
  ;; The whole-number rule went into `scale/linear-ticks`, which the
  ;; axis path read and `nice-legend-values` did not, so one column of
  ;; 1, 2, 3, 4 was labelled 1 2 3 4 on the axis and 1.0, 1.5, 2.0 ...
  ;; in the legend beside it.
  (let [values (fn [pose k]
                 (mapv :value (:entries (k (pj/plan pose)))))
        axis (fn [pose] (vec (-> pose pj/plan :panels first :x-ticks :labels)))]
    (testing "a whole column reads whole in both"
      (let [pose (-> {:x [1 2 3 4] :y [1 2 3 4] :n [1 2 3 4]}
                     (pj/lay-point :x :y {:size :n}))]
        (is (= ["1" "2" "3" "4"] (axis pose)))
        (is (= [1.0 2.0 3.0 4.0] (values pose :size-legend)))))

    (testing "and the alpha legend answers the same"
      (is (= [1.0 2.0 3.0 4.0]
             (values (-> {:x [1 2 3 4] :y [1 2 3 4] :n [1 2 3 4]}
                         (pj/lay-point :x :y {:alpha :n}))
                     :alpha-legend))))

    (testing "a column that is not whole keeps its fractions"
      (is (some #(not (== % (Math/rint (double %))))
                (values (-> {:x [1 2 3 4] :y [1 2 3 4] :n [1.5 2.5 3.5 4.5]}
                            (pj/lay-point :x :y {:size :n}))
                        :size-legend))))))

(deftest a-temporal-axis-is-ticked-over-the-domain-it-was-given
  ;; A `:domain` says what the axis spans. Ticking the data's own extent
  ;; instead labelled only the part of the axis the data covers, which
  ;; became reachable when a dated `:domain` started working at all.
  (let [pose (-> {:when (mapv (fn [year] (jt/local-date year 6 15)) (range 2015 2025))
                  :reading [12 15 11 18 22 19 25 24 28 31]}
                 (pj/lay-line :when :reading)
                 (pj/scale :x {:domain [(jt/local-date 2010 1 1)
                                        (jt/local-date 2030 1 1)]}))
        labels (vec (-> pose pj/plan :panels first :x-ticks :labels))]
    (is (= "2010" (first labels)))
    (is (= "2030" (last labels))
        "the ticks reach both ends of the domain, not just the data")))

(deftest a-log-axis-spanning-decades-is-labelled-between-the-powers
  ;; The candidate set is scored by the ticks the axis draws, not by the
  ;; ticks the generator offers. The 15%-of-log-span margin puts bounding
  ;; powers of ten just outside the domain, and counting those chose
  ;; powers of ten for an axis that then drew two of them.
  (let [pose (-> (tc/select-rows (rdatasets/gapminder-gapminder)
                                 #(= 2007 (:year %)))
                 (pj/lay-point :gdp-percap :life-exp)
                 (pj/scale :x :log)
                 (pj/options {:width 620 :height 400}))
        panel (-> pose pj/plan :panels first)
        [lo hi] (:x-domain panel)
        {:keys [values labels]} (:x-ticks panel)]
    (is (= ["300" "500" "1000" "2000" "3000" "5000" "10000" "20000" "30000" "50000"]
           labels)
        "a 2.5-decade axis carries intermediates; it read 1000 and 10000")
    (is (every? #(<= (double lo) (double %) (double hi)) values)
        "and every one of them sits inside the domain")))

(deftest a-log-gradient-bar-is-labelled-only-on-the-bar
  ;; `continuous-legend-ticks` passed the generator through with no
  ;; filter, so the tick at 100000 was placed at 1.14 of a bar that ends
  ;; at 1.0 -- drawn over the legend's title.
  (let [legend (-> (tc/select-rows (rdatasets/gapminder-gapminder)
                                   #(= 2007 (:year %)))
                   (pj/lay-point :life-exp :gdp-percap {:color :gdp-percap})
                   (pj/scale :color :log)
                   pj/plan
                   :legend)
        {:keys [ticks min max]} legend]
    (is (seq ticks))
    (is (every? #(<= 0.0 (double (:t %)) 1.0) ticks)
        "every tick sits on the bar")
    (is (every? #(<= (double min) (double (:value %)) (double max)) ticks)
        "and names a value the data reaches")
    (testing "a domain too narrow for two breaks is ticked across itself"
      ;; Between 6 and 9 the log generator offers only 10, which is
      ;; outside. `scale/log-ticks-drawn` ticks the domain itself there,
      ;; and the axis beside such a legend reads the same values.
      (let [narrow (-> {:x [1 2 3] :y [1 2 3] :v [6 7 9]}
                       (pj/lay-point :x :y {:color :v})
                       (pj/scale :color :log)
                       pj/plan
                       :legend
                       :ticks)]
        (is (= [6.0 7.0 8.0 9.0] (mapv :value narrow)))
        (is (every? #(<= 0.0 (double (:t %)) 1.0) narrow)
            "and every one of them sits on the bar")))))

(deftest a-date-axis-takes-breaks-written-as-dates
  ;; Timothy Pratley asked for a way to steer tick selection, having
  ;; read the book and concluded there was none. `:breaks` existed and
  ;; died on `LocalDate cannot be cast to Number`: a temporal axis holds
  ;; epoch-ms, and the breaks went through as date objects.
  (let [yearly (vec (for [i (range 30)] (jt/local-date (+ 1940 i) 6 15)))
        labels (fn [spec]
                 (-> {:d yearly :v (vec (repeat 30 1.0))}
                     (pj/lay-line :d :v)
                     (pj/options {:width 700})
                     (pj/scale :x spec)
                     pj/plan :panels first :x-ticks :labels))]
    (testing "dates as breaks land where they were asked for"
      (is (= ["1940" "1950" "1960" "1970"]
             (labels {:breaks (mapv #(jt/local-date % 1 1) [1940 1950 1960 1970])}))))
    (testing "a LocalDateTime and a java.util.Date are accepted too"
      (is (= 1 (count (labels {:breaks [(jt/local-date-time "1950-01-01T00:00")]}))))
      (is (= 1 (count (labels {:breaks [(java.util.Date. 0)]})))))
    (testing ":tick-labels still relabel them"
      (is (= ["forties" "fifties"]
             (labels {:breaks (mapv #(jt/local-date % 1 1) [1940 1950])
                      :tick-labels ["forties" "fifties"]}))))
    (testing "a single break, which wadogo's own formatter cannot label"
      ;; `find-minimum-step` reduces min over the gaps between ticks and
      ;; dies on "Wrong number of args (0)" when there is one tick.
      (is (= ["1950-01-01"] (labels {:breaks [(jt/local-date 1950 1 1)]}))))))

(deftest format-log-ticks-test
  (testing "Powers of 10 >= 1 render as integers"
    (is (= ["1" "10" "100" "1000"]
           (scale/format-log-ticks [1.0 10.0 100.0 1000.0]))))
  (testing "Decimal powers keep decimal form"
    (is (= ["0.001" "0.01" "0.1" "1" "10"]
           (scale/format-log-ticks [0.001 0.01 0.1 1.0 10.0])))))

(deftest column-name-matching-is-strict-test
  ;; Column name matching is strict: a keyword reference does not
  ;; match a string column name with the same characters and vice
  ;; versa. Pick one form and use it consistently with the dataset's
  ;; actual column names.
  (testing "string refs on a string-keyed dataset work"
    (let [ds (tc/dataset {"x" [1 2 3] "y" [4 5 6]})
          s (-> ds (pj/pose "x" "y") pj/lay-point pj/plot pj/svg-summary)]
      (is (= 3 (:points s)))))
  (testing "keyword refs on a keyword-keyed dataset work"
    (let [ds (tc/dataset {:x [1 2 3] :y [4 5 6]})
          s (-> ds (pj/pose :x :y) pj/lay-point pj/plot pj/svg-summary)]
      (is (= 3 (:points s)))))
  (testing "keyword refs on a string-keyed dataset throw"
    (let [ds (tc/dataset {"x" [1 2 3] "y" [4 5 6]})]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"not found in dataset"
                            (-> ds (pj/pose :x :y) pj/lay-point pj/plot)))))
  (testing "string refs on a keyword-keyed dataset throw"
    (let [ds (tc/dataset {:x [1 2 3] :y [4 5 6]})]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"not found in dataset"
                            (-> ds (pj/pose "x" "y") pj/lay-point pj/plot)))))
  (testing "a dataset whose column names are of mixed types still gets the message"
    ;; The message lists the available names, and listing them sorts
    ;; them. Sorting a keyword against a string or a number throws a
    ;; ClassCastException, so the helpful error was replaced by an
    ;; unhelpful one exactly where a name is most likely to be mistyped.
    (doseq [[label ds] [["keyword and string names"
                         (tc/dataset {:x [1 2 3] :y [4 5 6] "blue" ["a" "b" "c"]})]
                        ["keyword and integer names"
                         (tc/dataset {:x [1 2 3] :y [4 5 6] 0 ["a" "b" "c"]})]]]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :nope \(from :color\) not found in dataset"
                            (-> ds (pj/pose :x :y) (pj/lay-point {:color :nope}) pj/plot))
          label)))
  (testing "string :color falls through to literal CSS when no string column matches"
    ;; :color "#FF0000" is the canonical literal-color case and must
    ;; keep working; it does not name any dataset column.
    (let [v (-> (tc/dataset {:x [1 2 3] :y [4 5 6]})
                (pj/pose :x :y)
                (pj/lay-point {:color "#FF0000"})
                pj/plot)]
      (is (= 3 (:points (pj/svg-summary v))))))
  (testing "Typo gives error at plan time"
    (let [iris (tc/dataset {:sepal_length [5.0 6.0] :sepal_width [3.0 3.5]})]
      (is (thrown? clojure.lang.ExceptionInfo
                   (-> iris (pj/pose :sepl_length :sepal_width)
                       pj/lay-point pj/plot)))))
  (testing "pj/cross is purely structural -- still works with strings"
    (is (= 9 (count (pj/cross ["a" "b" "c"] ["a" "b" "c"]))))))

(deftest string-column-in-lay-test
  (testing "String column names in lay-point directly (no explicit pj/pose)"
    (let [s (-> {"x" [1 2 3] "y" [4 5 6]}
                (pj/lay-point "x" "y") pj/plot pj/svg-summary)]
      (is (= 3 (:points s)))))
  (testing "String column names in lay-line directly"
    (let [s (-> {"x" [1 2 3] "y" [4 5 6]}
                (pj/lay-line "x" "y") pj/plot pj/svg-summary)]
      (is (= 1 (:lines s)))))
  (testing "String column in lay-histogram directly"
    (let [s (-> {"x" [1 2 3 4 5 6 7 8 9 10]}
                (pj/lay-histogram "x") pj/plot pj/svg-summary)]
      (is (pos? (:polygons s))))))

(deftest named-color-test
  (testing "Named color strings work as fixed colors"
    (let [s (-> {:x [1 2 3] :y [4 5 6]}
                (pj/pose :x :y)
                (pj/lay-point {:color "red"})
                pj/plot pj/svg-summary)]
      (is (= 3 (:points s)))))
  (testing "Named color produces correct RGBA"
    (let [pl (-> {:x [1 2 3] :y [4 5 6]}
                 (pj/pose :x :y)
                 (pj/lay-point {:color "steelblue"})
                 pj/plan)
          c (:color (first (:groups (first (:layers (first (:panels pl)))))))]
      (is (> (nth c 2) 0.5) "steelblue should have high blue channel")))
  (testing "Unknown color string is reported at the pose, naming both readings"
    ;; It used to reach the renderer and die on "Unknown color", which
    ;; said nothing about the likelier mistake -- that a column of that
    ;; name was meant. The data is asked first now, so the message can
    ;; name what was looked up and what else it could have been.
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"not found in dataset.*not a color either"
                          (-> {:x [1 2 3] :y [4 5 6]}
                              (pj/pose :x :y)
                              (pj/lay-point {:color "notacolor"})
                              pj/plot))))
  (testing "A keyword naming a color is drawn, not looked up"
    ;; `:color :red` used to be reported as a missing column, because a
    ;; keyword was a column reference and nothing else.
    (let [c (-> {:x [1 2 3] :y [4 5 6]}
                (pj/pose :x :y)
                (pj/lay-point {:color :red})
                pj/plan :panels first :layers first :groups first :color)]
      (is (= [1.0 0.0 0.0 1.0] c))))
  (testing "A hex string missing its # is reported, not read as a shade"
    ;; clojure2d reads a bare `abc` as `#aabbcc`, so this used to draw a
    ;; color for what is far likelier a mistyped column name.
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"not found in dataset.*not a color either"
                          (-> {:x [1 2 3] :y [4 5 6]}
                              (pj/pose :x :y)
                              (pj/lay-point {:color "fff"})
                              pj/plan)))))

(deftest schema-all-marks-test
  (testing "Every mark type produces a valid plan"
    (let [iris (tc/dataset "https://raw.githubusercontent.com/mwaskom/seaborn-data/master/iris.csv"
                           {:key-fn keyword})
          xy-ds (tc/dataset {:x (range 10) :y (range 10)})
          eb-ds (tc/dataset {:x ["a" "b"] :y [10 20] :y-min [8 17] :y-max [12 23]})
          txt-ds (tc/dataset {:x [1 2] :y [3 4] :n ["a" "b"]})
          cases [["point" (-> iris (pj/pose :sepal_length :sepal_width) (pj/lay-point {:color :species}))]
                 ["bar" (-> iris (pj/pose :species) pj/lay-bar)]
                 ["histogram" (-> iris (pj/pose :sepal_length) pj/lay-histogram)]
                 ["line" (-> xy-ds (pj/pose :x :y) pj/lay-line)]
                 ["step" (-> xy-ds (pj/pose :x :y) pj/lay-step)]
                 ["lm" (-> iris (pj/pose :sepal_length :sepal_width) (pj/lay-smooth {:stat :linear-model :confidence-band true}))]
                 ["loess" (-> iris (pj/pose :sepal_length :sepal_width) pj/lay-smooth)]
                 ["area" (-> xy-ds (pj/pose :x :y) pj/lay-area)]
                 ["boxplot" (-> iris (pj/pose :species :sepal_width) pj/lay-boxplot)]
                 ["violin" (-> iris (pj/pose :species :sepal_width) pj/lay-violin)]
                 ["density" (-> iris (pj/pose :sepal_length) pj/lay-density)]
                 ["ridgeline" (-> iris (pj/pose :species :sepal_width) pj/lay-ridgeline)]
                 ["text" (-> txt-ds (pj/pose :x :y) (pj/lay-text {:text :n}))]
                 ["tile" (-> iris (pj/pose :sepal_length :sepal_width) pj/lay-tile)]
                 ["contour" (-> iris (pj/pose :sepal_length :sepal_width) pj/lay-contour)]
                 ["errorbar" (-> eb-ds (pj/pose :x :y) (pj/lay-errorbar {:y-min :y-min :y-max :y-max}))]
                 ["lollipop" (-> eb-ds (pj/pose :x :y) pj/lay-lollipop)]
                 ["summary" (-> iris (pj/pose :species :sepal_width) pj/lay-summary)]]]
      (doseq [[mark-name views] cases]
        (testing mark-name
          (is (pj/valid-plan? (pj/plan views {:validate false}))))))))

(deftest validation-test
  (testing "numeric faceting produces correct panels"
    (is (= 3 (-> {:x [1 2 3 4 5 6] :y [10 20 30 40 50 60]
                  :f [1.0 1.0 2.0 2.0 3.0 3.0]}
                 (pj/lay-point :x :y) (pj/facet :f) pj/plan :panels count)))
    (is (= 3 (-> {:x [1 2 3 4 5 6] :y [10 20 30 40 50 60]
                  :s ["a" "a" "b" "b" "c" "c"]}
                 (pj/lay-point :x :y) (pj/facet :s) pj/plan :panels count))))

  (testing "histogram on categorical column throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"numeric"
                          (-> {:x ["a" "b" "c"]} (pj/lay-histogram :x) pj/plan))))

  (testing "lm on categorical x throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"numeric"
                          (-> {:species ["a" "b" "c"] :y [1 2 3]}
                              (pj/lay-smooth :species :y {:stat :linear-model}) pj/plan))))

  (testing "loess on categorical x throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"numeric"
                          (-> {:species ["a" "b" "c" "d"] :y [1 2 3 4]}
                              (pj/lay-smooth :species :y) pj/plan))))

  (testing "errorbar without y-min/y-max throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"y-min.*y-max"
                          (-> {:x ["a" "b" "c"] :y [1 2 3]}
                              (pj/lay-errorbar :x :y) pj/plan))))

  (testing "text without :text column throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"text"
                          (-> {:x [1 2 3] :y [10 20 30]}
                              (pj/lay-text :x :y) pj/plan))))

  (testing "scale aesthetic validation"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"takes one of the aesthetics"
                          (pj/scale [] :z :log))))

  (testing "coord validation"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"Coordinate"
                          (pj/coord [] :invalid))))

  (testing "y-type propagated when x and y reference the same column"
    ;; Regression: previously (= x-res y-res) returned nil for y-type,
    ;; letting ClassCastException escape instead of a clear error.
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"numeric"
                          (-> {:species ["a" "b" "c"]}
                              (pj/lay-summary :species :species) pj/plan)))))

(deftest aesthetic-column-validation-test
  ;; persona-16 B1: validate-columns covers :color/:size/:alpha/:shape/:group/
  ;; :text/:y-min/:y-max/:fill, not just :x/:y. Closes the C2 epic:
  ;; P5-R2 C1, P9-R2 F5/F6, Skept-R4 F2/F5/F6, P7-R2 F1/F2/F3, P11-R2 F5,
  ;; P3-R2 footgun.
  (let [data {:x [1.0 2.0 3.0] :y [10.0 20.0 30.0] :g ["a" "b" "c"]}]
    (testing "typoed :color keyword throws with key and column name"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :speices \(from :color\) not found"
                            (-> data (pj/lay-point :x :y {:color :speices}) pj/plan))))

    (testing "typoed :size keyword throws"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :bogus \(from :size\) not found"
                            (-> data (pj/lay-point :x :y {:size :bogus}) pj/plan))))

    (testing "typoed :alpha keyword throws"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :bogus \(from :alpha\) not found"
                            (-> data (pj/lay-point :x :y {:alpha :bogus}) pj/plan))))

    (testing "typoed :group keyword throws"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :bogus \(from :group\) not found"
                            (-> data (pj/lay-point :x :y {:group :bogus}) pj/plan))))

    (testing "typoed :y-min keyword throws"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Column :bogus1 \(from :y-min\) not found"
                            (-> data (pj/lay-errorbar :x :y {:y-min :bogus1 :y-max :bogus2}) pj/plan))))

    (testing "literal :color string is not flagged (named color)"
      (is (some? (-> data (pj/lay-point :x :y {:color "red"}) pj/plan :panels))))

    (testing "literal :color hex is not flagged"
      (is (some? (-> data (pj/lay-point :x :y {:color "#FF0000"}) pj/plan :panels))))

    (testing "good :color column still works"
      (is (some? (-> data (pj/lay-point :x :y {:color :g}) pj/plan :panels))))

    (testing "nil :color (explicit cancellation) skipped by validator"
      (is (some? (-> data (pj/lay-point :x :y {:color nil}) pj/plan :panels))))

    (testing "string column ref in :color resolves correctly"
      (is (some? (-> {"a" [1 2 3] "b" [10 20 30] "g" ["x" "y" "z"]}
                     (pj/lay-point "a" "b" {:color "g"}) pj/plan :panels))))

    (testing "available columns listed in error for typo recovery"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Available: \(:g :x :y\)"
                            (-> data (pj/lay-point :x :y {:color :typo}) pj/plan))))))

(deftest compound-key-spellings-agree-test
  ;; One request, three spellings, one answer. The written-out form
  ;; used to escape the guard the bare form trips: `{:color {:column
  ;; [:a :b]}}` drew a single grey mark under a warning about a
  ;; numeric colour, while `{:color [:a :b]}` was reported.
  (let [data {:t [1 2 1] :v [1.0 2.0 3.0]
              :part ["sepal" "sepal" "petal"]
              :dimension ["length" "width" "length"]}]
    (testing "an aesthetic that unites a vector reads both spellings alike"
      (is (= 3 (-> data (pj/pose {:x :t :y :v :col [:part :dimension]})
                   pj/lay-point pj/plan :panels count)))
      (is (= 3 (-> data (pj/pose {:x :t :y :v :col {:column [:part :dimension]}})
                   pj/lay-point pj/plan :panels count)))
      (is (= 3 (-> data (pj/lay-point :t :v) (pj/facet [:part :dimension])
                   pj/plan :panels count))))

    (testing "an aesthetic that does not unite a vector reports both spellings alike"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #":color was given several columns"
                            (-> data (pj/lay-point :t :v {:color [:part :dimension]})
                                pj/plan)))
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #":color was given several columns"
                            (-> data (pj/lay-point :t :v {:color {:column [:part :dimension]}})
                                pj/plan))))))

(deftest mapping-only-map-is-a-pose-test
  ;; A map carrying a map-valued :mapping is a pose. Read as data --
  ;; which a map with neither :layers nor :poses used to be -- it
  ;; became a dataset whose columns were :mapping and :data, and drew
  ;; two points of nonsense under a mapping of {:x :mapping :y :data}.
  (let [data {:quarter [1 2 3 4] :revenue [10 12 14 13]}]
    (testing "a hand-built pose needs no :layers to be recognized"
      (let [fr (pj/pose {:mapping {:x :quarter :y :revenue} :data data})]
        (is (= {:x :quarter :y :revenue} (:mapping fr)))
        (is (= 4 (:points (pj/svg-summary fr))))))

    (testing "a mapping-only template is completed by pj/with-data"
      (is (= 4 (-> (pj/pose {:mapping {:x :quarter :y :revenue}})
                   (pj/with-data data)
                   pj/svg-summary
                   :points))))

    (testing "a dataset holding a column called :mapping is still data"
      ;; The value decides, not the key: a column is a sequence and a
      ;; mapping is a map.
      (is (= 3 (:points (pj/svg-summary
                         (pj/lay-point {:mapping [1 2 3] :v [4.0 5.0 6.0]}
                                       :mapping :v))))))))

(deftest dodge-cohort-reads-the-group-key-test
  ;; A dodge slot per combination the grouping names, not per legend
  ;; entry. The cohort used to key on a group's :label -- the colour
  ;; column's value alone -- so a compound key of two parts and two
  ;; dimensions got two slots, and the twelve bars were drawn at six
  ;; places, two to a place. ggplot2 on the same shape draws twelve
  ;; bars at twelve centres (measured with ggplot_build).
  (let [data {:part      (mapcat #(repeat 3 %) ["sepal" "sepal" "petal" "petal"])
              :dimension (mapcat #(repeat 3 %) ["length" "width" "length" "width"])
              :t         (vec (flatten (repeat 4 ["a" "b" "c"])))
              :v         [1.0 2.0 3.0 1.5 2.5 3.5 2.0 3.0 4.0 2.5 3.5 4.5]}
        layer-of (fn [fr] (-> fr pj/plan :panels first :layers first))]

    (testing "a compound grouping gets a slot per combination"
      (let [lay (layer-of (pj/lay-bar data :t :v {:color :part
                                                  :group :dimension
                                                  :position :dodge}))]
        (is (= 4 (count (:groups lay))))
        (is (= 4 (:n-groups (:dodge-ctx lay))))
        (is (= [0 1 2 3] (mapv :dodge-idx (:groups lay))))))

    (testing "a colour alone is unchanged"
      (let [lay (layer-of (pj/lay-bar data :t :v {:color :part :position :dodge}))]
        (is (= 2 (count (:groups lay))))
        (is (= 2 (:n-groups (:dodge-ctx lay))))
        (is (= [0 1] (mapv :dodge-idx (:groups lay))))))

    (testing "writing the second distinction changes the picture"
      ;; The plots used to be byte-identical, which is what made the
      ;; defect silent.
      (is (not= (pj/plot (pj/lay-bar data :t :v {:color :part
                                                 :group :dimension
                                                 :position :dodge}))
                (pj/plot (pj/lay-bar data :t :v {:color :part
                                                 :position :dodge})))))))

(deftest panels-disagreeing-about-an-axis-report-test
  ;; Sharing a scale across panels read the first panel's domain to
  ;; decide whether the axis held numbers, then took the categorical
  ;; branch for the rest -- concatenating a category list with a range
  ;; into ["a" "b" 0.85 4.15] and drawing it as a band axis.
  (let [data {:t [1 2 3 4] :species ["a" "b" "a" "b"] :len [1.0 2.0 3.0 4.0]}
        mixed (-> data (pj/lay-point :t [:species :len]) (pj/facet :series))]

    (testing "a categorical panel beside a numeric one is reported"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"disagree about what the y axis holds"
                            (pj/plan mixed))))

    (testing "and draws once the panels stop sharing that axis"
      (is (= [["a" "b"] [0.85 4.15]]
             (mapv :y-domain (:panels (pj/plan (pj/options mixed {:scales :free-y})))))))

    (testing "panels that agree still share"
      (is (= [[0.9 4.1] [0.9 4.1]]
             (mapv :y-domain (:panels (pj/plan (-> data
                                                   (pj/lay-point :t :len)
                                                   (pj/facet :species))))))))))

(deftest a-mapping-is-a-pose-test
  ;; A map naming aesthetics is a mapping, and lifts to a leaf
  ;; carrying it. Read as data -- which every keyword-keyed map used
  ;; to be -- `{:x :mpg :y :cyl}` became a two-column table whose
  ;; columns were :x and :y, holding the column names as values.
  (let [iris-ish {:sepal-length [1.0 2.0 3.0] :sepal-width [4.0 5.0 6.0]
                  :petal-length [7.0 8.0 9.0] :petal-width [1.5 2.5 3.5]}]

    (testing "a mapping lifts to a leaf with no data"
      (let [fr (pj/pose {:x :sepal-length :y :sepal-width})]
        (is (= {:x :sepal-length :y :sepal-width} (:mapping fr)))
        (is (nil? (:data fr)))))

    (testing "and is completed by pj/with-data"
      (is (= 3 (:points (pj/svg-summary
                         (-> (pj/pose {:x :sepal-length :y :sepal-width})
                             (pj/with-data iris-ish)))))))

    (testing "a pj/arrange cell may be a mapping"
      ;; The cell says which columns its panel draws; data and layers
      ;; come from the pose it is arranged into.
      (let [fr (-> (pj/arrange [{:x :sepal-length :y :sepal-width}
                                {:x :petal-length :y :petal-width}])
                   (pj/with-data iris-ish)
                   (pj/lay-point))
            s (pj/svg-summary fr)]
        (is (= 2 (:panels s)))
        (is (= 6 (:points s)))))

    (testing "a map of columns is still data"
      ;; The values decide: a column is a sequence, a column reference
      ;; is a keyword or a string.
      (is (= 3 (:points (pj/svg-summary
                         (pj/lay-point {:x [1 2 3] :y [4.0 5.0 6.0]} :x :y))))))

    (testing "a vector of row maps is still data"
      (is (= 2 (:points (pj/svg-summary
                         (pj/lay-point [{:x 1 :y 2.0} {:x 2 :y 3.0}] :x :y))))))))

(deftest arrange-takes-data-first-test
  ;; `(-> data (pj/arrange cells) (pj/lay-point))` is the natural
  ;; spelling once a cell may be a mapping, and it used to fail: the
  ;; dataset was read as the cell list and its first column reported
  ;; as rendered hiccup. The second argument decides -- a sequential
  ;; one is the cells, a map is the options.
  (let [d {:sepal-length [1.0 2.0 3.0] :sepal-width [4.0 5.0 6.0]
           :petal-length [7.0 8.0 9.0] :petal-width [1.5 2.5 3.5]}
        cells [{:x :sepal-length :y :sepal-width}
               {:x :petal-length :y :petal-width}]
        summary (fn [fr] (select-keys (pj/svg-summary fr) [:panels :points]))]

    (testing "data first, then cells"
      (is (= {:panels 2 :points 6}
             (summary (-> d (pj/arrange cells) (pj/lay-point))))))

    (testing "data first, then cells and options"
      (is (= {:panels 2 :points 6}
             (summary (-> d (pj/arrange cells {:cols 1}) (pj/lay-point))))))

    (testing "the older arities are unchanged"
      (let [poses [(pj/pose d :sepal-length :sepal-width)
                   (pj/pose d :petal-length :petal-width)]]
        (is (= {:panels 2 :points 6} (summary (-> (pj/arrange poses) (pj/lay-point)))))
        (is (= {:panels 2 :points 6}
               (summary (-> (pj/arrange poses {:cols 1}) (pj/lay-point)))))
        (is (= {:panels 2 :points 6}
               (summary (-> (pj/arrange [[(first poses)] [(second poses)]])
                            (pj/lay-point)))))))))

(deftest facet-validation-test
  ;; persona-16 B3. Closes P9-R2 F9, Skept-R4 F7, P3-R2 Footgun 5.
  (let [data {:x [1 2 3 4 5 6] :y [10 20 30 40 50 60]
              :g ["a" "b" "a" "b" "a" "b"]
              :h ["x" "y" "x" "y" "x" "y"]}]
    (testing "typoed facet column throws with available columns"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Facet column :speices.*not found"
                            (-> data (pj/lay-point :x :y) (pj/facet :speices) pj/plan))))

    (testing "a vector facet is a compound key: one panel per observed combination"
      ;; :g and :h move together in this data, so only two of the four
      ;; combinations occur. That is the whole difference between a
      ;; compound facet and pj/facet-grid, which fills the rectangle --
      ;; asserted together so neither can drift into the other.
      (is (= 2 (-> data (pj/lay-point :x :y) (pj/facet [:g :h]) pj/plan :panels count)))
      (is (= 4 (-> data (pj/lay-point :x :y) (pj/facet-grid :g :h) pj/plan :panels count))))

    (testing "a compound facet labels a panel by each column's value in turn"
      (is (= ["a / x" "b / y"]
             (mapv :col-label (-> data (pj/lay-point :x :y) (pj/facet [:g :h])
                                  pj/plan :panels)))))

    (testing "a typoed column inside a compound facet is reported"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Facet column :speices.*not found"
                            (-> data (pj/lay-point :x :y) (pj/facet [:g :speices]) pj/plan))))

    (testing "valid facet column still works"
      (is (= 2 (-> data (pj/lay-point :x :y) (pj/facet :g) pj/plan :panels count))))

    (testing "facet-grid with valid columns still works"
      (is (= 4 (-> data (pj/lay-point :x :y) (pj/facet-grid :g :h) pj/plan :panels count))))

    (testing "typoed facet-grid column throws"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Facet column :speices.*not found"
                            (-> data (pj/lay-point :x :y) (pj/facet-grid :speices :h) pj/plan))))))

(deftest lay-method-auto-infer-test
  ;; persona-16 B4. Closes Skept-R4 F1, P5-R2 C2.
  (let [four-col {:a [1 2 3] :b [4 5 6] :c [7 8 9] :d [10 11 12]}
        three-col {:x [1 2 3] :y [10 20 30] :g ["a" "b" "c"]}]
    (testing "4+ column auto-infer throws with helpful message"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Cannot auto-infer columns from 4 columns"
                            (-> four-col pj/lay-point pj/plan))))

    (testing "error message suggests explicit x/y"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"pj/lay-point data :x :y"
                            (-> four-col pj/lay-point pj/plan))))

    (testing "error message lists available columns"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Available columns: \(:a :b :c :d\)"
                            (-> four-col pj/lay-point pj/plan))))

    (testing "3-column auto-infer still works"
      (is (= 1 (-> three-col pj/lay-point pj/plan :panels count))))

    (testing "4+ column with explicit x/y still works"
      (is (= 1 (-> four-col (pj/lay-point :a :b) pj/plan :panels count))))))

(deftest valued-tile-domain-reaches-outer-edges-test
  ;; Issue #59: a tile on a numeric axis spans half a step either side
  ;; of its value, and the domain was read from the values alone, so
  ;; the outer rows and columns were cut to a fraction of the others.
  (let [plan (-> (for [i (range 5) j (range 5)] {:xi i :yi j :r (* i j)})
                 (pj/lay-tile :xi :yi {:fill :r})
                 pj/plan)
        panel (-> plan :panels first)
        tiles (-> panel :layers first :tiles)
        [x-lo x-hi] (:x-domain panel)
        [y-lo y-hi] (:y-domain panel)]
    (testing "the domain holds every tile's edges"
      (is (<= x-lo (reduce min (map :x-lo tiles))))
      (is (>= x-hi (reduce max (map :x-hi tiles))))
      (is (<= y-lo (reduce min (map :y-lo tiles))))
      (is (>= y-hi (reduce max (map :y-hi tiles)))))
    (testing "the tiles span half the step either side of their values"
      (is (= [-0.5 0.5] ((juxt :x-lo :x-hi) (first tiles)))))))

(deftest tile-categorical-color-test
  ;; Issue #40: `:fill` refuses a column of categories and suggests
  ;; `:color`, and a tile given a categorical `:color` died in extract
  ;; with a ClassCastException -- the column was renamed to `:fill`
  ;; and its minimum taken. Through v0.15.0.
  (let [d {:hour [1 2 3 1 2 3] :day [1 1 1 2 2 2]
           :shift ["early" "late" "early" "late" "early" "late"]}
        rgb (fn [[r g b]] [(Math/round (* 255.0 r)) (Math/round (* 255.0 g)) (Math/round (* 255.0 b))])
        cells (fn [plan] (->> plan :panels first :layers first :tiles
                              (map (juxt :x-lo :y-lo (comp rgb :color)))
                              (sort-by (juxt second first))))]
    (testing "each cell takes its row's category colour, and the legend names both"
      (let [plan (-> d (pj/lay-tile :hour :day {:color :shift}) pj/plan)
            early [228 26 28]
            late [55 126 184]]
        (is (= [[0.5 0.5 early] [1.5 0.5 late] [2.5 0.5 early]
                [0.5 1.5 late] [1.5 1.5 early] [2.5 1.5 late]]
               (cells plan)))
        (is (= ["early" "late"] (mapv :label (-> plan :legend :entries))))))
    (testing "a palette written with :values is used"
      (let [plan (-> d
                     (pj/lay-tile :hour :day {:color :shift})
                     (pj/scale :color {:values {"early" "#FF0000" "late" "#0000FF"}})
                     pj/plan)]
        (is (= #{[255 0 0] [0 0 255]} (set (map last (cells plan)))))))
    (testing ":fill still refuses categories, and names :color"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"map the column to :color"
                            (-> d (pj/lay-tile :hour :day {:fill :shift}) pj/plan))))))

(deftest tile-color-synonym-test
  ;; persona-11-R2 F8: lay-tile used to silently paint every tile the
  ;; midpoint color when the user passed {:color :value} instead of
  ;; {:fill :value}. Now :color is accepted as a synonym (only when
  ;; stat is :bin2d, i.e. from `pj/lay-tile` -- NOT for density2d/
  ;; contour which have their own intentional kde2d stat).
  (let [data (tc/dataset {:row (mapcat #(repeat 6 %) (range 6))
                          :col (flatten (repeat 6 (range 6)))
                          :value (map #(Math/sin (* % 0.5)) (range 36))})]

    (testing "lay-tile with :color produces the same plan as :fill"
      (let [p-color (-> data (pj/pose :col :row {:color :value})
                        pj/lay-tile pj/plan)
            p-fill (-> data (pj/pose :col :row {:fill :value})
                       pj/lay-tile pj/plan)
            tiles-color (-> p-color :panels first :layers first :tiles)
            tiles-fill (-> p-fill :panels first :layers first :tiles)]
        (is (= (count tiles-color) (count tiles-fill))
            "same number of tiles in both paths")
        (is (= (distinct (map :color tiles-color))
               (distinct (map :color tiles-fill)))
            "same distinct colors in both paths")
        (is (> (count (distinct (map :color tiles-color))) 1)
            ":color path produces varying tile colors, not uniform")))

    (testing "lay-density-2d with :color :species (categorical) still works"
      ;; This used to hit the over-broad tile-override; must stay :density-2d.
      (is (some? (-> (tc/dataset {:x [1.0 2.0 3.0 4.0 5.0 6.0 7.0 8.0 9.0 10.0]
                                  :y [1.0 2.1 3.0 4.2 5.1 6.0 7.0 8.1 9.0 10.0]
                                  :g ["a" "a" "a" "a" "a" "b" "b" "b" "b" "b"]})
                     (pj/pose :x :y {:color :g})
                     pj/lay-density-2d pj/plan))))))

(defn- tile-cells
  "Every heatmap cell a pose draws, as its column and row in the grid, the
   share of the panel's width and height it covers, and its fill. Shares
   rather than drawing units, because the axis labels decide how wide the
   panel is. A cell's rect sits a few groups below its translate. The
   legend's gradient steps are 12 wide, so they do not pass the filter."
  [pose]
  (let [nodes (tree-seq vector? seq (pj/plot pose {:format :svg}))
        first-rect (fn [n] (first (filter #(and (vector? %) (= :rect (first %)))
                                          (tree-seq vector? seq n))))
        bg (some #(when (and (vector? %) (= :rect (first %))
                             (= "rgb(232,232,232)" (:fill (second %))))
                    (second %))
                 nodes)
        cells (keep (fn [n]
                      (when (and (vector? n) (= :g (first n)) (:transform (second n)))
                        (let [{:keys [fill width height]} (second (first-rect (nth n 2 nil)))]
                          (when (and fill (not= fill "rgb(232,232,232)")
                                     (> (double width) 20))
                            (let [[tx ty] (map parse-double
                                               (re-seq #"[-\d.]+" (:transform (second n))))]
                              {:tx tx :ty ty :w width :h height :fill fill})))))
                    nodes)
        rank (fn [k] (zipmap (sort (distinct (map k cells))) (range)))
        col (rank :tx)
        row (rank :ty)
        share (fn [a b] (/ (Math/round (* 100.0 (/ (double a) (double b)))) 100.0))]
    (set (for [{:keys [tx ty w h fill]} cells]
           [(col tx) (row ty) (share w (:width bg)) (share h (:height bg)) fill]))))

(deftest tile-categorical-axes-test
  ;; Through v0.14.0 a tile read each row's cell as its value plus or
  ;; minus half the smallest step, and a category has no step: every
  ;; named axis died with a ClassCastException in extract/min-step.
  (let [values [0.1 0.5 0.9 -0.2]
        numeric {:a [1 1 2 2] :b [1 2 1 2] :v values}
        ;; Where each cell sits and what colour it is. The share of the
        ;; panel a cell spans is asserted separately below, because it
        ;; differs by axis kind: a numeric axis is padded past the
        ;; tiles' edges, as every numeric axis is, and a band axis is
        ;; not.
        cells (fn [data & {:keys [flip? opts]}]
                (set (map (fn [[c r _ _ fill]] [c r fill])
                          (tile-cells (cond-> (pj/lay-tile data :a :b (merge {:fill :v} opts))
                                        flip? (pj/coord :flip))))))
        spans (fn [data & {:keys [opts]}]
                (set (map (fn [[_ _ w h _]] [w h])
                          (tile-cells (pj/lay-tile data :a :b (merge {:fill :v} opts))))))
        expected (cells numeric)]
    (testing "the numeric grid draws four cells, the reference below"
      (is (= 4 (count expected))))
    (testing "named axes draw the cells a numeric grid of the same shape draws"
      (doseq [[nm data] {"strings" {:a ["p" "p" "q" "q"] :b ["u" "v" "u" "v"] :v values}
                         "keywords" {:a [:p :p :q :q] :b [:u :v :u :v] :v values}
                         "one of each" {:a ["p" "p" "q" "q"] :b [1 2 1 2] :v values}}]
        (is (= expected (cells data)) nm)))
    (testing "a numeric column declared categorical"
      (is (= expected (cells {:a [10 10 20 20] :b [1 2 1 2] :v values}
                             :opts {:x-type :categorical}))))
    (testing "under a flip, a category is placed on the axis that draws it"
      (is (= (cells numeric :flip? true)
             (cells {:a ["p" "p" "q" "q"] :b [1 2 1 2] :v values} :flip? true))))
    (testing "every cell is the same size: a band of two, or a step padded 5% each side"
      ;; 1 / 2.2 of the panel on a numeric axis (#59: the outer cells
      ;; used to be clipped), one half on a band axis.
      (is (= #{[0.45 0.45]} (spans numeric)))
      (is (= #{[0.5 0.5]} (spans {:a ["p" "p" "q" "q"] :b ["u" "v" "u" "v"] :v values})))
      (is (= #{[0.5 0.45]} (spans {:a ["p" "p" "q" "q"] :b [1 2 1 2] :v values}))))))

(deftest mixed-type-column-test
  ;; persona-skeptical-round-4 F5: a column whose values are heterogeneous
  ;; (number + string + keyword) used to crash with a multi-KB Malli
  ;; "Plan does not conform to schema" dump. Now thrown with a clear
  ;; column name and the discovered types.
  (testing "mixed-type :y column throws with type list"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"has mixed value types"
                          (-> (tc/dataset {:x [1 2 3 4 5]
                                           :y [1.0 "two" 3.0 :four 5.0]})
                              (pj/lay-point :x :y) pj/plan))))

  (testing "all-string column is fine (categorical)"
    (is (some? (-> {:x [1 2 3] :g ["a" "b" "c"]} (pj/lay-bar :g) pj/plan))))

  (testing "all-numeric column is fine"
    (is (some? (-> {:x [1.0 2.0 3.0] :y [4.0 5.0 6.0]}
                   (pj/lay-point :x :y) pj/plan)))))

(deftest aesthetic-cross-type-lookup-throws-test
  ;; Strict matching: a keyword reference does not satisfy a string
  ;; column name and vice versa. Position references that mismatch
  ;; throw at validation time. String :color references that do not
  ;; match any column fall through to literal CSS color (string
  ;; :color is the documented disambiguation case).
  (testing "string-keyed dataset + keyword position throws"
    (let [str-ds (tc/dataset {"x" [1.0 2.0 3.0]
                              "y" [10.0 20.0 30.0]
                              "g" ["A" "B" "A"]})]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"not found in dataset"
                            (-> str-ds (pj/lay-point :x :y {:color :g}) pj/plan)))))
  (testing "keyword-keyed dataset + string position throws"
    (let [kw-ds (tc/dataset {:x [1.0 2.0 3.0] :y [10.0 20.0 30.0] :g ["A" "B" "A"]})]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"not found in dataset"
                            (-> kw-ds (pj/lay-point "x" "y" {:color "g"}) pj/plan)))))
  (testing "keyword-keyed dataset + string :color that is a valid CSS color falls through"
    ;; \"red\" doesn't match any column, so :color is treated as a
    ;; literal CSS color -- the plot renders without grouping.
    (let [kw-ds (tc/dataset {:x [1.0 2.0 3.0] :y [10.0 20.0 30.0] :g ["A" "B" "A"]})
          pl (-> kw-ds (pj/lay-point :x :y {:color "red"}) pj/plan)]
      (is (= 1 (count (:panels pl)))))))

(deftest layer-x-y-override-test
  ;; persona-skeptical-round-4 F9: layer-level :x/:y overrides used to
  ;; warn as unknown options even though they actually worked. Now :x/:y
  ;; are accepted on layer opts (universal-layer-options).
  (testing "layer-level :x / :y on lay-* doesn't trigger an unknown-opt warning"
    (let [out (with-out-str
                (-> {:a [1 2 3] :b [4 5 6] :c [7 8 9] :d [10 11 12]}
                    (pj/pose :a :b)
                    (pj/lay-point {:x :c :y :d})))]
      (is (not (re-find #"does not recognize option" out))))))

(deftest plot-level-keys-stripped-from-wrong-scope-test
  ;; persona-skeptical-round-6 F1/F5/F6: plot-level keys like
  ;; :x-scale and :coord used to emit an "unrecognized option"
  ;; warning but still propagate into the layer's or pose's
  ;; mapping and leak into the final panel (identical output to
  ;; the canonical pj/scale / pj/coord form). Now the warning is
  ;; honest: unknown keys are stripped from the mapping, so the
  ;; panel uses default scales/coord.
  (let [ds {:x [1 10 100] :y [1 2 3]}]

    (testing "layer-level :x-scale is stripped, not honored"
      (let [fr (-> ds (pj/pose :x :y) (pj/lay-point {:x-scale {:type :log}}))
            layer-mapping (:mapping (first (:layers fr)))
            panel (first (:panels (pj/plan fr)))]
        (is (not (contains? (or layer-mapping {}) :x-scale))
            ":x-scale should not appear in layer mapping")
        (is (= :linear (get-in panel [:x-scale :type]))
            "panel x-scale should stay at default :linear")))

    (testing "pose-level :x-scale is stripped, not honored"
      (let [fr (-> ds (pj/pose :x :y {:x-scale {:type :log}}) pj/lay-point)
            panel (first (:panels (pj/plan fr)))]
        (is (not (contains? (:mapping fr) :x-scale))
            ":x-scale should not appear in pose mapping")
        (is (= :linear (get-in panel [:x-scale :type]))
            "panel x-scale should stay at default :linear")))

    (testing "layer-level :coord is stripped, not honored"
      (let [fr (-> ds (pj/pose :x :y) (pj/lay-point {:coord :flip}))
            layer-mapping (:mapping (first (:layers fr)))
            panel (first (:panels (pj/plan fr)))]
        (is (not (contains? (or layer-mapping {}) :coord))
            ":coord should not appear in layer mapping")
        (is (not= :flip (:coord panel))
            "panel coord should not reflect a stripped layer-level :flip")))

    (testing "canonical pj/scale still works"
      (let [fr (-> ds (pj/pose :x :y) pj/lay-point (pj/scale :x :log))
            panel (first (:panels (pj/plan fr)))]
        (is (= :log (get-in panel [:x-scale :type])))))

    (testing "canonical pj/coord still works"
      (let [fr (-> ds (pj/pose :x :y) pj/lay-point (pj/coord :flip))
            panel (first (:panels (pj/plan fr)))]
        (is (= :flip (:coord panel)))))))

(deftest input-validation-misc-test
  ;; persona-09-R2 F10/F11/F12. Low-severity input validation.
  (testing "options with width/height = 0 throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":width must round to a positive integer"
                          (-> {:a [1 2 3] :b [4 5 6]} (pj/lay-point :a :b)
                              (pj/options {:width 0}))))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":height must round to a positive integer"
                          (-> {:a [1 2 3] :b [4 5 6]} (pj/lay-point :a :b)
                              (pj/options {:height -100})))))

  (testing "options with fractional width that rounds to 0 throws (persona R2 internals)"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":width must round to a positive integer"
                          (-> {:a [1 2 3] :b [4 5 6]} (pj/lay-point :a :b)
                              (pj/options {:width 0.4})))))

  (testing "histogram with :bins <= 0 throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":bins must be a positive number"
                          (-> {:x [1 2 3]} (pj/lay-histogram :x {:bins 0}) pj/plan)))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":bins must be a positive number"
                          (-> {:x [1 2 3]} (pj/lay-histogram :x {:bins -1}) pj/plan))))

  (testing "histogram with :binwidth <= 0 throws"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":binwidth must be a positive number"
                          (-> {:x [1 2 3]} (pj/lay-histogram :x {:binwidth 0}) pj/plan))))

  (defn- drawn-bar-spans
    "The x-span of every filled shape drawn in `color`, from the rendered
   tree rather than the printed SVG -- a shape sits inside a
   `<g transform>`, so a printed coordinate is not a canvas one. A span
   is a difference and so survives the translation either way."
    [fr color]
    (->> (tree-seq vector? seq (pj/plot fr))
         (filter #(and (vector? %) (map? (second %))))
         (filter #(= color (:fill (second %))))
         (keep (fn [[_ attrs]]
                 (when-let [pts (:points attrs)]
                   (let [xs (take-nth 2 (map #(Double/parseDouble %)
                                             (re-seq #"[-0-9.]+"
                                                     (clojure.string/replace pts "," " "))))]
                     (- (apply max xs) (apply min xs))))))
         vec))

  (deftest a-histogram-of-one-distinct-value-draws-a-visible-bar
  ;; fastmath answers a constant column with a single bin whose :min and
  ;; :max are the value, and the bar came out zero units wide -- a shape
  ;; svg-summary counts and no reader can see. ggplot2 draws a visible
  ;; bar for the same data. A written `{:x 2}` is broadcast into a
  ;; constant column before any stat runs, so it arrives here as the
  ;; same thing and one rule covers both.
    (let [red "rgb(204,51,17)"
          panel-width (fn [fr] (let [[_ _ w _] (-> fr pj/frames :panels first :frames :drawing-area)] (double w)))]

      (testing "a constant column draws a bar, not a zero-width shape"
        (let [fr (-> {:v [2 2 2]} (pj/lay-histogram :v {:color "#cc3311"}))
              spans (drawn-bar-spans fr red)]
          (is (= 1 (count spans)))
          (is (pos? (first spans)) "the defect: this was 0.0")
        ;; The width is pad-domain's own degenerate padding, halved, so
        ;; the bar fills half the panel the axis built around it.
          (is (< (Math/abs (- (first spans) (/ (panel-width fr) 2.0))) 1.0))))

      (testing "the same holds on the :bins path, which also goes through fastmath"
        (let [spans (drawn-bar-spans (-> {:v [2 2 2]} (pj/lay-histogram :v {:bins 5 :color "#cc3311"})) red)]
          (is (= 1 (count spans)))
          (is (pos? (first spans)))))

      (testing "the padding is relative, so a constant million is not given a hairline"
        (let [fr (-> {:v [1000000 1000000]} (pj/lay-histogram :v {:color "#cc3311"}))
              spans (drawn-bar-spans fr red)]
          (is (= 1 (count spans)))
          (is (< (Math/abs (- (first spans) (/ (panel-width fr) 2.0))) 1.0))))

      (testing "an explicit :binwidth still decides the width itself"
      ;; This path builds its own edges and was never affected, so the
      ;; bar is the user's width and not half the panel.
        (let [fr (-> {:v [2 2 2]} (pj/lay-histogram :v {:binwidth 0.5 :color "#cc3311"}))
              spans (drawn-bar-spans fr red)]
          (is (= 1 (count spans)))
          (is (< (first spans) (/ (panel-width fr) 2.0)))))

      (testing "a value written on a histogram draws one bar, one place wide"
      ;; The categorical case the 0.14.0 notes describe: a written value
      ;; reaches the same constant column, and the bar comes out one
      ;; band step across, as pj/lay-bar draws at a written place.
        (let [bars (-> {:team ["red" "green" "blue"] :score [3 5 4]}
                       (pj/lay-bar :team :score {:color "#a6cee3"}))
              spans (drawn-bar-spans (pj/lay-histogram bars {:x 2 :color "#cc3311"}) red)
              panel (-> bars pj/frames :panels first)
              step (- (first (pj/to-drawing panel 2 1)) (first (pj/to-drawing panel 1 1)))]
          (is (= 1 (count spans)))
          (is (< (Math/abs (- (first spans) step)) 1.0))))

      (testing "a histogram of data that varies is unchanged"
        (let [counts (fn [n] (:polygons (pj/svg-summary
                                         (pj/plot (-> {:v [1 2 3 4 5 2 3 3]}
                                                      (pj/lay-histogram :v {:bins n}))))))]
          (is (pos? (counts 4)))
          (is (= (counts 4) (counts 4)))))))

  (testing "lay-rule-h/v 1-arity throws helpful error pointing at the required intercept opt"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"requires an opts map with :y-intercept"
                          (pj/lay-rule-h (pj/pose))))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"requires an opts map with :x-intercept"
                          (pj/lay-rule-v (pj/pose)))))

  (testing "lay-band-h/v 1-arity throws helpful error pointing at min/max opts"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"requires an opts map with :y-min and :y-max"
                          (pj/lay-band-h (pj/pose))))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"requires an opts map with :x-min and :x-max"
                          (pj/lay-band-v (pj/pose)))))

  (testing "lay-* with unknown :position throws (parallel to :mark/:stat validation)"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #":position :nonsense.*not a registered position"
                          (pj/lay-point {:a [1 2] :b [3 4]} :a :b {:position :nonsense})))))

(deftest svg-summary-aesthetic-coverage-test
  ;; persona round-2 (test-quality auditor) flagged that notebook
  ;; tests asserting only :points / :lines / :panels could pass even
  ;; if a :color "#hex" / :size N / :alpha N / :shape :col mapping
  ;; were silently dropped (the regression class that bit
  ;; tile-color-synonym-test). pj/svg-summary now extracts data-mark
  ;; aesthetic variety so a single predicate catches each failure
  ;; mode.
  (let [iris-ds (tc/dataset {:x (range 60)
                             :y (mapv (partial * 2) (range 60))
                             :g (concat (repeat 20 "a") (repeat 20 "b") (repeat 20 "c"))})
        base    (-> iris-ds (pj/lay-point :x :y))]
    (testing ":colors set is non-empty even on a default plot"
      (is (seq (:colors (pj/svg-summary base)))))

    (testing "explicit :color literal appears in :colors set"
      (let [s (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:color "#E74C3C"})))]
        (is (contains? (:colors s) "rgb(231,76,60)")
            "the literal #E74C3C must reach the rendered SVG")))

    (testing ":color column mapping produces strictly more colors than the default"
      (let [s-default (pj/svg-summary base)
            s-mapped  (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:color :g})))]
        (is (> (count (:colors s-mapped)) (count (:colors s-default)))
            "mapping :color :g (3 categories) must add colors beyond default")))

    (testing "explicit :size literal appears in :sizes set"
      (let [s (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:size 12})))]
        (is (contains? (:sizes s) 12.0)
            "the literal :size 12 must reach the rendered :rx")))

    (testing "explicit :alpha literal appears in :alphas set"
      (let [s (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:alpha 0.3})))]
        (is (contains? (:alphas s) 0.3)
            "the literal :alpha 0.3 must reach the rendered fill-opacity")))

    (testing ":shape column mapping produces multiple SVG primitive types"
      (let [s-default (pj/svg-summary base)
            s-mapped  (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:shape :g})))]
        (is (= 1 (count (:shapes s-default)))
            "default plot uses a single primitive type")
        (is (> (count (:shapes s-mapped)) 1)
            "mapping :shape :g (3 categories) must produce multiple primitive types")))

    (testing "every marker is counted when :shape is mapped"
      ;; A :square marker draws as a rounded rect of radius 0. Requiring a
      ;; positive rx to count a point dropped all 20 squares from the
      ;; summary: they failed that test and the nil-rx test for tiles, so
      ;; they landed in no bucket and 60 rows summarized as 40 marks.
      (let [s (pj/svg-summary (-> iris-ds (pj/lay-point :x :y {:shape :g})))]
        (is (= 60 (+ (:points s) (:polygons s)))
            "circles and squares count as :points, triangles as :polygons")
        (is (zero? (:tiles s))
            "square markers must not be mistaken for heatmap tiles")))))

(deftest alpha-on-marks-test
  ;; persona-16 H4 + H7. Closes P11-R2 F1, F2.
  ;; H4: line/step/lm/loess/errorbar/lollipop -- alpha was put in style but
  ;;     hardcoded to 1.0 in the renderer.
  ;; H7: boxplot/pointrange/text/label -- alpha wasn't in style at all.
  (let [data {:x [1.0 2.0 3.0 4.0 5.0] :y [10.0 20.0 15.0 25.0 18.0]
              :lab ["A" "B" "C" "D" "E"]
              :cat ["a" "b" "a" "b" "a"]}
        tmp (java.io.File/createTempFile "plotje-alpha-test" ".svg")
        renders-with-opacity? (fn [sketch op-pattern]
                                (pj/save sketch (.getAbsolutePath tmp))
                                (boolean (re-find op-pattern (slurp tmp))))]
    (testing "alpha 0.5 propagates to :line"
      (is (renders-with-opacity? (-> data (pj/lay-line :x :y {:alpha 0.5}))
                                 #"opacity=\"0.5\"")))

    (testing "alpha 0.5 propagates to :step"
      (is (renders-with-opacity? (-> data (pj/lay-step :x :y {:alpha 0.5}))
                                 #"opacity=\"0.5\"")))

    (testing "alpha 0.5 propagates to :lm regression line"
      (is (renders-with-opacity? (-> data (pj/lay-smooth :x :y {:stat :linear-model :alpha 0.5}))
                                 #"opacity=\"0.5\"")))

    (testing "alpha 0.4 propagates to :errorbar"
      (is (renders-with-opacity? (-> {:x [1.0 2.0] :y [10.0 20.0]
                                      :lo [5.0 15.0] :hi [15.0 25.0]}
                                     (pj/lay-errorbar :x :y {:y-min :lo :y-max :hi :alpha 0.4}))
                                 #"opacity=\"0.4\"")))

    (testing "alpha 0.6 propagates to :lollipop"
      (is (renders-with-opacity? (-> data (pj/lay-lollipop :cat :y {:alpha 0.6}))
                                 #"opacity=\"0.6\"")))

    (testing "alpha 0.3 propagates to :text"
      (is (renders-with-opacity? (-> data (pj/lay-text :x :y {:text :lab :alpha 0.3}))
                                 #"opacity=\"0.3\"")))

    (testing "alpha 0.7 propagates to :label"
      (is (renders-with-opacity? (-> data (pj/lay-label :x :y {:text :lab :alpha 0.7}))
                                 #"opacity=\"0.7\"")))

    (testing "alpha 0.4 propagates to :boxplot"
      (is (renders-with-opacity? (-> data (pj/lay-boxplot :cat :y {:alpha 0.4}))
                                 #"opacity=\"0.4\"")))

    (testing "alpha 0.8 propagates to :pointrange (via :summary)"
      (is (renders-with-opacity? (-> data (pj/lay-summary :cat :y {:alpha 0.8}))
                                 #"opacity=\"0.8\"")))

    (.delete tmp)))

(deftest bar-numeric-x-test
  ;; persona-16 H9. Closes P9-R2 F7.
  (testing "lay-bar counting with numeric x throws clear error"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"lay-bar \(counting\) requires a categorical column for :x"
                          (-> {:x [1 2 3 4 5]} (pj/lay-bar :x) pj/plan))))

  (testing "lay-bar value bars with two numeric axes draw numeric-position bars"
    ;; (see numeric-position-bar-test) -- no longer an error
    (is (= 3 (:polygons (pj/svg-summary
                         (pj/plot (-> {:x [1.0 2.0 3.0] :y [10 20 30]}
                                      (pj/lay-bar :x :y))))))))

  (testing "the numeric-x error suggests the :x-type override"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"\{:x-type :categorical\}"
                          (-> {:x [1 2 3 4 5]} (pj/lay-bar :x) pj/plan))))

  (testing "lay-bar counting with categorical x still works"
    (is (some? (-> {:cat ["a" "b" "c"]} (pj/lay-bar :cat) pj/plan))))

  (testing "lay-bar value bars with categorical x still works"
    (is (some? (-> {:cat ["a" "b" "c"] :y [10 20 30]}
                   (pj/lay-bar :cat :y) pj/plan))))

  (testing "{:x-type :categorical} lets lay-bar accept a numeric x"
    (is (some? (-> {:x [1 2 3] :y [10 20 30]}
                   (pj/lay-bar :x :y {:x-type :categorical}) pj/plan)))))

(deftest horizontal-value-bar-test
  (let [rank {:country ["US" "China" "Japan"] :gdp [21.4 14.7 5.1]}
        grp {:cat ["A" "A" "B" "B"] :val [10 20 30 40] :g ["x" "y" "x" "y"]}]
    (testing "category on y draws horizontal value bars (band scale on y)"
      (let [p (-> rank (pj/lay-bar :gdp :country) pj/plan :panels first)]
        (is (true? (:categorical? (:y-ticks p))))
        (is (not (:categorical? (:x-ticks p))))
        ;; numeric (value) axis is x, anchored at/through 0
        (is (<= (double (first (:x-domain p))) 0.0))))
    (testing "vertical bars unchanged (band scale on x)"
      (let [p (-> rank (pj/lay-bar :country :gdp) pj/plan :panels first)]
        (is (true? (:categorical? (:x-ticks p))))
        (is (not (:categorical? (:y-ticks p))))))
    (testing "grouped horizontal bars dodge"
      (is (= 4 (:polygons (pj/svg-summary
                           (pj/plot (pj/lay-bar grp :val :cat
                                                {:color :g :position :dodge})))))))
    (testing "horizontal stack/fill redirect to coord :flip with a clear error"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Stacked/filled horizontal value bars.*coord :flip"
                            (-> grp (pj/lay-bar :val :cat {:color :g :position :stack}) pj/plan)))
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Stacked/filled horizontal value bars"
                            (-> grp (pj/lay-bar :val :cat {:color :g :position :fill}) pj/plan))))))

(deftest numeric-position-bar-test
  (testing "numeric x + numeric y draws one bar per row at its x position"
    (let [p (-> {:x [1 2 3 4 5] :y [10 20 15 30 25]} (pj/lay-bar :x :y) pj/plan
                :panels first)]
      (is (not (:categorical? (:x-ticks p))))
      ;; x-domain widened by half a bar each side (gap 1, width 0.9 -> half 0.45)
      ;; plus padding, so it extends below the data minimum of 1.0
      (is (< (double (first (:x-domain p))) 1.0))
      (is (= 5 (:polygons (pj/svg-summary
                           (pj/plot (-> {:x [1 2 3 4 5] :y [10 20 15 30 25]}
                                        (pj/lay-bar :x :y)))))))))
  (testing "{:bar-width ...} overrides the inferred width"
    (let [wide (-> {:x [1 2 3] :y [10 20 15]} (pj/lay-bar :x :y {:bar-width 0.5})
                   pj/plan :panels first :x-domain)
          [lo hi] wide]
      ;; half-width 0.25 -> domain spans roughly [0.75, 3.25] before padding
      (is (< 0.5 (double lo) 0.8))
      (is (< 3.2 (double hi) 3.5))))
  (testing "{:bar-width ...} on a categorical axis sets the band fraction"
    ;; On a categorical axis a bar has no data units to be wide in, so
    ;; :bar-width is the fraction of the category band it fills -- the
    ;; same quantity :box-width names for a box. The default is 0.8.
    (let [band-extent
          (fn [pose]
            (->> (tree-seq vector? seq (pj/plot pose {:format :svg}))
                 (filter #(and (vector? %) (= :polygon (first %))))
                 (map (comp :points second))
                 (mapv (fn [pts]
                         (let [ys (->> (str/split pts #"\s+")
                                       (map #(parse-double (second (str/split % #",")))))]
                           (- (apply max ys) (apply min ys)))))
                 first))
          cohorts [{:cohort "a" :growth 1.0} {:cohort "b" :growth 2.0}]
          wide (band-extent (pj/lay-bar cohorts :growth :cohort))
          thin (band-extent (pj/lay-bar cohorts :growth :cohort {:bar-width 0.4}))]
      ;; 0.4 against the 0.8 default halves the bar, and the band is unmoved.
      (is (< 0.49 (/ thin wide) 0.51))))
  (testing "numeric bars support negative heights (diverging)"
    (is (= 3 (:polygons (pj/svg-summary
                         (pj/plot (-> {:x [1 2 3] :y [-10 20 -5]} (pj/lay-bar :x :y))))))))
  (testing "temporal x draws bars with calendar tick labels"
    (let [p (-> {:date [#inst "2024-01-01" #inst "2024-02-01" #inst "2024-03-01"] :v [10 20 15]}
                (pj/lay-bar :date :v) pj/plan :panels first)]
      (is (not (:categorical? (:x-ticks p))))
      (is (= 3 (:polygons (pj/svg-summary
                           (pj/plot (-> {:date [#inst "2024-01-01" #inst "2024-02-01" #inst "2024-03-01"] :v [10 20 15]}
                                        (pj/lay-bar :date :v)))))))))
  (testing "a bare pose with temporal x still infers a line, not bars"
    (is (= :line (-> {:date [#inst "2024-01-01" #inst "2024-02-01"] :v [10 20]}
                     (pj/pose :date :v) pj/plan :panels first :layers first :mark)))))

(deftest value-bar-log-scale-error-test
  ;; Value bars rest on a zero baseline, which a log value-axis cannot
  ;; represent. Instead of an internal pad-domain invariant crash, the user
  ;; gets a clear, actionable message pointing at the alternatives.
  (let [re #"log scale cannot include zero or negative"]
    (testing "vertical value bars on a log y axis"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo re
                            (-> {:c ["a" "b" "c"] :v [10 100 1000]}
                                (pj/lay-bar :c :v) (pj/scale :y :log) pj/plan))))
    (testing "numeric-position bars on a log y axis"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo re
                            (-> {:x [1 2 3] :y [10 100 1000]}
                                (pj/lay-bar :x :y) (pj/scale :y :log) pj/plan))))
    (testing "horizontal value bars on a log x axis"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo re
                            (-> {:c ["a" "b" "c"] :v [10 100 1000]}
                                (pj/lay-bar :v :c) (pj/scale :x :log) pj/plan))))
    (testing "count bars and histograms still work on a log axis"
      (is (some? (-> {:c ["a" "a" "b" "c" "c" "c"]} (pj/lay-bar :c)
                     (pj/scale :y :log) pj/plan)))
      (is (some? (-> {:x (range 100)} (pj/lay-histogram :x {:bins 10})
                     (pj/scale :y :log) pj/plan))))))

(deftest lollipop-y-type-categorical-rejected-test
  ;; user-report-3 issue 4: passing :y-type :categorical on a numeric :y
  ;; column previously NPE'd deep in the renderer; the layer requires a
  ;; numerical :y column.
  (testing "lay-lollipop with :y-type :categorical throws clear guidance"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"requires a numerical :y column.*pj/coord :flip"
                          (-> {:cat ["a" "b" "c"] :n [10 5 8]}
                              (pj/lay-lollipop :cat :n {:y-type :categorical})
                              pj/plan))))

  (testing "lay-lollipop without the bogus override still works"
    (is (some? (-> {:cat ["a" "b" "c"] :n [10 5 8]}
                   (pj/lay-lollipop :cat :n)
                   (pj/coord :flip)
                   pj/plan)))))

(deftest x-type-categorical-on-localdate-test
  ;; user-report-3 issue 2: {:x-type :categorical} on a LocalDate column
  ;; previously ClassCastException'd inside filter-infinities because
  ;; :packed-local-date reports as a numeric dtype.
  (testing "lay-lollipop with :x-type :categorical on a LocalDate column"
    (let [d (tc/dataset {:date [(java.time.LocalDate/of 2024 1 1)
                                (java.time.LocalDate/of 2024 6 1)
                                (java.time.LocalDate/of 2024 12 1)]
                         :value [3 8 5]})]
      (is (some? (-> d
                     (pj/lay-lollipop :date :value {:x-type :categorical})
                     (pj/coord :flip)
                     pj/plan))))))

(deftest visual-channel-scales-test
  ;; user-report-3 issue 1: pj/scale extended to :size, :alpha, :fill, :color.
  (let [d (tc/dataset {:user [:a :b :c] :n [10 100 1000]})]

    (testing "pj/scale :size :log produces a log-spaced size legend"
      (let [plan (-> d
                     (pj/lay-point :user :n {:size :n :x-type :categorical})
                     (pj/scale :size :log)
                     pj/plan)
            sl (:size-legend plan)]
        (is (= :log (:scale-type sl)))
        ;; log10(10), log10(100), log10(1000) = 1, 2, 3 are evenly
        ;; spaced, so the middle value sits halfway across the domain.
        ;; Where it lands between the radii is the scale's `:by`, which
        ;; defaults to :sqrt: 2 + 6*sqrt(0.5).
        (is (= [10.0 100.0 1000.0] (mapv :value (:entries sl))))
        (is (= [2.0 6.243 8.0]
               (mapv #(-> (:magnitude %) (* 1000) Math/round (/ 1000.0))
                     (:entries sl))))))

    (testing "and :by :linear spreads the same values evenly across the radii"
      (let [sl (-> d
                   (pj/lay-point :user :n {:size :n :x-type :categorical})
                   (pj/scale :size {:type :log :by :linear})
                   pj/plan
                   :size-legend)]
        (is (= [2.0 5.0 8.0] (mapv :magnitude (:entries sl))))))

    (testing "pj/scale :alpha :log produces a log-spaced alpha legend"
      (let [plan (-> d
                     (pj/lay-point :user :n {:alpha :n :x-type :categorical})
                     (pj/scale :alpha :log)
                     pj/plan)
            al (:alpha-legend plan)]
        (is (= :log (:scale-type al)))
        (is (= [10.0 100.0 1000.0] (mapv :value (:entries al))))))

    (testing "linear remains the default and is unchanged"
      (let [plan (-> d
                     (pj/lay-point :user :n {:size :n :x-type :categorical})
                     pj/plan)]
        (is (= :linear (:scale-type (:size-legend plan))))))

    (testing "non-positive values warn under :size :log (mirroring axis behavior)"
      (let [d-zero (tc/dataset {:user [:a :b :c :d] :n [10 100 1000 0]})
            out (with-out-str
                  (-> d-zero
                      (pj/lay-point :user :n {:size :n :x-type :categorical})
                      (pj/scale :size :log)
                      pj/plan))]
        (is (re-find #"non-positive values \(log scale on :n\)" out))))

    (testing ":categorical rejected on continuous visual channels"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"The aesthetic :size is continuous"
                            (pj/scale (pj/pose d) :size :categorical)))
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"The aesthetic :fill is continuous"
                            (pj/scale (pj/pose d) :fill :categorical)))))

  (testing ":fill :log on a tile heatmap plans cleanly with log scale-type"
    (let [d (tc/dataset (for [r (range 10) c (range 10)]
                          {:r r :c c :v (Math/pow 10.0 (/ (+ r c) 4.0))}))
          plan (-> d
                   (pj/lay-tile :r :c {:fill :v})
                   (pj/scale :fill :log)
                   pj/plan)
          legend (:legend plan)]
      (is (= :continuous (:type legend)))
      (is (= :log (:scale-type legend)))
      (is (seq (:ticks legend)) "log-spaced tick labels populated")))

  (testing "user-report-3 repro: pj/plot succeeds on size :log"
    (let [d (tc/dataset {:user [:a :b :c] :n [10 100 1000]})]
      (is (some? (-> d
                     (pj/lay-point :user :n {:size :n :x-type :categorical})
                     (pj/scale :size :log)
                     pj/plot)))))

  (testing "user-report-3 repro: pj/plot succeeds on tile fill :log"
    (let [d (tc/dataset (for [r (range 5) c (range 5)]
                          {:r r :c c :v (Math/pow 10.0 (/ (+ r c) 2.0))}))]
      (is (some? (-> d
                     (pj/lay-tile :r :c {:fill :v})
                     (pj/scale :fill :log)
                     pj/plot))))))

(deftest unknown-option-warning-test
  ;; persona-16 H1. Closes P1-R3 F2/F3/F4, Skept-R4 F4, P5-R2 L4.
  (let [data {:x [1 2 3] :y [10 20 30]}]
    (testing "pj/pose (position + opts) warns on unknown option key"
      (let [out (with-out-str (-> data (pj/pose :x :y {:colour :y}) pj/lay-point))]
        (is (re-find #"Warning: pj/pose does not recognize option" out))
        (is (re-find #":colour" out))))

    (testing "pj/pose (aesthetic-only opts) warns on unknown option key"
      (let [out (with-out-str (pj/pose data {:colour :y}))]
        (is (re-find #"Warning: pj/pose does not recognize option" out))))

    (testing "pj/options warns on unknown option key"
      (let [out (with-out-str (-> data (pj/lay-point :x :y) (pj/options {:titel "Hi"})))]
        (is (re-find #"Warning: pj/options does not recognize option" out))
        (is (re-find #":titel" out))))

    (testing "a retired configuration key is reported like any other"
      ;; `:annotation-dash` documented a dash pattern for reference
      ;; lines and was read by nothing, so a project setting it drew
      ;; solid lines and heard nothing about it. It is gone; a dashed
      ;; rule takes `:stroke-dash` on its layer.
      (let [out (with-out-str (-> data (pj/lay-point :x :y)
                                  (pj/options {:annotation-dash :dashed})))]
        (is (re-find #"Warning: pj/options does not recognize option" out))
        (is (re-find #":annotation-dash" out)))
      ;; The two keys beside it are live, and stay quiet.
      (is (= "" (with-out-str (-> data (pj/lay-point :x :y)
                                  (pj/options {:rule-color "#333"
                                               :band-opacity 0.2}))))))

    (testing "a renamed configuration key is reported by its new name"
      ;; Reported as unrecognized and nothing more, a rename reads as a
      ;; typo: the writer hunts for a misspelling that is not there,
      ;; because the setting exists under another name.
      (let [out (with-out-str (-> data (pj/lay-point :x :y)
                                  (pj/options {:annotation-stroke "#0000ff"})))]
        (is (re-find #"Renamed to :rule-color" out))
        (is (re-find #":annotation-stroke" out))))

    (testing "valid options stay quiet"
      (is (= "" (with-out-str (-> data (pj/pose :x :y {:color :y}))))))))

(deftest layer-mapping-warning-knows-layer-options-test
  ;; A layer's :mapping holds its layer-type options beside its
  ;; aesthetics, so the sub-pose walk has to accept both. Checking
  ;; against mapping keys alone reported every option as a typo.
  (let [data {:x [1 2 3] :y [10 20 30] :z [5 6 7]}]
    (testing "an option the layer type accepts stays quiet"
      ;; The two histograms name different columns, so the panel split
      ;; notes itself -- that is this call saying what it did, not a
      ;; complaint about `:bins`. What must not appear is an option
      ;; warning.
      ;; The split note is said where the split is decided, which is at
      ;; draft time -- capturing around the lay-* calls catches nothing.
      (let [pose (-> data
                     (pj/lay-histogram :x {:bins 3})
                     (pj/lay-histogram :z))
            built (with-out-str pose)
            drawn (with-out-str (pj/plot pose))]
        (is (not (re-find #"(?i)warning" built)))
        (is (not (re-find #"(?i)warning" drawn)))
        (is (not (re-find #":bins" drawn)))
        (is (re-find #"panel of its own" drawn))))

    (testing "a typo on a layer still warns"
      (let [out (with-out-str
                  (pj/plot {:data data
                            :mapping {:x :x :y :y}
                            :layers [{:layer-type :point
                                      :mapping {:colr "red"}}]}))]
        (is (re-find #"layer :mapping has unexpected key" out))
        (is (re-find #":colr" out))))

    (testing "an option another layer type accepts still warns"
      (let [out (with-out-str
                  (pj/plot {:data data
                            :mapping {:x :x :y :y}
                            :layers [{:layer-type :point
                                      :mapping {:bins 5}}]}))]
        (is (re-find #"layer :mapping has unexpected key" out))
        (is (re-find #":bins" out))))))

(deftest raw-data-plan-plot-test
  ;; persona-16 H2. Closes P1-R3 F2/F3, P9-R2 L2.
  (let [data {:x [1 2 3] :y [10 20 30]}]
    (testing "pj/plan on raw data does not throw"
      (is (some? (-> data pj/plan))))

    (testing "pj/plot on raw data does not throw"
      (is (some? (-> data pj/plot))))))

(deftest unknown-layer-type-test
  ;; persona-16 H3. Closes Skept-R4 F3.
  (let [data {:x [1 2 3] :y [10 20 30]}]
    (testing "unknown layer type keyword throws with registered list"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Unknown layer type: :stackedbar.*Registered layer types"
                            (-> data (pj/pose :x :y) (pj/lay :stackedbar) pj/plan))))))

(deftest scale-type-validation-test
  ;; persona-16 H12. Closes P1-R3 F5.
  (let [data {:x [1 2 3] :y [10 20 30]}]
    (testing "pj/scale with bogus type throws at API call time"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo
                            #"Unknown scale type: :bogus.*linear.*log"
                            (-> data (pj/lay-point :x :y) (pj/scale :x :bogus)))))

    (testing "pj/scale :linear and :log accepted"
      (is (some? (-> data (pj/lay-point :x :y) (pj/scale :x :linear))))
      (is (some? (-> data (pj/lay-point :x :y) (pj/scale :y :log)))))))

(deftest keyword-category-label-test
  ;; persona-16 H10. Closes P17 #9, #11.
  (let [data {:cat [:widget :gadget :sprocket] :n [10 20 15]}]
    (testing "keyword categorical x-axis labels render without leading colons"
      (let [svg (-> data (pj/lay-bar :cat) pj/plot pr-str)]
        (is (zero? (count (re-seq #":(widget|gadget|sprocket)" svg))))
        (is (= 3 (count (re-seq #"\"widget\"|\"gadget\"|\"sprocket\"" svg))))))

    (testing "keyword categorical legend labels render without leading colons"
      (let [svg (-> {:x [1 2 3] :y [10 20 30] :g [:cat-0 :cat-1 :cat-2]}
                    (pj/lay-point :x :y {:color :g}) pj/plot pr-str)]
        (is (zero? (count (re-seq #":cat-[0-9]" svg))))))

    (testing "facet strip labels strip keyword colons"
      (let [svg (-> {:x [1 2 3 4] :y [10 20 30 40] :grp [:a :b :a :b]}
                    (pj/lay-point :x :y) (pj/facet :grp) pj/plot pr-str)]
        (is (zero? (count (re-seq #":(a|b)" svg))))))

    (testing "string categories still work (regression check)"
      (let [svg (-> {:cat ["widget" "gadget"] :n [10 20]} (pj/lay-bar :cat) pj/plot pr-str)]
        (is (= 2 (count (re-seq #"\"widget\"|\"gadget\"" svg))))))))

(deftest nil-in-aesthetic-columns-test
  ;; persona-16 B2. Closes C3 epic: P5-R2 C1, P9-R2 F1/F2,
  ;; P7-R2 F1/F2/F3, P11-R2 F5.
  (testing "nil in numeric :size column drops rows cleanly"
    (let [p (-> {:x [1.0 2.0 3.0 4.0] :y [10.0 20.0 30.0 40.0]
                 :sz [1.0 nil 3.0 nil]}
                (pj/lay-point :x :y {:size :sz}) pj/plan)]
      (is (= 1 (count (:panels p))))
      (is (some? (:size-legend p)) "size legend still built from 2 valid rows")))

  (testing "nil in numeric :color column drops rows cleanly"
    (let [p (-> {:x [1.0 2.0 3.0 4.0] :y [10.0 20.0 30.0 40.0]
                 :c [1.0 2.0 nil 4.0]}
                (pj/lay-point :x :y {:color :c}) pj/plan)]
      (is (= 1 (count (:panels p))))
      (is (some? (:legend p)) "numeric color legend still built")))

  (testing "NaN in numeric :size column drops rows cleanly"
    (let [p (-> {:x [1.0 2.0 3.0] :y [10.0 20.0 30.0]
                 :sz [1.0 ##NaN 3.0]}
                (pj/lay-point :x :y {:size :sz}) pj/plan)]
      (is (= 1 (count (:panels p))))))

  (testing "all-nil :size column renders without legend"
    (let [p (-> {:x [1.0 2.0] :y [10.0 20.0] :sz [nil nil]}
                (pj/lay-point :x :y {:size :sz}) pj/plan)]
      (is (= 1 (count (:panels p))))
      (is (nil? (:size-legend p)) "size legend suppressed when all nil")))

  (testing "all-nil :alpha column renders without legend"
    (let [p (-> {:x [1.0 2.0] :y [10.0 20.0] :a [nil nil]}
                (pj/lay-point :x :y {:alpha :a}) pj/plan)]
      (is (= 1 (count (:panels p))))
      (is (nil? (:alpha-legend p)) "alpha legend suppressed when all nil")))

  (testing "nil in :y-min / :y-max drops rows for errorbar"
    (let [p (-> {:x [1.0 2.0 3.0] :y [10.0 20.0 30.0]
                 :lo [5.0 nil 25.0] :hi [15.0 nil 35.0]}
                (pj/lay-errorbar :x :y {:y-min :lo :y-max :hi}) pj/plan)]
      (is (= 1 (count (:panels p)))))))

(deftest pose-color-mapping-composes-test
  ;; persona-16 B5. Closes P1-R3 F1, P9-R2 F14.
  ;; A color aesthetic on a pose composes with the position mapping.
  (let [data {:x [1 2 3] :y [10 20 30] :g ["a" "b" "c"]}]
    (testing "leaf pose renders with merged color mapping"
      (let [p (-> data (pj/pose :x :y {:color :g}) pj/lay-point pj/plan)
            layer (first (:layers (first (:panels p))))]
        (is (= 3 (count (:groups layer))) "one group per :g value")))))

(deftest facet-broadcast-test
  (testing "Root-scope lay-smooth (loess) applies to all facet panels"
    (let [iris (rdatasets/datasets-iris)
          s (-> iris
                (pj/lay-point :sepal-length :sepal-width {:color :species})
                (pj/facet :species)
                pj/lay-smooth
                pj/plot
                pj/svg-summary)]
      (is (= 3 (:panels s)) "Faceted into 3 panels by species")
      (is (= 3 (:lines s)) "LOESS should appear in all 3 panels")
      (is (= 150 (:points s)) "Scatter points still render")))
  (testing "Existing faceted behavior unchanged"
    (let [s (-> (rdatasets/datasets-iris)
                (pj/lay-point :sepal-length :sepal-width {:color :species})
                (pj/facet :species)
                pj/plot
                pj/svg-summary)]
      (is (= 3 (:panels s)))
      (is (= 150 (:points s))))))

;; ============================================================
;; PROPOSED API () tests
;; ============================================================

(defn- summary
  "Render a pose and return svg-summary."
  [fr]
  (pj/svg-summary (pj/plot fr)))

(deftest basic-test
  (let [iris (tc/dataset "https://raw.githubusercontent.com/mwaskom/seaborn-data/master/iris.csv"
                         {:key-fn keyword})]
    (testing "Scatter + lm"
      (let [s (summary (-> (pj/pose iris {:color :species})
                           (pj/pose :sepal_length :sepal_width)
                           (pj/lay-point {:alpha 0.5})
                           (pj/lay-smooth {:stat :linear-model})))]
        (is (= 1 (:panels s)))
        (is (= 150 (:points s)))
        (is (= 3 (:lines s)))))

    (testing "SPLOM inference"
      (let [s (summary (-> (pj/pose iris {:color :species})
                           (pj/cross-matrix [:sepal_length :sepal_width :petal_length])))]
        (is (= 9 (:panels s)))
        (is (= 900 (:points s)))))

    (testing "Simpson's paradox via nil cancellation"
      (let [s (summary (-> (pj/pose iris {:color :species})
                           (pj/pose :sepal_length :sepal_width)
                           (pj/lay-point {:alpha 0.4})
                           (pj/lay-smooth {:stat :linear-model})
                           (pj/lay-smooth {:stat :linear-model :color nil})))]
        (is (= 1 (:panels s)))
        (is (= 150 (:points s)))
        (is (= 4 (:lines s)))))

    (testing "Faceted + multiple layers"
      (let [s (summary (-> (pj/pose iris)
                           (pj/pose :sepal_length :sepal_width)
                           pj/lay-point
                           pj/lay-smooth
                           (pj/facet :species)))]
        (is (= 3 (:panels s)))
        (is (= 150 (:points s)))
        (is (= 3 (:lines s)))))

    (testing "Data-first (no sketch call)"
      (let [s (summary (-> iris
                           (pj/pose :sepal_length :sepal_width {:color :species})
                           (pj/lay-point {:alpha 0.5})
                           (pj/lay-smooth {:stat :linear-model})))]
        (is (= 1 (:panels s)))
        (is (= 150 (:points s)))
        (is (= 3 (:lines s)))))

    (testing "Recipe"
      (let [recipe (-> (pj/pose)
                       (pj/pose :sepal_length :sepal_width)
                       (pj/lay-point)
                       (pj/lay-smooth {:stat :linear-model}))
            s (summary (pj/with-data recipe iris))]
        (is (= 1 (:panels s)))
        (is (= 150 (:points s)))))

    ;; "Mixed grid" (composite + facet) is deferred. Once two pj/pose
    ;; calls promote the leaf to a composite, pj/facet can't thread
    ;; through -- facet still routes through ensure-sk, which rejects
    ;; composites.

    (testing "Inference: one numerical column"
      (let [s (summary (-> (pj/pose iris)
                           (pj/pose :sepal_length)))]
        (is (pos? (:polygons s)))
        (is (zero? (:points s)))))

    (testing "Inference: one categorical column"
      (let [s (summary (-> (pj/pose iris)
                           (pj/pose :species)))]
        (is (= 3 (:polygons s)))))

    (testing "2D facet grid"
      (let [tips (tc/dataset "https://raw.githubusercontent.com/mwaskom/seaborn-data/master/tips.csv"
                             {:key-fn keyword})
            s (summary (-> (pj/pose tips {:color :smoker})
                           (pj/pose :total_bill :tip)
                           (pj/lay-point {:alpha 0.5})
                           (pj/facet-grid :day :sex)))]
        (is (= 8 (:panels s)))))

    (testing "Options pass through"
      (let [s (summary (-> (pj/pose iris)
                           (pj/pose :sepal_length :sepal_width)
                           (pj/lay-point)
                           (pj/options {:title "Test" :width 400})))]
        (is (= 1 (:panels s)))
        (is (some #{"Test"} (:texts s)))))

    (testing "Pose first, then layers"
      (let [s (summary (-> iris
                           (pj/pose :sepal_length :sepal_width {:color :species})
                           (pj/lay-point {:alpha 0.5})
                           (pj/lay-smooth {:stat :linear-model})))]
        (is (= 1 (:panels s)))
        (is (= 150 (:points s)))
        (is (= 3 (:lines s)))))

    (testing "Column inference"
      ;; Small dataset: pj/lay-point on raw data auto-infers x/y from
      ;; the first two columns (adapter behavior, preserved through
      ;; the Sketch retirement).
      (let [s (summary (pj/lay-point {:a [1 2 3 4 5] :b [2 4 3 5 4]}))]
        (is (= 5 (:points s)))))))

;; ---- Annotations-as-layers (pj/lay-rule-*, pj/lay-band-*) ----

(deftest lay-rule-band-test
  ;; Reference lines and shaded bands are ordinary layers: they are
  ;; drawn through `layer->membrane` like every other mark and travel on
  ;; a panel's `:layers`. These tests cover root-scope vs layer-scope,
  ;; facet interaction, color/alpha overrides, and the extent a rule on
  ;; a panel of its own gives its axes.
  (let [ds (tc/dataset {:x [1 2 3 4 5] :y [2 4 3 5 4]})
        marks-of (fn [panel] (mapv :mark (:layers panel)))
        layer-of (fn [panel mark] (first (filter #(= mark (:mark %)) (:layers panel))))]

    (testing "root-scope rule-h is a layer on the panel"
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/lay-point :x :y)
                                               (pj/lay-rule-h {:y-intercept 3})))))]
        (is (= [:point :rule-h] (marks-of panel)))
        (is (= 3 (:y-intercept (layer-of panel :rule-h))))))

    (testing "a rule written before a mark is drawn under it"
      ;; Draw order is layer order, which is what makes a rule a
      ;; background reference rather than something drawn over the data.
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/lay-rule-h {:y-intercept 3})
                                               (pj/lay-point :x :y)))))]
        (is (= [:rule-h :point] (marks-of panel)))))

    (testing "root-scope rule applies to every facet panel"
      (let [iris (tc/dataset "https://vincentarelbundock.github.io/Rdatasets/csv/datasets/iris.csv"
                             {:key-fn keyword})
            p (pj/plan (-> iris
                           (pj/lay-point :Sepal.Length :Sepal.Width)
                           (pj/facet :Species)
                           (pj/lay-rule-h {:y-intercept 3})))]
        (is (= 3 (count (:panels p))))
        (doseq [panel (:panels p)]
          (is (= 1 (count (filter #(= :rule-h (:mark %)) (:layers panel))))
              (str "panel " (:row panel) "/" (:col panel) " had "
                   (count (filter #(= :rule-h (:mark %)) (:layers panel))) " rules")))))

    (testing "pj/lay-rule-v with :color and :alpha flows into the plan"
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/lay-point :x :y)
                                               (pj/lay-rule-v {:x-intercept 2 :color "red" :alpha 0.5})))))
            l (layer-of panel :rule-v)]
        (is (= 2 (:x-intercept l)))
        (is (= [1.0 0.0 0.0 1.0] (:color l)))
        ;; The opacity is the layer's style, as it is for every mark,
        ;; and a rule honours it rather than always drawing solid.
        (is (= 0.5 (:opacity (:style l))))))

    (testing "pj/lay-band-h / pj/lay-band-v carry their min/max bounds"
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/lay-point :x :y)
                                               (pj/lay-band-h {:y-min 2 :y-max 4})
                                               (pj/lay-band-v {:x-min 1 :x-max 3})))))
            band-h (layer-of panel :band-h)
            band-v (layer-of panel :band-v)]
        (is (= 2 (:y-min band-h)))
        (is (= 4 (:y-max band-h)))
        (is (= 1 (:x-min band-v)))
        (is (= 3 (:x-max band-v)))))

    (testing "positioned rule with root-scope data layer renders both"
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/pose :x :y)
                                               pj/lay-point
                                               (pj/lay-rule-h :x :y {:y-intercept 3})))))]
        (is (= [:point :rule-h] (marks-of panel)))))

    (testing "plan with a rule and a band validates against schema"
      (let [p (pj/plan (-> ds
                           (pj/lay-point :x :y)
                           (pj/lay-rule-h {:y-intercept 3 :color "red" :alpha 0.5})
                           (pj/lay-band-v {:x-min 1 :x-max 3 :color "blue" :alpha 0.2})))]
        (is (pj/valid-plan? p))))

    (testing "rendered SVG uses :color override for rule"
      (let [sk-red (-> ds
                       (pj/lay-point :x :y)
                       (pj/lay-rule-h {:y-intercept 3 :color "red"}))
            sk-default (-> ds
                           (pj/lay-point :x :y)
                           (pj/lay-rule-h {:y-intercept 3}))
            svg-red (str (pj/plan->plot (pj/plan sk-red) :svg {}))
            svg-default (str (pj/plan->plot (pj/plan sk-default) :svg {}))]
        (is (clojure.string/includes? svg-red "rgb(255,0,0)"))
        (is (not (clojure.string/includes? svg-default "rgb(255,0,0)")))))

    (testing "a rule draws at the width and opacity it was given"
      ;; `:size` names the width, as it does on a line; `:alpha` was
      ;; accepted on these layers and drawn by nothing, so every rule
      ;; came out fully opaque.
      (let [svg (pr-str (pj/plot (-> ds
                                     (pj/lay-point :x :y)
                                     (pj/lay-rule-h {:y-intercept 3 :size 5
                                                     :alpha 0.25 :color "#cc0000"}))))
            drawn (re-find #"204,0,0.{0,60}" svg)]
        (is (clojure.string/includes? drawn ":stroke-width 5"))
        (is (clojure.string/includes? drawn ":stroke-opacity 0.25"))))

    (testing "a rule outside everything the data reaches widens the axis"
      ;; A reference line asked for and then clipped away is a picture
      ;; that answers no question. The rule reports its own extent, so
      ;; the axis reaches it.
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/lay-point :x :y)
                                               (pj/lay-rule-h {:y-intercept 100})))))
            [y-lo y-hi] (:y-domain panel)]
        (is (<= y-lo 2))
        (is (>= y-hi 100))))

    (testing "a rule under polar coordinates is reported"
      ;; These four were skipped without a word under `(pj/coord
      ;; :polar)`, so a plot asked for a reference line and got one
      ;; without it. They travel among the layers now, so the same
      ;; check every other unsupported mark meets applies to them.
      (doseq [[layer-fn opts] [[pj/lay-rule-h {:y-intercept 2}]
                               [pj/lay-rule-v {:x-intercept 2}]
                               [pj/lay-band-h {:y-min 1 :y-max 2}]
                               [pj/lay-band-v {:x-min 1 :x-max 2}]]]
        (is (thrown-with-msg?
             clojure.lang.ExceptionInfo #"not supported with polar coordinates"
             (pj/plan (-> ds (pj/lay-point :x :y) (pj/coord :polar)
                          (layer-fn opts)))))))

    (testing "a date rule outside the data carries the ticks out with it"
      ;; The axis reaches the rule either way -- the domain is widened
      ;; by the test above. What a temporal axis does not read off the
      ;; domain is its ticks: they are picked over the extent the data
      ;; covers, so a rule at December on two months of data left every
      ;; label in January and February and five sixths of the axis
      ;; bare. A numeric axis never had the gap, because numeric ticks
      ;; are picked over the domain the rule already widened.
      (let [d1 (java.time.LocalDate/parse "2024-01-01")
            d2 (java.time.LocalDate/parse "2024-03-01")
            far (java.time.LocalDate/parse "2024-12-01")
            dated {:d [d1 d2] :y [1.0 2.0]}
            ticks-of (fn [pose]
                       (let [vs (-> pose pj/plan :panels first :x-ticks :values)]
                         (mapv #(str (resolve/epoch-ms->local-date-time %)) vs)))
            plain (ticks-of (-> dated (pj/lay-point :d :y)))
            ruled (ticks-of (-> dated
                                (pj/lay-point :d :y)
                                (pj/lay-rule-v {:x-intercept far})))]
        ;; Without the rule the ticks stop inside the data, as before.
        (is (= "2024-02-26T00:00" (last plain)))
        ;; With it they run out to the rule rather than stopping short.
        (is (>= (compare (last ruled) "2024-11-01T00:00") 0)
            (str "last tick was " (last ruled)))
        (is (= "2024-01-01T00:00" (first ruled)))))

    (testing "a pose carrying only a rule still produces a panel"
      ;; The pose's mapping gives the axes their extent; the rule adds
      ;; its own value to the axis it names.
      (let [p (pj/plan (-> ds
                           (pj/pose :x :y)
                           (pj/lay-rule-h :x :y {:y-intercept 3})))
            panel (first (:panels p))]
        (is (= 1 (count (:panels p))))
        (is (= [:rule-h] (marks-of panel)))
        (let [[y-lo y-hi] (:y-domain panel)]
          (is (<= y-lo 2))
          (is (>= y-hi 5)))))

    (testing "a pose carrying only a band extends the domain to include it"
      (let [panel (first (:panels (pj/plan (-> ds
                                               (pj/pose :x :y)
                                               (pj/lay-band-h :x :y {:y-min 10 :y-max 20})))))
            [y-lo y-hi] (:y-domain panel)]
        (is (<= y-lo 2))
        (is (>= y-hi 20))))

    (testing "a rule on a pose with no data at all is still drawable"
      (let [panel (first (:panels (pj/plan (-> (pj/pose)
                                               (pj/lay-rule-h {:y-intercept 3})))))]
        (is (= [:rule-h] (marks-of panel)))
        ;; The rule spans x and no column names it, so the rule reports
        ;; no x extent and the panel's own `[0 1]` fallback stands --
        ;; the same one every axis nothing informs gets, and unpadded,
        ;; because there is no data extent to pad. Reporting `[0 1]`
        ;; from the rule instead put a floor and a ceiling into the
        ;; domain merge, which flattened any layer whose values stay
        ;; under 1: see "a rule does not give the axis it spans an
        ;; extent" below.
        (is (= [0.0 1.0] (mapv double (:x-domain panel))))))

    (testing "a rule does not give the axis it spans an extent"
      (let [dens (pj/lay-density ds :x)
            alone (:y-domain (first (:panels (pj/plan dens))))
            with-rule (:y-domain (first (:panels (pj/plan (pj/lay-rule-v dens {:x-intercept 2})))))]
        ;; A density's y stays well under 1, so a `[0 1]` from the rule
        ;; used to win the merge and draw the density along the bottom.
        (is (< (second alone) 1.0))
        (is (= alone with-rule))))))

(deftest one-column-on-both-axes-test
  ;; `prepare-points` read "y names the column x names" as "this layer
  ;; has no y", which is what a histogram or a rug means, and
  ;; synthesized a zero for every y. A y = x reference diagonal was
  ;; drawn flat along the axis, with nothing said. Shipped in v0.14.0
  ;; and earlier.
  (let [ds {:a [1 2 3 4] :b [10 20 30 40]}
        ys-of (fn [pose]
                (->> (pj/plan pose) :panels first :layers
                     (mapcat :groups) (mapcat :ys) (mapv double)))]
    (testing "the values drawn are the column's own, not zeros"
      (is (= [1.0 2.0 3.0 4.0] (ys-of (pj/lay-point ds :a :a))))
      (is (= [10.0 20.0 30.0 40.0] (ys-of (pj/lay-point ds :b :b)))))

    (testing "the domain covers those values"
      (let [panel (first (:panels (pj/plan (pj/lay-point ds :a :a))))]
        (is (= [0.85 4.15] (mapv double (:y-domain panel))))))

    (testing "every mark that reads a y draws it"
      (doseq [lay [pj/lay-point pj/lay-line pj/lay-area]]
        (is (= [1.0 2.0 3.0 4.0] (ys-of (lay ds :a :a)))
            (str "drawn by " lay))))

    (testing "a layer with no y of its own still has one synthesized"
      ;; The other half of the same flag, which is what it is for.
      (is (= 1 (count (:panels (pj/plan (pj/lay-rug ds :a)))))))))

(deftest arrange-column-cells-test
  (let [d {:a [1 2 3] :b [4 5 6] :c [7 8 9] :d [1 3 2]}
        summary (fn [fr] (select-keys (pj/svg-summary fr) [:panels :points]))]
    (testing "univariate columns"
      (is (= 3 (:panels (summary (-> (pj/arrange d [:a :b :c]) (pj/lay-histogram)))))))
    (testing "bivariate pairs"
      (is (= {:panels 2 :points 6}
             (summary (-> (pj/arrange d [[:a :b] [:c :d]]) (pj/lay-point))))))
    (testing "a pose supplies data and layers"
      (is (= {:panels 2 :points 6}
             (summary (pj/arrange (pj/lay-point (pj/pose d)) [[:a :b] [:c :d]])))))
    (testing "a composite pose has panels appended"
      (is (= 4 (:panels (summary (pj/arrange (pj/arrange d [[:a :b] [:c :d]])
                                             [[:a :c] [:b :d]]))))))
    (testing "mixed forms"
      (is (= 3 (:panels (summary (-> (pj/arrange d [[(pj/pose d) :a] [:b :c] [[:a :d]]])
                                     (pj/lay-histogram)))))))))

(deftest tile-grouped-fill-follows-its-row-test
  ;; The fill values were read in row order and the tile bounds in group
  ;; order, so a grouped tile painted each group's cells with another
  ;; row's value. A dropped row shifted every value after it.
  (let [cells (fn [pose]
                (->> (pj/plan pose) :panels first :layers first :tiles
                     (sort-by :x-lo)
                     (mapv #(second (:color %)))))
        data {:x [1 2 3 4] :y [1 1 1 1] :f [0 1 2 3] :g ["b" "a" "b" "a"]}
        plain (cells (pj/lay-tile data :x :y {:fill :f}))]
    (is (apply < plain) "brightness follows :f from left to right")
    (is (= plain (cells (pj/lay-tile data :x :y {:fill :f :group :g}))))
    (is (= plain (cells (pj/lay-tile data :x :y {:fill :f :color :g}))))
    (testing "a missing fill drops its row without shifting the others"
      (let [[lo hi] (cells (pj/lay-tile {:x [1 2 3] :y [1 1 1] :f [1 nil 3]} :x :y {:fill :f}))]
        (is (< lo hi))))))

(deftest tile-on-a-log-axis-test
  ;; The half-step was taken before the transform, so x = 1, 10, 100
  ;; reached -3.5 and the log scale refused the plot. On a log axis a
  ;; tile spans the same factor either side of its value.
  (let [plan (pj/plan (-> (pj/lay-tile {:x [1 10 100] :y [1 1 1] :f [1 2 3]} :x :y {:fill :f})
                          (pj/scale :x :log)))
        tiles (-> plan :panels first :layers first :tiles)
        ratios (map #(/ (:x-hi %) (:x-lo %)) tiles)]
    (is (every? #(< (Math/abs (- % 10.0)) 1e-9) ratios))
    (is (pos? (first (-> plan :panels first :x-domain))))))

(deftest bin2d-legend-is-labelled-in-whole-counts-test
  ;; A 2D histogram's fill has no column for the whole-number check to
  ;; read, and its legend was labelled 0.0, 0.5, 1.0, 1.5, 2.0.
  (is (= ["0" "1" "2"]
         (->> (pj/plan (pj/lay-tile {:x [1 1 2 3] :y [1 1 2 3]} :x :y))
              :legend :ticks (map :label)))))

(deftest composite-warns-only-where-no-cell-reads-the-option-test
  ;; The composite's options reach every cell, so a plain cell warned
  ;; about a :color-label its coloured neighbour's legend reads.
  (let [colored (pj/lay-point {:a [1 2] :b [1 2] :g ["x" "y"]} :a :b {:color :g})
        plain (pj/lay-point {:a [1 2] :b [1 2]} :a :b)
        out (fn [cells] (with-out-str
                          (pj/plan (pj/options (pj/arrange cells) {:color-label "GT"}))))]
    (is (not (str/includes? (out [colored plain]) ":color-label")))
    (is (= 1 (count (re-seq #":color-label" (out [plain plain])))))))

(deftest categorical-tile-colour-does-not-read-fill-settings-test
  ;; A categorical :color on a tile draws palette colours, as on any
  ;; mark: :color-label titles it, and a fill setting, which shapes a
  ;; gradient, is unread and warns. A numeric :color is the fill, and
  ;; reads them.
  (let [data {:x [1 2] :y [1 1] :g ["a" "b"] :z [1.0 2.0]}
        run (fn [color opts]
              (let [out (java.io.StringWriter.)
                    plan (binding [*out* out]
                           (pj/plan (pj/options (pj/lay-tile data :x :y {:color color}) opts)))]
                [(-> plan :legend :title) (str out)]))]
    (let [[title out] (run :g {:fill-label "F"})]
      (is (= :g title))
      (is (str/includes? out ":fill-label")))
    (is (= "C" (first (run :g {:color-label "C"}))))
    (let [[title out] (run :z {:fill-label "F"})]
      (is (= "F" title))
      (is (not (str/includes? out ":fill-label"))))))

(deftest tile-of-whole-numbers-is-ticked-at-whole-numbers-test
  ;; The half-step that draws the outer tiles whole widened the extent
  ;; the wholeness rule reads, so a grid at 1 and 2 was ticked 0.4, 0.6,
  ;; ... 2.6. The half-step is padding for that rule.
  (let [panel (-> (pj/lay-tile {:x [1 2 3 1 2 3] :y [1 1 1 2 2 2] :f [1 2 3 4 5 6]} :x :y {:fill :f})
                  pj/plan :panels first)]
    (is (= [1.0 2.0 3.0] (mapv double (-> panel :x-ticks :values))))
    (is (= [1.0 2.0] (mapv double (-> panel :y-ticks :values))))
    (is (= [0.35 3.65] (:x-domain panel)) "the domain still reaches the tile edges")))

(deftest tile-color-beside-fill-is-not-drawn-test
  ;; With both, the cells are painted from :fill. The :color used to
  ;; build a legend of palette colours no cell used, in place of the
  ;; fill's gradient legend, with no message.
  (let [d {:x [1 2] :y [1 1] :f [0 1] :g ["a" "b"]}
        run (fn [opts] (let [out (java.io.StringWriter.)
                             plan (binding [*out* out] (pj/plan (pj/lay-tile d :x :y opts)))]
                         [(select-keys (:legend plan) [:title :type]) (str out)]))]
    (doseq [color [:g "white"]]
      (let [[legend out] (run {:fill :f :color color})]
        (is (= {:title :f :type :continuous} legend))
        (is (= 1 (count (re-seq #"not drawn on a tile" out))))))))

(deftest tile-written-color-warns-test
  ;; A written colour on a tile with no :fill was dropped with no
  ;; message; the tile binned as it does with neither.
  (let [out (with-out-str (pj/plan (pj/lay-tile {:x [1 2 3] :y [1 2 3]} :x :y {:color "red"})))]
    (is (= 1 (count (re-seq #"a written colour is not drawn on a tile" out))))))

(deftest density-2d-color-is-not-drawn-test
  ;; One density is computed from all rows, so a :color changed nothing
  ;; on the panel, yet it replaced the density legend with one of
  ;; palette colours nothing used.
  (let [d {:x [1 2 3 4 5 6 2 3] :y [2 1 4 3 6 5 3 2] :g ["a" "b" "a" "b" "a" "b" "a" "b"]}]
    (doseq [lay [pj/lay-density-2d pj/lay-contour]]
      (let [out (java.io.StringWriter.)
            plan (binding [*out* out] (pj/plan (lay d :x :y {:color :g})))]
        (is (= :relative-density (-> plan :legend :title)))
        (is (= 1 (count (re-seq #"is not drawn: one density" (str out)))))))))

(deftest contour-lines-read-the-legend-settings-test
  ;; The lines were coloured by each level's share of the densest cell
  ;; and read only the range, so a midpoint, a domain or a log type
  ;; moved the legend bar and left the lines as they were.
  (let [d {:x [1 2 3 4 5 6 2 3 4 5] :y [2 1 4 3 6 5 3 2 4 4]}
        colors (fn [pose] (->> (pj/plan pose) :panels first :layers first :levels (mapv :color)))
        plain (colors (pj/lay-contour d :x :y))]
    (is (seq plain))
    (is (not= plain (colors (pj/options (pj/lay-contour d :x :y)
                                        {:color-midpoint 0.01}))))
    (is (not= plain (colors (-> (pj/lay-contour d :x :y)
                                (pj/scale :color {:domain [0 100]})))))))

(deftest colour-names-read-everywhere-test
  ;; `:point-stroke` and a gradient's stops read colours directly, so a
  ;; CSS name was taken for hex digits and failed.
  (let [d {:x [1 2 3] :y [1 2 3] :n [1.0 2.0 3.0]}]
    (is (vector? (pj/plot (pj/options (pj/lay-point d :x :y) {:config {:point-stroke "black" :point-stroke-width 1}}))))
    (is (vector? (pj/plot (pj/options (pj/lay-point d :x :y {:color :n}) {:color-range {:low "red" :high "blue"}}))))))

(deftest area-stroke-is-a-written-colour-test
  ;; A column name passed `pj/plan` and crashed at `pj/plot` naming nothing.
  (let [d {:x [1 2 3] :y [1 2 3] :g ["a" "b" "a"]}]
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"one written colour"
                          (pj/plan (pj/lay-area d :x :y {:stroke :g}))))
    (is (vector? (pj/plot (pj/lay-area d :x :y {:stroke :red}))))))

(deftest date-column-on-color-reports-test
  (let [d {:x [1 2] :y [1 2] :t [(java.time.LocalDate/of 2020 1 1) (java.time.LocalDate/of 2021 1 1)]}]
    (doseq [opts [{:color :t} {:color :t :color-type :categorical}]]
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"does not read dates yet"
                            (pj/plan (pj/lay-point d :x :y opts)))))))

(deftest unread-colour-gradient-and-palette-settings-warn-test
  (let [d {:x [1 2 3] :y [1 2 3] :g ["a" "b" "a"] :n [1.0 2.0 3.0]}
        out (fn [pose] (with-out-str (pj/plan pose)))]
    (is (str/includes? (out (pj/options (pj/lay-point d :x :y {:color :g}) {:color-range :viridis})) ":color-range"))
    (is (str/includes? (out (pj/scale (pj/lay-point d :x :y {:color :g}) :color {:range :viridis})) ":range shapes a gradient"))
    (is (str/includes? (out (pj/scale (pj/lay-point d :x :y {:color :n}) :color {:values ["red" "blue"]})) ":values is the palette"))
    (testing "read settings do not warn"
      (is (not (str/includes? (out (pj/options (pj/lay-point d :x :y {:color :n}) {:color-range :viridis})) "Warning")))
      (is (not (str/includes? (out (pj/options (pj/lay-tile {:x [1 2] :y [1 1] :f [1 2]} :x :y {:fill :f}) {:color-range :viridis})) "Warning"))))))

(deftest scale-fill-warns-once-and-truly-test
  ;; `pj/scale :fill` on a point warned twice, and on a 2D density, which
  ;; reads it, warned falsely.
  (let [out (with-out-str (pj/plan (pj/scale (pj/lay-point {:x [1 2] :y [1 2]} :x :y) :fill {:label "F"})))]
    (is (= 1 (count (re-seq #"Warning" out))))
    (is (str/includes? out "did you mean :color")))
  (is (not (str/includes? (with-out-str (pj/plan (pj/scale (pj/lay-density-2d {:x [1 2 3 4 2 3] :y [2 1 4 3 3 2]} :x :y) :fill {:label "F"})))
                          "Warning"))))

;; ---- Three messages found on Zulip after 0.16.0 ----

(deftest shift-given-a-column-names-the-option-test
  ;; A column on :dx failed as a raw ClassCastException naming Keyword
  ;; and Number, and neither the option nor the layer.
  (is (thrown-with-msg? clojure.lang.ExceptionInfo
                        #"lay-point :dx must be a number in the units of the :x axis, but got :d"
                        (pj/lay-point {:x [1 2] :y [1 2] :d [0.1 0.2]} :x :y {:dx :d})))
  (is (thrown-with-msg? clojure.lang.ExceptionInfo
                        #"lay-label :dy must be a number in the units of the :y axis"
                        (pj/lay-label {:x [1 2] :y [1 2] :t ["a" "b"] :d [1 2]} :x :y {:text :t :dy :d})))
  (is (= 2 (:points (pj/svg-summary (pj/lay-point {:x [1 2] :y [1 2]} :x :y {:dx 0.5}))))))

(deftest column-read-by-a-series-is-named-as-such-test
  ;; The column was pivoted away by the series, so the old message's
  ;; advice -- pj/overlay -- could not bring it back.
  (let [data {:x [1 2 3] :a [1 2 3] :b [2 3 4] :c [3 4 5]}
        msg (fn [f] (try (f) nil (catch clojure.lang.ExceptionInfo e (ex-message e))))
        series-msg (msg #(-> data (pj/lay-line :x [:a :b :c]) (pj/lay-point :x :c)))
        plain-msg (msg #(-> data (pj/lay-line :x :a) (pj/lay-point :x :zz)))]
    (is (re-find #"a series on this pose has already read" series-msg))
    (is (re-find #"\{:data data\}" series-msg))
    (is (not (re-find #"pj/overlay" series-msg)))
    (is (re-find #"doesn't exist in the data" plain-msg))
    ;; The way out the message names draws.
    (is (= {:panels 1 :lines 3 :points 3}
           (select-keys (pj/svg-summary (-> data (pj/lay-line :x [:a :b :c])
                                            (pj/lay-point :x :c {:data data :overlay true})))
                        [:panels :lines :points])))))

(deftest rule-or-band-given-a-value-before-its-options-test
  ;; (pj/lay-rule-v pose 7 {:x-intercept 2}) read 7 as a written value:
  ;; the x axis stretched to 7 with no warning, and the rule drew at 2.
  (let [pose (pj/lay-point {:x [1 2 3] :y [1 2 3]} :x :y)]
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"lay-rule-v was given 7 before its options map.*\{:x-intercept \.\.\.\}"
                          (pj/lay-rule-v pose 7 {:x-intercept 2})))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"lay-band-h was given 1 before its options map"
                          (pj/lay-band-h pose 1 2 {:y-min 1 :y-max 2})))
    ;; Columns there still pick the panel, a number naming a column
    ;; included, and a pose with no data yet cannot be checked.
    (is (= 1 (:panels (pj/svg-summary (pj/lay-rule-v pose :x :y {:x-intercept 2})))))
    (is (= 1 (:panels (pj/svg-summary (-> (tc/dataset {0 [1 2 3] 1 [1 2 3]})
                                          (pj/lay-point 0 1)
                                          (pj/lay-rule-v 0 1 {:x-intercept 2}))))))
    (is (some? (pj/lay-rule-v (pj/pose) 7 {:x-intercept 2})))))

(deftest arrange-nil-is-an-empty-cell-test
  ;; nil was refused with advice to drop the cell, which in a grid moves
  ;; every later cell along. It is a cell left empty: it keeps its slot
  ;; and draws nothing, also when the composite carries data and layers.
  (let [iris (rdatasets/datasets-iris)
        slots (fn [v] (mapv first (vals (into (sorted-map) (-> v pj/plan :chrome :layout)))))
        poses (pj/arrange [(pj/lay-point iris :sepal-length :sepal-width) nil
                           (pj/lay-point iris :petal-length :petal-width)] {:cols 3})
        data-first (-> iris
                       (pj/arrange [{:x :sepal-length :y :sepal-width} nil
                                    {:x :petal-length :y :petal-width}] {:cols 3})
                       pj/lay-point)
        coloured (pj/arrange [(pj/lay-point iris :sepal-length :sepal-width {:color :species}) nil
                              (pj/lay-point iris :petal-length :petal-width {:color :species})])]
    (is (= [0.0 200.0 400.0] (slots poses)))
    (is (= {:panels 2 :points 300} (select-keys (pj/svg-summary poses) [:panels :points])))
    (is (= {:panels 2 :points 300} (select-keys (pj/svg-summary data-first) [:panels :points])))
    ;; An empty cell neither agrees nor disagrees about a legend.
    (is (= #{:color} (-> coloured pj/plan :chrome :shared-aesthetics)))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"every one is nil"
                          (pj/arrange [nil nil])))))

(deftest series-pairs-count-message-test
  (is (thrown-with-msg? clojure.lang.ExceptionInfo
                        #"was given 1 column on :x, \[:t\], and 2 on :y"
                        (pj/lay-line {:t [1 2] :a [1 2] :b [2 3]} [:t] [:a :b]))))
